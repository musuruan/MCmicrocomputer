10 REM *CAMPO ELETTRICO*
20 REM *CARICA LA ROUTINE DI SCREENCOPY*
30 PRINT"(CLR)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)UN ATTIMO PREGO..."
40 FORK=5120TO5449:READA:POKEK,A:NEXT
50 REM *SCHERMATA INIZIALE E NOTE*
60 CLR:PRINT"(CLR)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(CBM-A)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(CBM-S)"
70 FORI=1TO11:PRINT"   (SHIFT-B)                                (SHIFT-B)":NEXTI
80 PRINT"(RGHT)(RGHT)(RGHT)(CBM-Z)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(SHIFT-C)(CBM-X)"
90 PRINT"(UP)(UP)(UP)(UP)(UP)(UP)(UP)(UP)(UP)(UP)(UP)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-N)(CBM-Y)(CBM-Y)(CBM-N)(CBM-Y)(SHIFT-P)(CBM-N)(SHIFT-M)(SHIFT-N)(CBM-H)(SHIFT-O)(CBM-Y)(CBM-H)(SHIFT-O)(CBM-Y)(CBM-H)"
100 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-N)  (CBM-N)(CBM-Y)(SHIFT-P)(CBM-N)  (CBM-H)(SHIFT-O)(CBM-Y) (CBM-H) (CBM-H)"
110 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-N)(CBM-P)(CBM-P)(CBM-N) (CBM-N)(CBM-N)  (CBM-H)(CBM-H)  (SHIFT-L)(CBM-P)(CBM-H)"
120 PRINT"(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-N)(CBM-Y)(CBM-Y)(CBM-N)  (CBM-N)(CBM-Y)(CBM-Y) (SHIFT-P)(CBM-Y) (SHIFT-P)(CBM-Y)(CBM-N)(CBM-Y)(SHIFT-P)(CBM-N)(CBM-N)(CBM-Y)(CBM-Y)(CBM-N)(CBM-Y)(SHIFT-P)"
130 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-N)(CBM-Y) (CBM-N)  (CBM-N)(CBM-Y)  (CBM-N)  (CBM-N) (CBM-N)(SHIFT-M)(CBM-Y)(CBM-N)(CBM-N)  (CBM-N) (CBM-N)"
140 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-N)(CBM-P)(CBM-P)(CBM-N)(CBM-P)(CBM-P)(CBM-N)(CBM-P)(CBM-P) (CBM-N)  (CBM-N) (CBM-N) (SHIFT-M)(CBM-N)(CBM-N)(CBM-P)(CBM-P)(CBM-N)(CBM-P)(SHIFT-@)"
150 PRINT"(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT-O)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(CBM-Y)(SHIFT-P)"
160 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-G) DI VINCENZO CALAMIA (CBM-M)"
170 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT-L)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(CBM-P)(SHIFT-@)"
180 PRINT"(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)PREMI UN TASTO"
190 GETA$:IFA$=""THEN190
200 PRINT"(CLR)(SWLC)(DOWN)(RGHT)(RGHT)IL PROGRAMMA SIMULA IL CAMPO ELETTRICOGENERATO  DA 'N' CARICHE, ";
210 PRINT"ATTRAVERSO  LARAPPRESENTAZIONE CON LE LINEE DI FORZA."
220 PRINT"(DOWN)  E' INOLTRE  POSSIBILE  DETERMINARE  ILC.E. IN UN  PUNTO, IN  MODULO, DIREZIONEE VERSO CON L'OPZIONE 'C'."
230 PRINT"(DOWN)  IL MODULO  DEL C.E. IN UN  PUNTO VIENECALCOLATO A MENO DELLA COSTANTE 'K' CHE,NEL VUOTO, VALE 9*10^9 NM^2/C^2."
240 PRINT"(DOWN)  PER  STAMPARE  LO  SCHERMO  GRAFICO E'DISPONIBILE L'OPZIONE 'P'."
250 PRINT"(DOWN)  GLI ASSI DI RIFERIMENTO HANNO  ORIGINENELL'ANGOLO IN BASSO A SINISTRA."
260 PRINT"(DOWN)  LE  COORDINATE  DELLE  CARICHE  DEVONOESSERE > 10CM  PER  POTER  DISEGNARE  LECARICHE COME UN CERCHIETTO CONTENENTE ILSEGNO."
270 PRINT"(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RVS)PREMI UN TASTO(OFF)";
280 GETA$: IFA$=""THEN280
290 REM * INPUT DATI*
300 CLR:INPUT"(CLR)NUMERO CARICHE ((RVS)N<=15(OFF))";NQ%
310 IFNQ%>0THEN320:ELSEPRINT"(DOWN)(DOWN)(RVS)IL NUMERO DELLE CARICHE  DEVE ESSERE >0!(OFF)":SLEEP4:GOTO300
320 IFNQ%=<10THEN330:ELSEDIMXQ(NQ%),YQ(NQ%),Q(NQ%),OQ(NQ%)
330 SR=1:SC$="*SCALA 1/"
340 FORI=1TONQ%
350 PRINT"(DOWN)CARICA"I"(COULOMB)=";:INPUTQ(I)
360 PRINT"(SWLC)COORDINATE CARICA"I":((RVS)X>10CM(OFF))=";:INPUTXQ(I)
370 IFXQ(I)>10THEN380:ELSEPRINT"(UP)":GOTO360
380 INPUT"    ''       ''      ((RVS)Y>10CM(OFF))=";YQ(I):OQ(I)=YQ(I):IFYQ(I)<10THEN380
390 IFYQ(I)>10THEN400:ELSEPRINT"(UP)":GOTO380
400 PRINT"(DOWN)I DATI SONO CORRETTI?  [S/N]"
410 GETKEYA$:IFA$="S"THEN430
420 IFA$="N"THEN350:ELSEGOTO410
430 NEXTI
440 PRINT"(CLR)VUOI ATTIVARE IL MODO 'FAST'?    [S/N] "
450 GETKEYA$:IFA$="N"THEN480
460 IFA$<>"S"THEN450
470 FAST
480 POKE808,100:REM *DISABILITA RUN/STOP*
490 REM *INIZIO CALCOLI*
500 FORI=1TONQ%:IFXQ(I)<=310ANDYQ(I)<=190THEN540
510 SX=INT(XQ(I)/310)+1:SY=INT(YQ(I)/190)+1
520 IFSX>SYANDSX>SRTHENSR=SX
530 IFSY>SRTHENSR=SY
540 NEXTI
550 SW=16*SR:SW$=STR$(SW):PW=24*SR:PW$="       1/"+STR$(PW)
560 FORI=1TONQ%:YQ(I)=200*SR-YQ(I):NEXT
570 GOSUB580:GOTO670
580 GRAPHIC1,1:SCALE1,320*SR,200*SR
590 CHAR1,0,0," **LINEE DI FORZA DEL CAMPO ELETTRICO** ",1
600 SW$=SC$+SW$+"*":CHAR1,0,1,SW$,0
610 GOSUB620:GOTO670
620 FORI=1TONQ%:CIRCLE1,XQ(I),YQ(I),3*SR,3*SR
630 DRAW1,(XQ(I)-3*SR),YQ(I)TO(XQ(I)+3*SR),YQ(I):IFQ(I)<0THEN650
640 DRAW1,XQ(I),(YQ(I)-3*SR)TOXQ(I),(YQ(I)+3*SR)
650 NEXTI
660 RETURN
670 IFNQ%>4THEN760
680 IFNQ%=4THEN740
690 IFNQ%=2THEN710
700 NL%=11-NQ%:GOTO770
710 IF(Q(1)*Q(2))>0THEN730
720 GOTO700
730 NL%=10:GOTO770
740 FORW=1TONQ%:IF(Q(1)*Q(W))<0THEN700
750 NEXTW:NL%=6:GOTO770
760 NL%=5
770 FORI=1TONQ%:FORK=0TONL%-1
780 XP=XQ(I)+10*SR*(COS(K*2*~/NL%)):YP=YQ(I)+10*SR*(SIN(K*2*~/NL%))
790 XA=XP:YA=YP
800 GOSUB890
810 IFF1=1THEN870
820 XB=XA+5*SR*TX/TT:YB=YA+5*SR*TY/TT:TX=0:TY=0:DRAW1,XA,YA
830 XC=ABS(XD-XB):YC=ABS(YD-YB):IFXC<3*SRANDYC<3*SRTHEN880
840 XD=XA:XA=XB:IFXA<10*SRORXA>310*SRGOTO870
850 YD=YA:YA=YB:IFYA<15*SRORYA>195*SRGOTO870
860 GOTO800
870 F1=0
880 NEXTK:NEXTI
890 FORJ=1TONQ%:A=(XA-XQ(J)):B=(YA-YQ(J)):D2=(A^2+B^2):D=SQR(D2)
900 IFD>8*SRGOTO920
910 F1=1:GOTO980
920 E=Q(J)/D2:TX=TX+E*A/D:TY=TY+E*B/D:TT=SQR(TX^2+TY^2)
930 IFQ(I)>0THEN950
940 TT=-TT
950 NEXTJ
960 IFTT<>0THEN980
970 F1=1
980 IFI>NQ%ANDK>0THEN1000
990 RETURN
1000 SLOW:CHAR1,0,24,"(SWLC)A=ALTRO ESEMPIO*C=CALCOLI*P=STAMPA*E=END"
1010 GETA$:IFA$=""THEN1010
1020 IFA$="P"THEN1090
1030 IFA$="E"THEN1080
1040 IFA$="A"THEN1070
1050 IFA$="C"THEN1290
1060 GOTO1010
1070 GRAPHIC0:GOTO300
1080 GRAPHIC0:CLR:PRINTCHR$(142)"(CLR)":POKE808,110:END
1090 IFSTTHEN1100:ELSEGOTO1120
1100 CHAR1,0,24,"(SWLC)*ACCENDI LA STAMPANTE  E PREMI UN TASTO*"
1110 GETA$:IFA$=""THEN1110
1120 CHAR1,0,24,"                                        "
1130 CHAR1,0,1,"                                        "
1140 OPEN4,4:PRINT#4,CHR$(27);CHR$(108);CHR$(13);:CLOSE4:IFSTTHEN1100
1150 FAST:SYS5120:SLOW
1160 CHAR1,0,1,SW$,0:OPEN4,4
1170 PRINT#4,CHR$(17);" CARICA N.  MODULO [C]  X [CM]   Y [CM]  SCALA GRAFICO"
1180 PRINT#4,"(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T) (CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)"
1190 FORI=1TONQ%:PRINT#4,USING"#####";I;
1200 PRINT#4,USING"##########.##";Q(I);
1210 PRINT#4,USING"#########.##";XQ(I);
1220 PRINT#4,USING"######.##";OQ(I);
1230 IFI=1THENPRINT#4,PW$;
1240 PRINT#4,"":NEXTI:CLOSE4
1250 CHAR1,0,1,SC$,0:GOTO1000
1260 GRAPHIC0:PRINT"(CLR)PUNTO TROPPO VICINO AD UNA CARICA!!!!!!!"
1270 PRINT" PREMI UN TASTO E RIBATTI LE COORDINATE "
1280 GETA$:IFA$=""THEN1280
1290 GRAPHIC0:PRINT"(CLR)(SWLC)(RVS)CALCOLO  DEL CAMPO ELETTRICO IN UN PUNTO(OFF)"
1300 DB=20*SR:DB$=STR$(DB):RN$="((RVS)":RF$="CM(OFF))":WX$=RN$+"X>"+DB$+RF$:WY$=RN$+"Y>"+DB$+RF$
1310 PRINT"(DOWN)(DOWN)(DOWN)(DOWN)COORDINATE PUNTO"WX$;:INPUTXP:XD=XP
1320 PRINT"    ''      ''  "WY$;:INPUTYP:YD=200*SR-YP
1330 GRAPHIC1,0:CHAR1,0,1,V2$,0:CHAR1,0,0,"**VETTORE CAMPO ELETTRICO  IN UN PUNTO**",1:SCALE1,320*SR,200*SR
1340 TX=0:TY=0
1350 FORJ=1TONQ%:A=(XD-XQ(J)):B=(YD-YQ(J)):D2=A^2+B^2:D=SQR(D2)
1360 IFD<4*SRTHEN1260
1370 E=Q(J)/D2:TX=TX+E*A/D:TY=TY+E*B/D:TT=SQR(TX^2+TY^2):NEXTJ
1380 IFTT=0THEN1500
1390 XX=3*SR*TX/TT:YY=3*SR*TY/TT:XF=XD+4*SQR(2)*XX:YF=YD+4*SQR(2)*YY
1400 XG=XF-XX+YY:YG=YF-YY-XX:XH=XF-XX-YY:YH=YF-YY+XX
1410 IFXD<5*SRORXD>315*SRTHEN1500
1420 IFXF<5*SRORXF>315*SRTHEN1500
1430 IFXG<5*SRORXG>315*SRTHEN1500
1440 IFXH<5*SRORXH>315*SRTHEN1500
1450 IFYD<5*SRORYD>190*SRTHEN1500
1460 IFYF<5*SRORYF>190*SRTHEN1500
1470 IFYG<5*SRORYG>190*SRTHEN1500
1480 IFYH<5*SRORYH>190*SRTHEN1500
1490 DRAW1,XD,YDTOXF,YFTOXG,YG:DRAW1,XF,YFTOXH,YH
1500 TT=TT*10^4:TT=SGN(TT)*INT((ABS(TT)*10^4)+.5)/10^4:TT$=STR$(TT):XP$=STR$(XP):YP$=STR$(YP)
1510 M1$="(SWLC)IN P(":V$=",":M2$=") E=":M3$="K [V/M]":V1$="          ":V2$=V1$+V1$+V1$+V1$
1520 MM$=M1$+XP$+V$+YP$+M2$+TT$+M3$
1530 CHAR1,0,1,MM$,0
1540 SLEEP5:GOTO1000
1550 DATA 76,217,20,32,33,20,169,96,162,4,160,0
1560 DATA 32,186,255,169,0,32,189,255,32,192,255,162
1570 DATA 96,76,201,255,169,96,32,204,255,169,96,76
1580 DATA 195,255,169,15,32,210,255,169,13,32,210,255
1590 DATA 76,28,20,169,0,141,73,21,173,75,21,240
1600 DATA 16,201,1,240,5,238,73,21,24,96,173,74
1610 DATA 21,201,64,176,244,173,77,21,208,239,173,76
1620 DATA 21,201,200,176,232,173,76,21,41,7,133,251
1630 DATA 173,76,21,74,74,41,254,168,185,135,20,56
1640 DATA 229,251,133,251,185,136,20,24,109,75,21,133
1650 DATA 252,173,74,21,41,248,168,173,74,21,41,7
1660 DATA 170,24,96,7,62,199,60,135,59,71,58,7
1670 DATA 57,199,55,135,54,71,53,7,52,199,50,135
1680 DATA 49,71,48,7,47,199,45,135,44,71,43,7
1690 DATA 42,199,40,135,39,71,38,7,37,199,35,135
1700 DATA 34,71,33,7,32,234,32,0,32,32,139,32
1710 DATA 51,20,173,73,21,208,8,189,209,20,49,251
1720 DATA 56,208,1,24,96,128,64,32,16,8,4,2
1730 DATA 1,32,3,20,169,8,32,210,255,169,199,141
1740 DATA 78,21,169,0,141,74,21,141,75,21,141,77
1750 DATA 21,162,7,134,253,173,78,21,141,76,21,32
1760 DATA 191,20,110,79,21,206,76,21,198,253,208,243
1770 DATA 110,79,21,173,79,21,9,128,32,210,255,238
1780 DATA 74,21,208,3,238,75,21,173,75,21,240,209
1790 DATA 173,74,21,201,64,208,202,169,13,32,210,255
1800 DATA 173,78,21,56,233,7,141,78,21,201,252,208
1810 DATA 173,169,15,32,210,255,169,13,32,210,255,76
1820 DATA 28,20,76,38,20,0

