1 REM ***************
2 REM *             *
3 REM *    S K I    *
4 REM *             *
5 REM * G. TORRESAN *
6 REM *             *
7 REM *T. 0422/91240*
8 REM *             *
9 REM ***************
10 PRINT"(CLR)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(BLK)(RVS) S K I (OFF)":PRINT"(DOWN)(DOWN)(YEL) BY GABRIELE TORRESAN"
20 PRINT"(DOWN)(DOWN)(RED)  TASTI DA USARE:":PRINT"(DOWN)(DOWN) (BLU)(RVS)S(OFF) SINISTRA":PRINT"(DOWN) (RVS)D(OFF) DESTRA"
30 PRINT"(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(PUR)LUNGHEZZA DELLA PISTA?"
40 PRINT"      [1-9]
50 GETP$:IFP$<"1"ORP$>"9"THEN50
60 AZ=VAL(P$)*50
70 POKE36879,25:POKE36869,255:POKE650,128:POKE56,28:POKE52,28
80 PRINT"(CLR)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RED)(RVS)(SHIFT-N)(SHIFT-M)(DOWN)(LEFT)(LEFT)(LEFT)(SHIFT-N)(RGHT)(RGHT)(SHIFT-M)(DOWN)(LEFT)(LEFT)(LEFT)(LEFT)(LEFT)(SHIFT-N)(CBM-+)(CBM-+)(CBM-+)(CBM-+)(SHIFT-M)(DOWN)(LEFT)(LEFT)(LEFT)(LEFT)(LEFT)(CBM-+)(RGHT)(RGHT)(CBM-+)(DOWN)(LEFT)(LEFT)(LEFT)(LEFT)(CBM-+)(OFF)(BLU)4(PUR)0(RED)(RVS)(CBM-+)(OFF)(DOWN)(LEFT)(LEFT)(LEFT)(LEFT)(RVS)(CBM-+)(CBM-+)(OFF)(BLU)1(RED)(RVS)(CBM-+)(OFF)"
90 N=3
100 FORI=7552TO7663:READR:POKEI,R:NEXT
110 K=7790:L=7812:M=8164:F=38884:G=38510:H=38532:X=11:M$="%'(%%%%'(%%%"
120 FORI=0TO999:NEXT
130 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)"
140 TI$="000000"
150 FORII=0TOAZ
160 N=N-1
170 IFN=1THEN200
180 IFN=2THEN210
190 POKEM+B,56:POKEM+B+1,59:POKEF+B,5:POKEF+B+1,5
200 POKEM+A,55:POKEM+A+1,58:POKEF+A,5:POKEF+A+1,5:B=A
210 P=INT(RND(1)*21):IFPEEK(M+P)<>32ORPEEK(M+P+1)<>32THEN210
220 POKEM+P,54:POKEM+P+1,57:POKEF+P,5:POKEF+P+1,5:A=P
230 GOSUB610:NEXTII
240 N=0:O=2:S=0
250 FORII=0TOAZ:GOSUB610
260 IFRND(1)<.1THENQ=INT(RND(1)*8)+8:POKEM+Q,209:POKEF+Q,3
270 Q=INT(RND(1)*4):POKEM+Q,193:POKEF+Q,5:Q=INT(RND(1)*4)+18:POKEM+Q,216:POKEF+Q,5
280 N=N+1:IFN=8THENN=0:Q=INT(RND(1)*10)+6:POKEM+Q,60:POKEF+Q,4+O:O=-O
290 IFN<>0THEN320
300 FORI=6TO15:IFPEEK(L+I)=60*((O<0)*(X<I)+(O>0)*(X>I))THENS=S-ABS(X-I)*7:I=15
310 NEXTI
320 S=S+3:NEXTII
330 FORI=1TO10:XX=X:GOSUB610:FORJ=0TO89:NEXTJ,I
340 T$(0)="(RVS)(RED)(CBM-A)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)(OFF)"
350 T$(1)="(RVS)(SHIFT--) T R A G U A R D O (SHIFT--)(OFF)"
360 T$(2)="(RVS)(CBM-Z)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)(OFF)"
370 FORI=0TO2:PRINTT$(I);:XX=X:GOSUB610:FORJ=0TO89:NEXTJ,I
380 Y=0:Z=0
390 FORI=1TO5:PRINT"(BLK)(RVS)(SHIFT--)(OFF)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RVS)(SHIFT--)(OFF)";:XX=X:GOSUB670:FORJ=0TO89:NEXTJ,I
400 FORI=1TO6:XX=X:GOSUB670:FORJ=0TO89:NEXTJ,I
410 POKEK+X,32:POKEL+X,32:POKEG+X,1:POKEH+X,1
420 FORI=1TO6:PRINT:FORJ=0TO89:NEXTJ,I
430 FORI=1TO2
440 XX=X:GOSUB670:FORJ=0TO89:NEXTJ,I
450 POKEK+X,61:POKEL+X,53
460 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RVS)(BLU)PUNTEGGIO:"S
476 PRINT"(DOWN)(DOWN)(BLK)(RVS)TEMPO:"
480 PRINT"(RVS)"MID$(TI$,4,1)" MINUTI, "RIGHT$(TI$,2)" SECONDI"
490 POKE36877,0:POKE36878,15
500 FORI=1TO12:E=ASC(MID$(M$,I,1))+200
510 POKE36875,0:POKE36875,E:FORJ=0TO159:NEXTJ,I
520 POKE36878,0:POKE36875,0:GOTO570
530 POKE36878,15:POKE36877,180
540 FORI=15TO0STEP-1:POKE36878,I:FORJ=1TO20:NEXTJ,I
550 POKEL+X,150:POKEH+X,2
560 FORI=0TO999:NEXT
570 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(YEL)(RVS)UN'ALTRA DISCESA(Y/N)?(OFF)"
580 GETP$:IFP$="Y"THENPOKE36869,240:POKE56,30:RUN
590 IFP$="N"THENPOKE36869,240:POKE36879,27:PRINT"(CLR)(BLU)";:END
600 GOTO580
610 GETR$:Z=0:W=1:XX=X
620 IFR$=""THEN660
630 IFR$="S"ANDX>0THENXX=XX-1:W=1:IFY<>2THENZ=1:Y=2:GOTO670
640 IFR$="D"ANDX<21THENXX=XX+1:W=1:IFY<>4THENZ=1:Y=4:GOTO670
650 IFW=1THEN670
660 IFY=2ORY=4THENY=0:Z=1
670 POKEK+X,32:POKEL+X,32:POKEG+X,1:POKEH+X,1:PRINT:X=XX
680 IFPEEK(K+X)=209ORPEEK(L+X)=209THENX=X+(INT(RND(1)*7)-3):Z=1
690 IFPEEK(K+X)<>32ORPEEK(L+X)<>32THEN530
700 POKEK+X,Y+48:POKEL+X,Y+49
710 POKEG+X,4:POKEH+X,6:IFZ=0THEN740
720 POKE36878,0:POKE36877,250
730 FORJ=1TO15:POKE36878,J:NEXTJ:FORJ=14TO0STEP-1:POKE36878,J:NEXT
740 RETURN
750 DATA24,24,24,126,189,189,189,189,189,60,36,36,36,36,195,129
760 DATA56,48,48,124,186,186,186,186,186,44,41,58,44,200,176,32
770 DATA28,12,12,62,93,93,93,93,93,52,148,92,52,19,13,4
780 DATA0,1,0,1,3,0,1,3,7,1,3,7,15,1,3,7,15,31,1,3,7,15,31,63
790 DATA128,192,128,192,224,128,192,224,240,192,224,240,248,192,224,240,248,252,192
800 DATA224,224,248,252,254
810 DATA6,30,126,30,2,2,2,2
820 DATA77,77,77,127,28,28,28,28

