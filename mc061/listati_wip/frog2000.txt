10 GRAPHIC0,1:COLOR0,1:COLOR4,1:COLOR5,6
20 CHAR,5,2,"(SHIFT-O)(CBM-Y)  (SHIFT-B) (CBM-Y)(SHIFT-P) (SHIFT-O)(SHIFT-P)"
30 CHAR,5,3,"(CBM-G)   (SHIFT-B) (CBM-O)(SHIFT-@) (SHIFT-L)(SHIFT-@)"
40 CHAR,5,4,"(CBM-G)   (SHIFT-B) (SHIFT-O)(CBM-Y) (SHIFT-O)(SHIFT-P)"
50 CHAR,5,5,"(SHIFT-L)(CBM-O)  (SHIFT-B) (SHIFT-L)(CBM-O) (SHIFT-L)(SHIFT-@)":COLOR5,3:CHAR,9,7,"THE FROG 2000"
60 COLOR5,12:CHAR,6,12,"PROGRAMMA PER COMMODORE 128"
70 COLOR5,6:CHAR,19,16,"BY":COLOR5,7:CHAR,11,18,"MARIA TERESA INTINI":COLOR5,8
80 BLOAD"SPRITE":BLOAD"LM":SYS2816
90 CHAR,6,20,"PREMI UN TASTO PER INIZIARE",1:GETKEYA$
100 SCNCLR:CHAR,4,4,"SCEGLI IL LIVELLO DI DIFFICOLTA'"
110 COLOR5,11:CHAR,14,6,"[1 - 2 - 3 ]",1
120 GETKEYA$:IFA$<"1"ORA$>"3"THEN120
130 V=VAL(A$)*4:GRAPHIC1,1:COLOR1,12:FORD=0TO24:CHAR,31,D,"         ",1:NEXT
140 FORS=0TO6:DRAW1,0,S*30TO255,S*30:NEXT:COLOR1,11
150 FORS=1TO8:SPRITES,1,S+2,1:NEXT:FORS=1TO6:MOVSPRS,0,S*32+20:NEXT
160 CHAR,33,10,"VITE",1:COLOR1,4:CHAR,33,2,"GIRI",1:COLOR1,8:CHAR,33,6,"TEMPO",1
170 MOVSPR8,126,237:MOVSPR7,295,220:SPRITE7,0
180 VI=6:G=0:J=250:POKEJ,0:TI$="000000":GOSUB290
190 COLLISION1,240:IFPEEK(J)=1THENGOSUB280
200 IFC=1THENC=0:GOTO250
210 COLOR1,4:G$=STR$(G):CHAR,33,3,G$
220 COLOR1,11:V$=STR$(VI):CHAR,33,11,V$
230 COLOR1,8:CHAR,33,7,RIGHT$(TI$,4):IFTI$="000059"THENQ=2:GOTO310:ELSEGOTO190
240 C=1:FORS=1TO6:MOVSPRS,90#0:NEXT:COLLISION1:COLLISION2:COLLISION1,240:RETURN
250 MOVSPR6,0,212:VI=VI-1:SOUND1,2000,150,1,200,10,2,60
260 FORT=1TO9:FORS=1TO16:MOVSPR8,126,237:SPRITE7,1,S,0:NEXT:NEXT:SPRITE7,0
270 IFVI=0THENQ=1:GOTO310:ELSEGOSUB290:GOTO190
280 G=G+1:MOVSPR8,126,237:SOUND1,5000,20,2,4900,100,0:POKEJ,0:RETURN
290 MOVSPR1,90#V:MOVSPR2,270#V+2:MOVSPR3,90#V+2
300 MOVSPR4,90#V+3:MOVSPR5,270#V+1:MOVSPR6,270#V+3:RETURN
310 FORS=1TO8:SPRITES,0:MOVSPRS,0#0:NEXT:GRAPHIC0,1
320 IFQ=2THENM$="HAI FINITO IL TEMPO A TUA DISPOSIZIONE":Q=0
330 IFQ=1THENM$="   SEI STATO INVESTITO TROPPE VOLTE   ":Q=0
340 CHAR,1,3,M$,1
350 COLOR5,6:CHAR,11,6,"GIOCHI ANCORA  S/N":GETKEYA$:IFA$="S"THEN100
360 COLOR5,8:CHAR,15,10,"FINE GIOCO":SLEEP1:END

