10 REM  QUESTO PROGRAMMA E' DA USARE SOLO DOPO AVER CARICATO E LANCIATO
20 REM  IL PROGRAMMA   HCOPY
30 REM
100 PRINT"(CLR)(YEL)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)  GRAFICI DI FUNZIONI (S PER STAMPARE)(DOWN)(DOWN)(DOWN)"
110 PRINT"1-  Y=SEN(X)":PRINT"2-  Y=COS(X)":PRINT"3-  Y=X^3-X+5"
120 PRINT"4-  Y=SEN(X)*COS(X/2)":PRINT"5-  Y=X*COS(X)":PRINT"(DOWN)0-  FINE"
130 INPUT"(DOWN)(DOWN)COSA SCEGLI ";S$
140 S=VAL(S$):IFS<0ORS>5THEN100
145 IF S=0THEN END
150 GOSUB10000:REM ATTIVA PAGINA GRAFICA
160 GOSUB11000:REM CANCELLA PAGINA GRAFICA
170 REM DISEGNA GLI ASSI
180 FORX=0TO319:Y=100:GOSUB20000:Y=0:GOSUB20000:Y=199:GOSUB20000:NEXT
190 FORY=0TO199:X=160:GOSUB20000:X=0:GOSUB20000:X=319:GOSUB20000:NEXT
195 REM DISEGNA GRAFICO
200 FORX1=-150TO150
210 ON S GOSUB2000,2100,2200,2300,2400:REM CALCOLA VALORE FUNZIONE IN X1
220 X=X1+160:GOSUB20000:REM PLOT X,Y
230 NEXT
240 GETA$:IFA$=""THEN240
250 POKE53265,PEEK(53265)AND223:POKE53272,21 :REM DISATTIVA PAGINA GRAFICA
260 IFA$<>"S"THEN100
265 PRINT"(CLR)STAMPA"
270 INPUT"(DOWN)(DOWN)(DOWN)(DOWN)TIPO DI STAMPA (0-3) ";T
280 INPUT"(DOWN)COLONNA INIZIO STAMPA (0-40) ";C
290 INPUT"(DOWN)RIGHE DA STAMPARE (25=TUTTO) ";R
300 GOSUB10000:REM ATTIVA PAGINA GRAFICA
310 SYS49200,T,C,8192,R :REM STAMPA
330 A$="":GOTO250
1999 REM FUNZIONI
2000 Y=100-SIN(X1/25)*60:RETURN
2100 Y=100-COS(X1/25)*60:RETURN
2200 Y=100-(X1/35)^3-X1/35+5:RETURN
2300 Y=100-SIN(X1/16)*COS(X1/32)*60:RETURN
2400 X2=X1/1.7:Y=100-X2*COS(X2/16):RETURN
9999 REM
10000 POKE53265,PEEK(53265)OR32:POKE53272,(PEEK(53272)AND240)OR8
10010 FORA=1024TO2023:POKEA,1:NEXT:RETURN
11000 FORA=8192TO16192:POKEA,0:NEXT:RETURN
20000 REM PLOT X,Y
20010 P=320*INT(Y/8)+8*INT(X/8)+(YAND7)
20020 B=7-(XAND7)
20030 POKE8192+P,PEEK(8192+P)OR2^B
20040 RETURN

