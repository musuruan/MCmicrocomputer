100 O1=1:I=17:J=1:POKE37154,127
110 GOSUB430:FORL=1TO10:A$=A$+"(SHIFT-M)(DOWN)":C$=C$+"(SHIFT-N)(DOWN)(LEFT)(LEFT)":B$=B$+"(SHIFT-N)(UP)":D$=D$+"(SHIFT-M)(UP)(LEFT)(LEFT)":NEXT
120 FORL=1TO20:E$=E$+" (DOWN)(LEFT)":F$=F$+"(CBM-G)(DOWN)(LEFT)":G$=G$+"(CBM-@)":H$=H$+"(CBM-T)":NEXT
130 FORL=1TO20:L$=L$+"(LEFT)":I$=I$+"(UP)":X$=X$+"(RGHT)":Y$=Y$+"(DOWN)":NEXT
140 IX=0:IY=1
150 GOSUB740
160 A=A*2:B=B*4:X=X*2:Y=Y*4:X1=X-2*(X>0):Y1=Y-1*4*(Y>0)
170 PRINT"(CLR)":V=X1/2-X/2:V1=-(21-X)*3*(X>0):V2=-(21-X1)*3*(X1<>0):K=Y1/4-Y/4
180 PRINT"(HOME)"LEFT$(A$,X)"(LEFT)"LEFT$(E$,V1)LEFT$(I$,V1/3)"(UP)"LEFT$(Y$,V);
190 PRINTLEFT$(X$,-1*(X>0))LEFT$(I$,-1*(X=0))LEFT$(G$,V)"(DOWN)(LEFT)"LEFT$(E$,V2)LEFT$(I$,V2/3);
200 PRINT"(RGHT)"LEFT$(I$,-1*(X1=0))LEFT$(A$,A-X1)"(LEFT)"LEFT$(E$,-(21-A)*3*(A>0)) 
210 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)"LEFT$(B$,X)LEFT$(I$,V)"(DOWN)"LEFT$(H$,V)"(UP)";
220 PRINTLEFT$(B$,A-X1)
230 PRINT"(HOME)"TAB(19)LEFT$(C$,Y)"(RGHT)"LEFT$(F$,-(21-Y/2)*3*(Y>0))LEFT$(I$,21-Y/2)"(LEFT)";
240 PRINTLEFT$(L$,K)LEFT$(Y$,K)"(RGHT)"LEFT$(F$,-(21-Y1/2)*3*(Y1>0))LEFT$(I$,21-Y1/2);
250 PRINT"(UP)"LEFT$(I$,-1*(Y1<=4))LEFT$(G$,K)LEFT$(L$,K)"(DOWN)(LEFT)"LEFT$(I$,-1*(Y1=0))LEFT$(C$,B-Y1);
260 PRINT"(RGHT)"LEFT$(F$,-(22-B/2)*3*(B>0))
270 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)"TAB(19)LEFT$(D$,Y)LEFT$(L$,K)LEFT$(I$,K);
280 PRINT"(DOWN)(RGHT)"LEFT$(H$,K)LEFT$(L$,K)"(UP)(LEFT)"LEFT$(D$,B-Y1)
290 F=A/2+B/4:O=A/2:IFB/4>A/2THENNO=B/4
300 IFR=1THENO=O+1
310 PRINT"(HOME)"LEFT$(Y$,O)LEFT$(X$,A/2)LEFT$(H$,20-F)LEFT$(L$,20-F)LEFT$(Y$,20-2*O)"(DOWN)"LEFT$(H$,20-F)
320 IFPEEK(197)=39ORPEEK(37137)=122THEN380
330 IFPEEK(197)=55ORPEEK(37137)=118ANDFL<>1THENIY=-IY:IX=-IX:GOTO150
340 IFPEEK(197)=18ORPEEK(37152)=119ANDFL<>1THENIY=-T*(IY=0):IX=T*(IX=0):GOTO150
350 IFPEEK(197)=41ORPEEK(37137)=110ANDFL<>1THENIY=T*(IY=0):IX=-T*(IX=0):GOTO150
360 IFPEEK(197)=13THENPRINT"(CLR)  POSIZIONE ATTUALE(DOWN)(LEFT)":K3=K3+10:GOSUB650
370 GOTO320
380 A=A+2*(A>0):X=X+2*(X>0):X1=X1+2*(X1>0):B=B+4*(B>0):Y=Y+4*(Y>0):Y1=Y1+4*(Y1>0)
390 IFA=0ANDB=0ANDR<>1THENPOKE36878,15:FORZ=1TO255STEP8:POKE36876,Z:NEXT:POKE36878,0
400 IFA=0ANDB=0ANDFL=1THENO1=0:POKE36879,25:PRINT"(CLR)":GOSUB660:GOSUB860:POKE37154,255:END
410 IFA>0ORB>0ORR=1THENI=I+IX:J=J+IY:R=-R*(A>0)
420 GOTO170
430 DIMA%(18):POKE36879,8:PRINT"(WHT)":TI$="000000"
440 A%(0)=0
450 A%(1)=16126
460 A%(2)=10882
470 A%(3)=10938
480 A%(4)=11144
490 A%(5)=10558+256*(RND(1)>.5)
500 A%(6)=12256
510 A%(7)=8270
520 A%(8)=16248
530 A%(9)=2370+64*(RND(1)>.7)
540 A%(10)=14842
550 A%(11)=10286
560 A%(12)=12264+8*(RND(1)>.5)
570 A%(13)=8510
580 A%(14)=12192+32*(RND(1)>.5)
590 A%(15)=10430
600 A%(16)=10888
610 A%(17)=16126
620 A%(18)=0
630 Z=INT(RND(0)*8+1)*2-1:A%(Z)=A%(Z)+1
640 :PRINT"(CLR)      LABIRINTO(DOWN)(DOWN)":PRINT"   ? ? ? ? ? ? ? ?"
650 PRINT"  (BLU)(CBM-+)(WHT)(CBM-+)(BLU)(CBM-+)(WHT)(CBM-+)(BLU)(CBM-+)(WHT)(CBM-+)(BLU)(CBM-+)(WHT)(CBM-+)(BLU)(CBM-+)(WHT)(CBM-+)(BLU)(CBM-+)(WHT)(CBM-+)(BLU)(CBM-+)(WHT)(CBM-+)(BLU)(CBM-+)(WHT)(CBM-+)(BLU)(CBM-+)(CBM-+)(BLU)(CBM-+)"
660 FORL=O1TO14:PRINTTAB(2);
670 FORL1=0TO18:IFL=JANDL1=ITHENPRINT"(WHT).(BLU)";:NEXT
680 IF(2^LANDA%(L1))=0THENPRINT"(BLU)(CBM-+)";:NEXT:GOTO700
690 PRINT" ";:NEXT
700 PRINT:NEXT:PRINT:IFFL=1THENRETURN
710 PRINT"(DOWN)(WHT)    PREMI UN TASTO "
720 IFPEEK(197)=64ANDPEEK(37137)=126THEN720
730 RETURN
740 X=0:Y=0:R=0:T=IX+IY:XX=I:YY=J
750 IFJ>14ORI>18ORI<=0ORJ<=0THENFORBI=1TO10:H$="(CBM-Y)(LEFT) ":NEXT:I=I+1:J=J+1:B=U:FL=1:A=U:GOTO810
760 IF(2^(J+IY)ANDA%(I+IX))=0THENA=U:B=U:GOTO810
770 K=1-ABS(IY):K1=1-ABS(IX):IF(2^(J-T*K)ANDA%(I+T*K1))<>0THENX=U
780 IF(2^(J+T*K)ANDA%(I-T*K1))<>0THENY=U
790 J=J+IY:I=I+IX:U=U+1
800 GOTO750
810 IFA=0THEN850
820 A=A-1*((2^(J+T*K)ANDA%(I-T*K1))<>0)
830 B=B-1*((2^(J-T*K)ANDA%(I+T*K1))<>0)
840 IFA=BANDU<>ATHENR=1:A=A-1:B=B-1
850 U=0:J=YY:I=XX:RETURN
860 PRINT"(BLU)SEI USCITO IN",INT(TI/60)"SECONDI"
870 PRINT"(BLU)VUOI RIGIOCARE? (S/N)"
880 IFPEEK(197)=64THEN880
890 IFPEEK(197)=41THENRUN
900 RETURN

