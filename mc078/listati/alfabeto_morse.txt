50 LDF=50:DIMD$(50):GOTO100
60 A=53280:POKEA,14:POKEA+1,6
61 PRINT"(CLR)(LBLU)":END
70 FORX=1TO3000:NEXTX:RETURN
100 REM --------------------------
110 REM --SIMULAZIONE LINGUAGGIO--
120 REM ---------MORSE------------
130 REM
140 REM -----BY  LUCIANO ROSA-----
150 REM
160 PRINT"(CLR)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(GRN)"
170 A=53280:POKEA,0:POKEA+1,0
180 PRINTTAB(5)"SIMULAZIONE LINGUAGGIO MORSE":A$="(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)"
185 A$=A$+"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)DI  LUCIANO ROSA"
190 FORX=1TOLEN(A$):PRINTLEFT$(A$,X):FORY=1TO85:NEXTY:NEXTX:GOSUB70
200 PRINT"(CLR)(DOWN)(DOWN)(RGHT)(RGHT)OPZIONI:"
210 PRINT"(DOWN)(DOWN)(DOWN)(RGHT)1.ESERCITAZIONE"
220 PRINT"(DOWN)(RGHT)2.DECIFRAZIONE"
230 PRINT"(DOWN)(RGHT)3.TRADUZIONE MORSE"
232 PRINT"(DOWN)(RGHT)4.TABELLA MORSE"
235 PRINT"(DOWN)(RGHT)5.FINE"
240 GETB$:B=VAL(B$):IFB<1ORB>5THEN240
250 ONBGOTO260,360,690,1080,60
260 PRINT"(CLR)(RGHT)(RVS) + (OFF) = -    (RVS) - (OFF) = .";
265 PRINT"    (RVS) X (OFF) = FINE"
270 GETB$:IFB$="+"THENTE=150:GOSUB300
280 IFB$="-"THENTE=70:GOSUB300
285 IFB$="X"THEN200
290 GOTO270
300 SI=54272:FL=SI :FH=SI+1:TL=SI+2
310 TH=SI+3:A=SI+4:H=SI+6:L=SI+24
320 POKEL,15:POKEA,16:POKEH,4*16+4
330 W=SI+4:POKEFH,29:POKEFL,69:POKEW,17
340 FORI=1TOTE:NEXTI:POKEW,0:POKEA,0
345 POKE H,0:RETURN
360 PRINT"(CLR)":REM DECIFRAZIONE
361 PRINT"(HOME)(RGHT)(RVS) + (OFF) = -    (RVS) - (OFF) = .";
365 PRINT"    (RVS) X (OFF) = FINE":PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)";
366 GETB$:IFB$=""THEN366
367 FORM=1TO80:GETB$
375 IFB$="+"THENB$="-":D$=D$+B$:GOTO400
380 IFB$="-"THENB$=".":D$=D$+B$:GOTO420
385 IFB$="X"THENU$="":GOTO200
390 NEXTM:IFB$=""THEN500
400 PRINTB$;:TE=150:GOSUB300:GOTO367
420 PRINTB$;:TE=70:GOSUB300:GOTO367
500 RESTORE:FORIL=1TO42
510 READF$,G$
520 IFD$=F$THEND$="":GOTO540
530 NEXTIL:GOTO550
540 U$="(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)":U$=U$+U$+U$
541 F=F+1:V$=LEFT$(U$,F)
542 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)"+V$;
543 PRINTG$;
544 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)            "
545 GOTO361
550 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)NON ESISTE!"
555 GOSUB70:D$=""
560 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)           "
570 GOTO361
600 DATA".-",A,"-...",B,"-.-.",C
605 DATA"-..",D,".",E,"..-.",F
610 DATA"--.",G,"....",H,"..",I
615 DATA".-..",L,"--",M,"-.",N
620 DATA"---",O,".--.",P,"--.-",Q
625 DATA".-.",R,"...",S,"-",T
630 DATA"..-",U,"...-",V,"--..",Z
635 DATA".---",J,"-.-",K,".--",W
640 DATA"-..-",X,"-.--",Y
650 DATA"-.-.-.",";",".-.-.-","."
655 DATA"---...",":","..--..","?"
660 DATA".----","1","..---","2"
665 DATA"...--","3","....-","4"
670 DATA".....","5","-....","6"
675 DATA"--...","7","---..","8"
676 DATA"----.","9","-----","0"
680 DATA"........","(DEL)(DEL)","--..--",","
690 A$="--------------------":A$=A$+A$
700 PRINT"(CLR)(DOWN)(DOWN)(RGHT)(RGHT)OPZIONI:"
710 PRINT"(DOWN)(DOWN)(DOWN)(RGHT)1.TRASFORM.DIRETTA"
720 PRINT"(DOWN)(RGHT)2.ESERC.DECIFRAZIONE"
725 PRINT"(DOWN)(RGHT)3.LIVELLO DIFFICOLTA' "
730 PRINT"(DOWN)(RGHT)4.RITORNO MENU"
740 GETB$:B=VAL(B$):IFB$="_"THEN200
750 IFB<1ORB>4THEN740
760 ONBGOTO770,910,1300,200
770 PRINT"(CLR)(RGHT)(RVS) _ (OFF) = USCITA(DOWN)":PRINTA$
780 GETB$:IFB$=""THEN780:
785 IFB$="_"THEN200
790 RESTORE:FORI=1TO42:READF$,G$
800 IFB$=G$THENF=0:GOTO815
810 NEXTI:GOTO780
815 GOSUB820:GOTO780
820 F=F+1:A$="(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)"
825 A$=A$+A$:WK$=LEFT$(A$,F)
830 PRINT"(HOME)(DOWN)(DOWN)(DOWN)"+WK$;G$;
835 LU=LU+1
840 IFLU>LEN(F$)THENLU=0:GOTO890
850 H$=MID$(F$,LU,1)
855 IFH$="-"THENTE=150:GOSUB300:GOTO870
860 IFH$="."THENTE=55:GOSUB300
870 FORX=1TOLDF:NEXTX
875 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)";F$
880 FORX=1TO200:NEXTX:GOTO835
890 PRINT"(HOME)(DOWN)(DOWN)(DOWN)  ":PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)        "
900 RETURN
910 PRINT"(CLR)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)OPZIONI:"
911 PRINT"(DOWN)(DOWN)(RGHT)(RGHT)1.DATI"
912 PRINT"(DOWN)(RGHT)(RGHT)2.DATI2"
913 PRINT"(DOWN)(DOWN)(DOWN)(RGHT)SCEGLI IL FILE LEGGERE"
914 GETFL$:FIL=VAL(FL$) :IFFIL<1ORFIL>2THEN914
915 IFFIL=1THENFL$="DATI"
916 IFFIL=2THENFL$="DATI2"
920 PRINT"(DOWN)(DOWN)(DOWN)(RGHT)INSERISCI IL DISCHETTO CONTENENTE"
925 PRINT"(RGHT)I DATI (LETTURA 'DATI') E BATTI "
930 PRINT"(RGHT)(RVS) RETURN (OFF)"
935 GETA$:IFA$=""THEN935
940 IFA$<>CHR$(13)THEN935
945 PRINT"(CLR)":A$="--------------------"
946 A$=A$+A$:PRINT"(HOME)(DOWN)"A$
947 PRINT"(HOME)(RGHT)V=VERIF.GRUPPO  S=ALTRO GRUPPO  N=STOP"
950 OPEN1,8,2,FL$+",S,R"
955 FORI=1TO50:INPUT#1,D$(I)
960 NEXTI:CLOSE1
970 AB=INT(50*RND(1))+1
980 C$=D$(AB):MI=0
985 MI=MI+1
990 B$=MID$(C$,MI,1)
995 RESTORE:FORI=1TO42:READF$,G$
1000 IFB$=G$THENF=0:GOTO1020
1010 NEXTI:GOTO1030
1020 GOSUB820:GOTO1030
1030 IFMI<LEN(D$(AB))THENGOTO1070
1040 GETNM$:IFNM$=""THEN1040
1045 IFNM$="S"THEN970
1046 IFNM$="V"THEN1060
1050 GOTO200
1060 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)";C$:GOTO1040
1070 FORX=1TO250:NEXTX:GOTO985
1080 PRINT"(CLR)";:REM TABELLA
1090 PRINT"(CBM-A)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)"
1100 PRINT"(SHIFT--)"
1101 PRINT"(HOME)(DOWN)"TAB(3)"TABELLA MORSE"
1110 PRINT"(CBM-Q)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-R)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-W)"
1115 FORX=1TO19
1120 PRINT"(SHIFT--)      (SHIFT--)           (SHIFT--)"
1135 NEXTX
1140 PRINT"(SHIFT--)      (SHIFT--)           (SHIFT--)"
1150 PRINT"(CBM-Z)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-E)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)"
1155 PRINT"(HOME)(DOWN)(DOWN)"
1160 RESTORE:FORI=1TO20: READF$,G$
1170 PRINTTAB(3)G$;TAB(9)F$:NEXTI
1180 PRINT"(HOME)";
1190 PRINTTAB(19)"(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)"
1200 PRINTSPC(38)"(SHIFT--)"
1210 PRINTTAB(19)"(CBM-R)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-R)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-W)"
1220 FORX=1TO20
1230 PRINTSPC(26)"(SHIFT--)"SPC(11)"(SHIFT--)":NEXTX
1240 PRINTTAB(19)"(CBM-E)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-E)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)"
1255 PRINT"(HOME)(DOWN)(DOWN)"
1260 FORI=21TO40:READF$,G$
1270 PRINTTAB(22)G$;TAB(28)F$:NEXTI
1280 GETA$:IFA$<>CHR$(13)THEN1280
1290 GOTO200
1300 INPUT"(DOWN)(DOWN)(RGHT)LIVELLO (1-4)   1(LEFT)(LEFT)(LEFT)";LD$
1400 LDF=VAL(LD$):IFLDF<1ORLDF>4THEN1300
1410 IFLDF=1THENLDF=50
1420 IFLDF=2THENLDF=100
1430 IFLDF=3THENLDF=210
1440 IFLDF=4THENLDF=320
1450 GOTO700

