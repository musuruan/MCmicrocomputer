0 REM *********************************
1 REM *********************************
2 REM *  STAMPA DIRECTORY  2 COLONNE  *
3 REM *********************************
5 REM ****     RAIMONDO PIAZZA     ****
6 REM ****     VIA R. PILO, 92     ****
7 REM ****  92024 CANICATTI' (AG)  ****
8 REM *********************************
9 REM *********************************
10 CLR:FF=1:NA=0:NB=0
20 POKE53281,6:POKE53280,6 :PRINTCHR$(14)
30 T$(0)="DEL":T$(1)="SEQ":T$(2)="PRG":T$(3)="USR":T$(4)="REL":T$(5)="---"
40 S1$="(CBM-A)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)":NN$="(RVS)(SHIFT-N)OME(OFF):"
45 ID$=" (RVS)(SHIFT-I)D(OFF):":BB$="(SHIFT--)"
50 S2$="(CBM-Z)(CBM-R)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-R)(CBM-X)":BF$="(SHIFT-B)LOCCHI LIBERI         "
60 S3$=" (CBM-Z)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)":BS$=" (SHIFT--)":BD$=" (SHIFT--)":SP$="  ":SZ$="   "
65 S4$="                                     "
70 IN$="(HOME)(RVS)(WHT)(CBM-A)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*) (SHIFT-R)(SHIFT-A)(SHIFT-Y) (SHIFT-P)(SHIFT-I)(SHIFT-A)(SHIFT-Z)(SHIFT-Z)(SHIFT-A) SOFT (C) 1989 (SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)"
75 ES$=CHR$(27):RS$=CHR$(64)
80 IM$="(RVS)(GRN)(CBM-Z)(SHIFT-*)(SHIFT-*) (SHIFT-S)TAMPA DIRECTORY SU DUE COLONNE (SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)"
90 IP$="(RVS)(YEL)*  (SHIFT-I)NSERISCI IL DISCO, PREMI UN TASTO  *(WHT)"
100 IQ$="(RVS)(YEL)*****  1-(SHIFT-S)TAMPA         2-(SHIFT-A)NNULLA  *****(WHT)"
105 IC$="(RVS)(YEL)*  (SHIFT-A)VANZAM. CARTA: 1- 1/6''  2- 1/8''  *(WHT)"
110 IR$="(RVS)(YEL)****  1-(SHIFT-C)ONFERMA        2-(SHIFT-A)NNULLA  *****(WHT)"
115 IS$="(RVS)(YEL)* (SHIFT-P)REPARA LA STAMPANTE, PREMI UN TASTO *(WHT)"
117 IT$="(RVS)(YEL)**  (SHIFT-I)NTESTAZIONE  (MAX 32 CARATTERI)  **(WHT)"
120 PRINT"(CLR)";IN$;IM$;IP$:GOSUB 500
130 GOSUB 1000:CLOSE2:CLOSE15
140 PRINT IN$;IM$;IR$:SW=1:GOSUB 500
150 IF Z$="2" THEN RUN
155 PRINT "(CLR)";IN$;IM$;IT$:PRINT:PRINT:PRINT
156 PRINT "  (CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(CBM-T)(UP)(UP)":INPUT M$
157 IF LEN(M$)>32 THEN GOTO 155
158 GOSUB 700
160 FF=FF+1:IF FF<3 THEN GOTO 120
170 PRINT"(CLR)";IN$;IM$;IQ$:SW=1:GOSUB 500
180 IF Z$="2" THEN RUN
189 REM
190 REM ******************
191 REM * FASE DI STAMPA *
192 REM ******************
220 PRINTIN$;IM$;IC$:SW=1:GOSUB500:AV$=Z$
230 PRINTIN$;IM$;IS$:GOSUB500
240 OPEN4,4,7
250 PRINT#4,ES$+RS$
260 IF AV$="2" THEN PRINT#4,ES$+"0"
270 N=NB:IF NA>NB THEN N=NA
280 FOR X=0 TO 3
290 PRINT#4,SZ$+SZ$+A$(X)+SP$+SP$+B$(X)
300 NEXT X
310 FOR X=4 TO N
320 PRINT#4,SZ$+SZ$;A$(X);SP$+SZ$;B$(X)
330 NEXT X
340 RUN
341 REM
342 REM **** FINE MAIN PROGRAM ****
343 REM
500 REM ******************
501 REM * PREMI UN TASTO *
502 REM ******************
510 GET Z$
515 IF Z$="" THEN GOTO 510
520 IF Z$<>"1" AND Z$<>"2" AND SW=1 THEN GOTO 510
530 SW=0
540 RETURN
550 REM
600 REM **************************
601 REM * COSTRUZIONE STRINGA N$ *
602 REM **************************
610 N$="":FOR L=1 TO I
620 GET#2,L$
630 IF L$<>CHR$(96) THEN N$=N$+L$
640 NEXT L
650 RETURN
660 REM
700 REM *************************
701 REM * CENTRAGGIO STRINGA M$ *
702 REM *************************
710 AX$="":T=INT((32-LEN(M$))/2)
720 FOR L=1 TO T:AX$=AX$+" ":NEXTL
730 AX$=AX$+M$
740 M$=LEFT$(AX$,32)
745 IF LEN(M$)=32 THEN GOTO 750
746 M$=M$+" ":GOTO 745
750 IF FF=2 THEN GOTO 770
760 A$(0)=M$:GOTO 780
770 B$(0)=M$
780 RETURN
790 REM
800 REM *************************
801 REM * AVANZAMENTO DI I-BYTE *
802 REM *************************
810 FOR L=1 TO I
820 GET#2,A$
830 NEXT L
840 RETURN
850 REM
1000 REM ****************************
1001 REM * MEM. E VISUAL. DIRECTORY *
1002 REM ****************************
1010 IF XX=1 THEN GOTO 1030
1020 DIM A$(150),B$(150):XX=1
1030 BK=0
1040 CLOSE 15:OPEN 15,8,15,"I"
1050 CLOSE 2:OPEN2,8,2,"$"
1060 I=142:GOSUB 800
1070 I=16:GOSUB 600
1075 IF FF=2 THEN GOTO 1090
1080 A$(1)=S1$:GOTO 1100
1090 B$(1)=S1$
1100 NX$=BB$+NN$+N$+ID$
1110 I=2:GOSUB 800
1120 I=5:GOSUB 600
1130 NX$=NX$+N$+BB$
1140 IF FF=2 THEN GOTO 1160
1150 A$(2)=NX$:A$(3)=S2$:NA=4:GOTO 1170
1160 B$(2)=NX$:B$(3)=S2$:NB=4
1170 I=89:GOSUB 800
1180 FOR J=1 TO 8
1190 GET#2,T$,A$,B$
1200 IF ST THEN GOTO 1420
1210 IF T$="" THEN T$=CHR$(128)
1220 I=16:GOSUB 600:NM$=N$
1230 IF ASC(T$)=192 THEN GOTO 1260
1240 IF (ASC(T$)-128)>4 OR (ASC(T$)-128)<0 THEN GOTO 1270
1250 TP$=T$(ASC(T$)-128):GOTO 1280
1260 TP$=T$(ASC(T$)-187):GOTO 1280
1270 TP$="XXX"
1280 I=10:GOSUB 800
1290 BL=ASC(A$+CHR$(0)):BL$=STR$(BL)
1300 IF LEN(BL$)=4 THEN 1320
1310 BL$=" "+BL$:GOTO 1300
1320 IF ASC(T$)=128 AND ASC(A$+CHR$(0))<>0 THEN GOTO 1380
1325 IF ASC(T$)=128 AND ASC(A$+CHR$(0))=0 THEN GOTO 1380
1330 IF ASC(T$)=128 AND N$="" THEN GOTO 1380
1340 NX$=BS$+BL$+SP$+NM$+SP$+TP$+BD$
1350 IF FF=2 THEN GOTO 1370
1360 A$(NA)=NX$:NA=NA+1:BK=BK+BL:GOTO1380
1370 B$(NB)=NX$:NB=NB+1:BK=BK+BL
1380 I=1:GOSUB 800
1390 IF J<8 THEN GET#2,A$,A$
1400 NEXT J
1410 GOTO 1180
1420 BK$=STR$(664-BK)
1430 IF LEN(BK$)=4 THEN GOTO 1450
1440 BK$=" "+BK$:GOTO 1430
1450 NX$=SP$+BK$+SP$+BF$
1460 IF FF=2 THEN GOTO 1480
1470 A$(NA)=S3$:NA=NA+1:A$(NA)=NX$:NA=NA+1:GOTO1490
1480 B$(NB)=S3$:NB=NB+1:B$(NB)=NX$:NB=NB+1
1482 IF NA>=NB THEN GOTO 1490
1484 FOR CT=NA TO NB
1485 A$(CT)=S4$
1486 NEXT CT
1490 IF FF=2 THEN GOTO 1560
1500 PRINT IN$;IM$;IR$
1510 FOR Z=0 TO NA
1520 PRINT "    ";A$(Z)
1540 NEXT Z
1550 GOTO 1600
1560 PRINT IN$;IM$;IR$
1570 FOR Z=0 TO NB
1580 PRINT "    ";B$(Z)
1590 NEXT Z
1600 RETURN

