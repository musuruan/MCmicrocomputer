100 REM*********************************
110 REM***** OTHELLO DI L. TAVOLATO ****
120 REM*********************************
130 :
140 CLR:POKE53280,6:POKE53281,6:DIMS(9,9),MC(60):GOSUB1420
150 :
160 DATA 255,248,0,128,8,0,128,8,0,128,8,0,128,8,0,128,8,0,128,8,0,128,8,0
170 DATA128,8,0,128,8,0,128,8,0,128,8,0,128,8,0,255,248,0,0,0,0,0,0,0,0,0,0,0
180 DATA0,0,0,0,0,0,0,0,0,0,0,0,0,1,-1,1,-1,0,-1,-1,0,-1,1,-1,1,0,1,1
190 :
200 REM*******INIZIALIZZAZIONI  ******
210 :
220 FORI=1TO64:READA:POKE831+I,A:NEXTI
230 FORI=1TO8:READO(I),V(I):NEXTI
240 :
250 L$="(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)":POKE53269,0
260 FORO=0TO9:FORV=0TO9:S(O,V)=0:NEXTV,O:C$(0)="(BLK)":C$(1)="(WHT)":BS=1144:BC=55416
270 S(4,4)=1:S(5,5)=1:S(4,5)=-1:S(5,4)=-1:T(0)=2:T(1)=2:V=1:O=1:G=1
280 IFDM=0THENR=RND(0):G=-(R<0.51)+(R>0.5):CO=G:GOTO400
290 PRINTLEFT$(L$,6);TAB(21);"(GRN)VUOI GIOCARE
300 PRINTTAB(21)"(DOWN)CONTRO DI ME ? ";
310 GETG$:ON-(G$<>"S"ANDG$<>"N")GOTO310:PRINTG$:IFG$="N"THENCO=0:GOTO340
320 PRINTTAB(21)"(DOWN)(DOWN)LIVELLO (1-3) ? ";
330 POKE198,0:WAIT198,1:GETM$:ON-(VAL(M$)<1ORVAL(M$)>3)GOTO330:PRINTM$:CO=-1
340 FORI=1TO15:PRINTLEFT$(L$,5+I)TAB(20)"                   ":NEXTI
350 IFG$="S"THEN380
360 PRINTLEFT$(L$,12)TAB(22)"(WHT)PLAYER 1 :   2
370 PRINTLEFT$(L$,14)TAB(22)"(BLK)PLAYER 2 :   2":GOTO450
380 PRINTLEFT$(L$,12)TAB(22)"(WHT)PLAYER   :   2
390 PRINTLEFT$(L$,14)TAB(22)"(BLK)OTHELLO  :   2":GOTO450
400 PRINTLEFT$(L$,12)TAB(21)"(WHT)OTHELLO 1 :   2
410 PRINTLEFT$(L$,14)TAB(21)"(BLK)OTHELLO 2 :   2
420 :
430 REM*** PILOTAGGIO SPRITE & GAME ***
440 :
450 POKE53269,1:POKE53287,-(G=1):POKE2040,13:GOSUB1380:FF=0
460 GETX$:X=PEEK(56320)
470 IF(T(0)=0ORT(1)=0ORT(0)+T(1)=64)ORFF>1THENGOSUB1310
480 IFX$="(UP)"ORX=126THENV=V+1*(V>1)-7*(V=1)
490 IFX$="(DOWN)"ORX=125THENV=V-1*(V<8)+7*(V=8)
500 IFX$="(LEFT)"ORX=123THENO=O+1*(O>1)-7*(O=1)
510 IFX$="(RGHT)"ORX=119THENO=O-1*(O<8)+7*(O=8)
520 IFX$="D"THENGOSUB1420:DM=0:GOTO250
530 IFX$="P"THENFF=FF+1:GOSUB830
540 IFX$="N"THENGOSUB1420:DM=1:GOTO250
550 IFX$="H"THENGOSUB890
560 IF(X$=CHR$(13)ORX=111)ANDS(O,V)=0THENGOSUB640
570 IFCO=GTHENGOSUB890:IFDM<1THENCO=G
580 POKE53248,21+O*16:POKE53249,71+V*16
590 FORI=1TO200:NEXT:REM RALLENTAMENTO PER COMPILATORE
600 GOTO460
610 :
620 REM* C'E' UNA PEDINA AVVERSARIA ? *
630 :
640 F1=0:FORO0=-1TO1:FORV0=-1TO1:F1=F1-(S(O+O0,V+V0)=-G):NEXTV0,O0
650 ON-(F1>0)GOSUB780:RETURN
660 :
670 REM**** MANGIA LE PEDINE ****
680 :
690 S1=0:FORK=1TO8:O1=O(K):V1=V(K):O2=O+O1:V2=V+V1:S3=0:IFS(O2,V2)<>-GTHEN740
700 S3=S3+1:O2=O2+O1:V2=V2+V1:ON-(S(O2,V2)=0)GOTO740:IFS(O2,V2)<>GTHEN700
710 S1=S1+S3:ON-(U<>0)GOTO740:O2=O:V2=V
720 ON-(F1=>0)GOSUB1380:FORZ=0TOS3:CS=O2*2+V2*80:POKEBS+CS,81:POKEBC+CS,-(G=1)
730 S(O2,V2)=G:O2=O2+O1:V2=V2+V1:F1=-1:NEXTZ:FF=0
740 NEXTK:RETURN
750 :
760 REM**** CALCOLO DEL PUNTEGGIO ****
770 :
780 GOSUB690:IFS1<=0ORU<>0THENRETURN
790 G1=(G+1)/2:G2=(1-G)/2
800 T(G1)=T(G1)+S1+1:T(G2)=T(G2)-S1
810 PRINTLEFT$(L$,12+G1*2);TAB(32);RIGHT$("  "+C$(G2)+STR$(T(G2)),5)
820 PRINTLEFT$(L$,12+G2*2);TAB(32);RIGHT$("  "+C$(G1)+STR$(T(G1)),5)
830 G=-G:POKE53287,(G+1)/2:RETURN
840 :
850 REM******************************
860 REM**** STRATEGIA DI OTHELLO ****
870 REM******************************
880 :
890 U=1:CP=-2
900 :
910 REM* SCANSIONE MOSSE POSSIBILI *
920 :
930 FORO=1TO8:FORV=1TO8:ON-(S(O,V)<>0)GOTO1020:GOSUB640:IFF1<=0ORS1<=0THEN1020
940 CP=CP+3:ON-(M$="1")GOTO1010:IN=0
950 :
960 REM* VALUTAZIONE DELLA MOSSA *
970 :
980 IFO=1ORO=8ORV=1ORV=8THENGOSUB1100
990 IF(O=2ORO=7)AND(V=2ORV=7)THENGOSUB1250
1000 IFO>2ANDO<7ANDV>2ANDV<7THENIN=2
1010 MC(CP)=S1+IN:MC(CP+1)=O:MC(CP+2)=V
1020 NEXTV,O:U=0:IFCP<=0THENV=1:O=1:FF=FF+1:GOTO830
1030 :
1040 REM* SCELTA MOSSA MIGLIORE *
1050 :
1060 MC=1:FORI=1TOCPSTEP3:IFMC(I)>MC(MC)THENMC=I
1070 IFMC(I)=MC(MC)THENR=RND(0):MC=-I*(R>=0.5)-MC*(R<0.5)
1080 NEXTI:O=MC(MC+1):V=MC(MC+2):ON-(X$<>"H")GOTO780:RETURN
1090 :
1100 Q=O:W=V:DO=(O>2ANDO<7):DV=(V>2ANDV<7):Q=O:W=V:IFDO=DVTHENIN=30:RETURN
1110 Y1=S(O+DO,V+DV):Y2=S(O-DO,V-DV):IFY1=Y2THENIN=10:RETURN
1120 IFM$="2"THENIN=5:RETURN
1130 IF(Y1=GANDY2=-G)OR(Y1=-GANDY2=G)THEN1210
1140 IF(Y1=-G)OR(Y2=-G)THEN1170
1150 IN=5:RETURN
1160 :
1170 Y3=G:Y4=G:IFY2=GTHENDO=-DO:DV=-DV
1180 Q=Q+DO:W=W+DV:IFS(Q,W)=Y3ORS(Q,W)=Y4THENIN=20:RETURN
1190 ON-(S(Q,W)<>0)GOTO1180:RETURN
1200 :
1210 GOSUB1170:IFIN<>0THENRETURN
1220 DO=-DO:DV=-DV:Y3=-G:Y4=0:Q=O:W=V:GOSUB1180
1230 IN=-IN*(Q<>9ANDQ<>0ANDW<>9ANDW<>0):RETURN
1240 :
1250 DO=(O=2)-(O=7):DV=(V=2)-(V=7)
1260 S=S(O+DO,V+DV):IFS=0THENIN=-(INT(S1/2)+1):RETURN
1270 IN=-10*(S=G):RETURN
1280 :
1290 REM*****IL VINCITORE *****
1300 :
1310 PRINTLEFT$(L$,11-(T(1)<T(0))*2);
1320 PRINTTAB(20)"(YEL)(CBM-A)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)
1330 PRINTTAB(20)"(SHIFT--)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--)
1340 PRINTTAB(20)"(CBM-Z)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)":RETURN
1350 :
1360 REM*** FLASHIN' DEL CURSORE *****
1370 :
1380 POKE53248,21+O*16:POKE53249,71+V*16:FORI=1TO6
1390 FORJ=1TO500:NEXTJ:REM** IN COMPILAZIONE AUMENTARE IL CICLO A 500 **
1400 POKE53269,-(PEEK(53269)=0):NEXTI:RETURN
1410 :

1420 PRINT"(CLR)(DISH)(LGRN)(SHIFT-U)(SHIFT-*)(SHIFT-I)(SHIFT-*)(CBM-R)(SHIFT-*)(SHIFT--) (SHIFT--)(CBM-A)(SHIFT-*)(SHIFT--) (SHIFT--) (SHIFT-U)(SHIFT-*)(SHIFT-I)  BY
1430 PRINT"(SHIFT--) (SHIFT--) (SHIFT--) (CBM-Q)(SHIFT-*)(CBM-W)(CBM-Q) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)
1440 PRINT"(SHIFT-J)(SHIFT-*)(SHIFT-K) (SHIFT--) (SHIFT--) (SHIFT--)(CBM-Z)(SHIFT-*)(CBM-Z)(SHIFT-*)(CBM-Z)(SHIFT-*)(SHIFT-J)(SHIFT-*)(SHIFT-K)     (YEL)LUIGI TAVOLATO";CHR$(142)
1450 PRINT"(YEL)(CBM-A)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)
1460 PRINT"(SHIFT--)(GRN)(CBM-A)(SHIFT-*)(CBM-R)(SHIFT-*)(CBM-R)(SHIFT-*)(CBM-R)(SHIFT-*)(CBM-R)(SHIFT-*)(CBM-R)(SHIFT-*)(CBM-R)(SHIFT-*)(CBM-R)(SHIFT-*)(CBM-S)(YEL)(SHIFT--)
1470 PRINT"(SHIFT--)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(YEL)(SHIFT--)
1480 PRINT"(SHIFT--)(GRN)(CBM-Q)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(CBM-W)(YEL)(SHIFT--)
1490 PRINT"(SHIFT--)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(YEL)(SHIFT--)
1500 PRINT"(SHIFT--)(GRN)(CBM-Q)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(CBM-W)(YEL)(SHIFT--)
1510 PRINT"(SHIFT--)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(YEL)(SHIFT--)
1520 PRINT"(SHIFT--)(GRN)(CBM-Q)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(CBM-W)(YEL)(SHIFT--)
1530 PRINT"(SHIFT--)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(WHT)(SHIFT-Q)(GRN)(SHIFT--)(BLK)(SHIFT-Q)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(YEL)(SHIFT--)
1540 PRINT"(SHIFT--)(GRN)(CBM-Q)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(CBM-W)(YEL)(SHIFT--)
1550 PRINT"(SHIFT--)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(BLK)(SHIFT-Q)(GRN)(SHIFT--)(WHT)(SHIFT-Q)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(YEL)(SHIFT--)
1560 PRINT"(SHIFT--)(GRN)(CBM-Q)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(CBM-W)(YEL)(SHIFT--)
1570 PRINT"(SHIFT--)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(YEL)(SHIFT--)
1580 PRINT"(SHIFT--)(GRN)(CBM-Q)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(CBM-W)(YEL)(SHIFT--)
1590 PRINT"(SHIFT--)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(YEL)(SHIFT--)
1600 PRINT"(SHIFT--)(GRN)(CBM-Q)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-+)(SHIFT-*)(CBM-W)(YEL)(SHIFT--)
1610 PRINT"(SHIFT--)(GRN)(SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--) (SHIFT--)(YEL)(SHIFT--)
1620 PRINT"(SHIFT--)(GRN)(CBM-Z)(SHIFT-*)(CBM-E)(SHIFT-*)(CBM-E)(SHIFT-*)(CBM-E)(SHIFT-*)(CBM-E)(SHIFT-*)(CBM-E)(SHIFT-*)(CBM-E)(SHIFT-*)(CBM-E)(SHIFT-*)(CBM-X)(YEL)(SHIFT--)
1630 PRINT"(CBM-Z)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)
1640 PRINT"(CBM-A)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)
1650 PRINT"(SHIFT--) (LGRN)(RVS)P(OFF)(GRN)ASSO - (LGRN)(RVS)N(OFF)(GRN)UOVA PARTITA - (LGRN)(RVS)D(OFF)(GRN)EMO - (LGRN)(RVS)H(OFF)(GRN)ELP (YEL)(SHIFT--)
1660 PRINT"(CBM-Z)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)";:RETURN

