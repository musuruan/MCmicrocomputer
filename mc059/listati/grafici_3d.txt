10 GOSUB620:SCNCLR:GRAPHIC1,1:GRAPHIC0
20 O=320:V=200:SU=1
30 IX=6:IZ=3
40 L=(O/IX/2)
50 P=(V/IZ/3)
60 PRINTTAB(12)"  GRAFICI 3D "
70 PRINTTAB(52)"  BY LUCIANO "
80 PRINTTAB(48)"COLORE GRAFICO (2-16)";:INPUTCO
90 IFCO<2ORCO>16THENSCNCLR:GOTO60
100 FAST:TRAP800
110 REM **    CALCOLO...    **
120 IN=25
130 DIMG(L,P)
140 FORA=-P/2TOP/2
150 FORB=-L/2TOL/2
160 X=A*20/L:Z=B*20/P
170 REM **   LA  FUNZIONE   **
180 :
190 Y=INT(SIN(SQR(X*X+Z*Z+0.001))/(SQR(X*X+Z*Z+0.001))*75+15*1.3)/200
200 G(B+L/2,A+P/2)=Y*SU*V
210 NEXTB,A:COLOR1,CO
220 :
230 REM **       DRAW       **
240 FORZ=1TOP
250 XB=IX*Z
260 ZB=V/2+Z*IZ+IN*SU
270 XO=XB+IX
280 ZO=ZB-IZ-G(1,Z)
290 FORX=1TOL
300 XN=XB+X*IX
310 ZN=ZB-X*IZ-G(X,Z)
320 DRAW1,XO,ZO:DRAW1,XN,ZNTOXO,ZO
330 XO=XN:ZO=ZN
340 NEXTX,Z
350 :
360 REM **       DRAW       **
370 FORX=1TOL
380 XB=IX*X+P*IX
390 ZB=V/2-X*IZ+P*IZ+IN*SU
400 ZO=ZB-IZ-G(X,P-1)
410 XO=XB-IX
420 FORZ=0TOP-1
430 XN=XB-Z*IX
440 ZN=ZB-Z*IZ-G(X,P-Z)
450 DRAW1,XO,ZO:DRAW1,XN,ZNTOXO,ZO
460 XO=XN:ZO=ZN
470 NEXTZ,X:SLOW
480 FORI=0TO25:GRAPHIC2,0,I:FORA=0TO20:NEXTA,I
490 REM ** FERMA L'IMMAGINE **
500 COLOR1,2:CHAR1,13,24,"PREMI UN TASTO"
510 GETKEYA$
520 REM **       MENU       **
530 PRINT"(CLR)(DOWN)(DOWN)(DOWN)(DOWN)    MENU"
540 PRINT:PRINT"    (RVS) 1 (OFF) MODIFICA FUNZIONE"
550 PRINT:PRINT"    (RVS) 2 (OFF) SALVA GRAFICO SU DISCO"
560 PRINT:PRINT"    (RVS) 3 (OFF) CARICA GRAFICO DA DISCO"
570 PRINT:PRINT"    (RVS) 4 (OFF) FINE"
580 FORI=25TO0STEP-1:GRAPHIC2,0,I:FORA=0TO20:NEXTA,I:GRAPHIC0
590 GETKEYA$:A=VAL(A$):IFA<1ORA>4THEN590
600 ONAGOTO830,860,960,1070
610 REM **   INTESTAZIONE   **
620 COLOR0,1:COLOR4,1
630 DOUNTILJ=6:READA$:A$="          "+A$:PRINT"(CLR)(WHT)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)":J=J+1
640 COLOR5,13:PRINT"(UP)"A$:GOSUB770
650 COLOR5,16:PRINT"(UP)"A$:GOSUB770
660 COLOR5,2:PRINT"(UP)"A$:GOSUB780:IFJ=1ORJ=4THENGOSUB780
670 COLOR5,16:PRINT"(UP)"A$:GOSUB770
680 COLOR5,13:PRINT"(UP)"A$:GOSUB770
690 COLOR5,12:PRINT"(UP)"A$:GOSUB770
700 LOOP:SCNCLR:COLOR5,2:GOTO790
710 DATA "    UZZO LUCIANO
720 DATA "      SOFTWARE
730 DATA "      PRESENTA
740 DATA "     GRAFICI 3D
750 DATA "  WRITTEN IN  1986
760 DATA "  BY  UZZO LUCIANO
770 SLEEP.5:RETURN
780 SLEEP2:RETURN
790 RETURN
800 COLOR0,12:COLOR4,14:COLOR5,14
810 SLOW:SCNCLR:GRAPHIC0:PRINT"(DOWN) ERROR:"
820 END
830 REM **  MODIFICA FUNZ.  **
840 SCNCLR:PRINT"MODIFICA LA FUNZIONE E DAI IL RUN"
850 LIST170-199:END
860 REM ** SALVA IL GRAFICO **
870 SCNCLR:PRINT"INSERISCI UN DISCO E PREMI UN TASTO"
880 GETKEYA$
890 CATALOG"GG*"
900 PRINT:PRINT"NOME DEL GRAFICO (OMETTI LE 2 "CHR$(34)"G"CHR$(34)")"
910 OPEN1,0:INPUT#1,VN$:CLOSE1
920 IFLEN(VN$)>16THENPRINT"(UP)(UP)";:GOTO900
930 NN$="GG"+VN$
940 KEY1,"S"+CHR$(34)+NN$+CHR$(34)+",8,1C00,3FFF"+CHR$(13)+"X"+CHR$(13)+"GOTO480(WHT)"+CHR$(13)
950 PRINT:PRINT"PREMI F1 PER SALVARE(BLK)":MONITOR
960 REM **  CARICA GRAFICO  **
970 SCNCLR:PRINT"INSERISCI UN DISCO E PREMI UN TASTO"
980 GETKEYA$
990 CATALOG"GG*"
1000 PRINT:PRINT"NOME DEL GRAFICO (OMETTI LE 2 "CHR$(34)"G"CHR$(34)")"
1010 OPEN1,0:INPUT#1,VN$:CLOSE1
1020 IFLEN(VN$)>16THENPRINT"(UP)(UP)";:GOTO900
1030 NN$="GG"+VN$
1040 KEY1,"L"+CHR$(34)+NN$+CHR$(34)+",8"+CHR$(13)+"X"+CHR$(13)+"GOTO1060(WHT)"+CHR$(13)
1050 PRINT:PRINT"PREMI F1 PER CARICARE(BLK)":MONITOR
1060 GOTO480
1070 REM **   RESET SYSTEM   **
1080 COLOR0,12:COLOR4,14:COLOR5,14
1090 SYS16384

