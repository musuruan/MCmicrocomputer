1 REM *****************************
2 REM **                         **
3 REM **  GIOCO DEL  S I M O N   **
4 REM **                         **
5 REM *****************************
6 :
10 PRINT"{SC}":GOSUB 19000:REM PRESENTAZIONE
120 PRINT"{SC}"
140 PRINT:PRINT:PRINT:PRINT:PRINT:PRINT
150 PRINT"{RV}      INSERIRE GRADO DI DIFFICOLTA'     {RO}{CD}"
160 PRINT"{RV}                 (1-5)                  {RO}"
170 GET A: IF A<1 OR A>5 THEN 170
180 B=3+4*A
181 FOR I=1 TO B
182 C$=STR$(INT(RND(1)*4)+1):C$=MID$(C$,2,1)
183 IF I=1 THEN Z$=Z$+C$:GOTO  190
184 IF C$=Q$OR C$=NN$THEN GOTO 182
189 Z$=Z$+C$
190 Q$=C$
191 IF I>=2 THEN NN$=MID$(Z$,I-1,1)
192 NEXT I
194 PRINT"{SC}":REM DISEGNO CASELLE
195 PRINT:PRINT:PRINT
196 PRINT"{LB}"
200 PRINT"   {LR}{SHIFT-U}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-I}  {GN}{SHIFT-U}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-I}  {YL}{SHIFT-U}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-I}  {BK}{SHIFT-U}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-I} "
210 PRINT"   {LR} *****   {GN} *****   {YL} *****   {BK} *****  "
220 PRINT"   {LR} *****   {GN} *****   {YL} *****   {BK} *****  "
230 PRINT"   {LR} *****   {GN} *****   {YL} *****   {BK} *****  "
240 PRINT"   {LR} *****   {GN} *****   {YL} *****   {BK} *****  "
250 PRINT"   {LR} *   *   {GN} *   *   {YL} *   *   {BK} *   *  "
260 PRINT"   {LR} * 1 *   {GN} * 2 *   {YL} * 3 *   {BK} * 4 *  "
270 PRINT"   {LR} *   *   {GN} *   *   {YL} *   *   {BK} *   *  "
280 PRINT"   {LR} *****   {GN} *****   {YL} *****   {BK} *****  "
290 PRINT"   {LR} *****   {GN} *****   {YL} *****   {BK} *****  "
300 PRINT"   {LR} *****   {GN} *****   {YL} *****   {BK} *****  "
310 PRINT"   {LR}{SHIFT-J}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-K}  {GN}{SHIFT-J}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-K}  {YL}{SHIFT-J}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-K}  {BK}{SHIFT-J}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-K}"
311 PRINT:PRINT
312 PRINT "   {SHIFT-U}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-I}  {SHIFT-U}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-I}  {SHIFT-U}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-I}  {SHIFT-U}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-I}"
313 PRINT "   {SHIFT--}     {SHIFT--}  {SHIFT--}     {SHIFT--}  {SHIFT--}     {SHIFT--}  {SHIFT--}     {SHIFT--}"  
314 PRINT "   {SHIFT-J}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-K}  {SHIFT-J}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-K}  {SHIFT-J}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-K}  {SHIFT-J}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-*}{SHIFT-K}"
320 REM INIZIO DEL GIOCO
325 FOR K=1 TO B
326 FOR LL=1TO 300:NEXT
330 FOR I=1 TO K
335 X$=MID$(Z$,I,1):E=200  :GOSUB 60000
337 NEXT I
340 FOR I=1 TO K
341 TI$="000000"
350 GET L$
360 IF TI$>"000004"THEN GOTO 61000
361 IF L$=""GOTO 350
370 IF L$<>MID$(Z$,I,1)THEN GOTO 61000
371 X$=L$:E=20:GOSUB 60000
380 NEXT I
390 NEXT K
400 PRINT"{HM}":PRINT"{RV}                HAI VINTO           "
1030 GOSUB63100: END
19000  PRINT"{SC}":REM PRESENTAZIONE
19002  POKE 53281,9:POKE 53280,5
20000 PRINTTAB(240)
20001 PRINT"{GN} *****   *   *     *   ******  *     *"
20010 PRINT" *       *   **   **   *    *  **    *"
20020 PRINT" *       *   * * * *   *    *  * *   *"
20030 PRINT" *****   *   *  *  *   *    *  *  *  *"
20040 PRINT"     *   *   *     *   *    *  *   * *"
20050 PRINT"     *   *   *     *   *    *  *    **"
20060 PRINT" *****   *   *     *   ******  *     *"
20061 PRINT
20064 S=54272:FOR L=0 TO 24:POKE S+L,0:NEXT
20065  POKE S+1,130:POKE S+5,9:POKE S+15,30:POKE S+24,15
20066 FOR L=1TO 15:POKE S+24,L:POKE S+4,21:FOR T=1 TO 50:NEXT
20067 POKES+4,20:NEXT:RETURN
60000 REM ATTIVAZIONE TASTO
60010 G=VAL(X$)-1:N=1828+9*G:F=50*(G+1)
60040 FOR Q=NTON+3
60041 IF Q=N THEN POKE Q,32:GOTO 60043
60042 POKE Q,166
60043 NEXT Q
60045 GOSUB 63000
60050 FOR ZZ=1 TO E
60060 NEXT ZZ
60070 FOR Q=NTON+3
60071 IF Q=N THEN POKE Q,32:GOTO 60073
60072 POKE Q, 32
60073 NEXT Q
60080 RETURN
61000 REM ROUTINE IN CASO DI SCONFITTA
61001 PRINT"{HM}":PRINT"{RV}              HAI PERSO                "
61010 PRINT"{RV}        L' ESATTA SEQUENZA E' :        "
61012 FOR LL=1 TO 999:NEXT
61013 E=300
61030 FOR I=1 TO K
61040 X$=MID$(Z$,I,1):GOSUB 60000
61070 NEXT I
61071 FOR F=255 TO 1 STEP-2:GOSUB 63000:NEXT
61091 S=54272:FOR L=0TO 24:POKE S+L,0:NEXT
61092 POKE S+0,240:POKES+1,33: POKE S+1,100
61093 POKE S+5,219:POKE S+15,28:POKE S+24,15:POKE S+4,19
61095 FOR T=1 TO 100:NEXT
61097 POKE S+4,18:FOR T=1 TO 20:NEXT:POKE S+24,0
61098 GET A$:IFA$=""GOTO 61098
61099 PRINT"{SC}"
61100 END
63000 V=54272:REM GENERAZIONE SUONO
63010 POKE V+1,F:POKE V+24,10:POKE V+4,33:POKE V+5,20
63020 POKEV+6,8:POKEV+4,32:RETURN
63100 E=300:REM ROUTINE IN CASO DI VITTORIA
63101 FOR LL=1 TO 999: NEXT
63104 FOR I=1 TO B
63105 X$=MID$(Z$,I,1):GOSUB 60000
63106 NEXT I
63110 S=54272
63116 FOR L=0 TO 24:POKE S+L,0:NEXT
63120 POKE S,240:POKES+1,33:POKE S+5,8
63121 POKE S+22,104:POKE S+23,1:POKE S+24,79
63126 FOR N=1 TO 30:POKE S+4,129
63130 FOR T=1 TO 20:NEXT:POKE S+4,128
63135 NEXT
63151 ZZ=0:A$=""
63152 FOR L=STO S+24:POKE L,0:NEXT
63153 POKE S+5,88:POKE S+6,195
63154 POKE S+24,8
63155 READ HF,LF,DR
63156 IF HF<0THEN RESTORE:ZZ=ZZ+1:HF=0:IF ZZ=2 THEN 63164
63157 POKE S+1,HF:POKE S+4,33
63158 FOR T=1 TO DR:NEXT
63159 POKE S+4,32:FOR T=1 TO 50:NEXT
63160 GOTO 63155
63161 DATA 25,177,250,28,214,250,25,177
63162 DATA 250,25,177,250,25,177,125,28,214,125
63163 DATA 32,94,750,25,177,250,28,214
63164 DATA 250,19,63,250,19,63,250,19,63,250
63165 DATA 21,154,63,24,63,63,25,177
63166 DATA 25,24,63,125,19,63,250,-1,-1,-1
63167 GET A$:IF A$=""GOTO 63167


