100 GOTO108
102 IFDS=0THENRETURN:ELSEGOSUB104:CHAR1,0,22,DS$:CLOSE2:CLOSE3:CLOSE4:END
104 SOUND1,3000,30:RETURN
106 :
108 FAST:DIMX$(10),FK(7):NX=1:RT$=CHR$(13):ES$=CHR$(27)
110 PRINT"(HOME)(HOME)(CLR)(SWLC)(GRN)
112 CHAR1,10,10,"0) MORSE FILE MAKER.
114 CHAR1,10,14,"1) PRINT FILE.
116 GETKEYA$:IFA$<"0"ORA$>"1"THEN116
118 FF=VAL(A$):IF FFTHENBEGIN
120 : FORJ=0TO7:READFK$(J):FK(J)=-1:NEXT:YY$="(RVS) YES (OFF) NO ":YN$=" YES (RVS) NO (OFF)"
122 : CHAR1,10,22,"PRINTER OR SCREEN: (P/S)"
124 : GETKEYA$:IFA$<>"P"ANDA$<>"S"THEN124
126 : IFA$="P"THENDV=4:ELSEDV=3
128 : OPENDV,DV,15:CLOSEDV
130 : IFSTTHENGOSUB104:CHAR1,10,22,ES$+"Q PRINTER NOT READY... ",1:GETKEYA$:RUN
132 : SCNCLR
134 : FORJ=2TO16STEP2:J1=J/2-1
136 :   CHAR1,10,J,FK$(J1)+YN$
138 : NEXT
140 : CHAR1,10,22,"(0/7):SELEZIONA, RETURN:STAMPA, ESCAPE:ESCE."
142 : DO
144 :   DO:GETKEYA$:LOOP UNTILINSTR("01234567"+RT$+ES$,A$)
146 :   IFA$=ES$THENRUN:ELSEIFA$=RT$THENEXIT
148 :   A=VAL(A$):FK(A)=NOT(FK(A)):IFFK(A)THENA$=YN$:ELSEA$=YY$
150 :   CHAR1,41,2+A*2,A$
152 : LOOP
154 : OPENDV,DV:IFDV=4THENCHAR1,10,22,ES$+"QPRINTING...":ELSESCNCLR
156 BEND
158 :
160 SCNCLR:RESTORE 322
162 FI$="A.":N=3:NN=0:T=0
164 READA$:DO
166 ALFA$=ALFA$+A$
168 : X$=ALFA$:GOSUB218
170 READ A$:IFA$="END"THENEXIT
172 LOOP
174 :
176 FI$="N.":N=3:NN=0:T=1
178 READA$:DO
180 NUM$=NUM$+A$
182 : X$=NUM$:GOSUB218
184 READ A$:N=N+3:IFA$="END"THENEXIT
186 LOOP
188 :
190 FI$="P.":N=3:NN=0:T=2
192 READA$:DO
194 PUNT$=PUNT$+A$
196 : X$=PUNT$:GOSUB218
198 READ A$:IFA$="END"THENEXIT
200 LOOP
202 :
204 X$=ALFA$           :FI$="AA." :N=15:NN=0:T=3:GOSUB218
206 X$=ALFA$+NUM$      :FI$="AN." :N=15:NN=0:T=4:GOSUB218
208 X$=ALFA$+PUNT$     :FI$="AP." :N=15:NN=0:T=5:GOSUB218
210 X$=NUM$+PUNT$      :FI$="NP." :N=15:NN=0:T=6:GOSUB218
212 X$=ALFA$+NUM$+PUNT$:FI$="ANP.":N=30:NN=0:T=7:GOSUB218
214 CLOSEDV:END
216 :
218 IF FK(T) THEN222
220 IFFFTHENGOSUB226:ELSEGOSUB258
222 RETURN
224 :
226 FORN0=1TON
228 : FF$=FI$+MID$(STR$(NN),2)
230 : DOPEN#2,(FF$):GOSUB102
232 : PRINT#DV,X$
234 : PRINT#DV,FF$
236 : DO
238 :   INPUT#2,A$:SA=ST:PRINT#DV,A$
240 : LOOPUNTILSA
242 : PRINT#DV
244 : NN=NN+1:NX=NX+1
246 : CLOSE2:GOSUB102
248 : IFDV=4THENBEGIN:CHAR1,10,22,ES$+"QUN TASTO PER CONTINUARE, ESCAPE:ESCE."
250 :   POKE208,0:GETKEYA$:IFA$=ES$THENEND:ELSECHAR1,10,22,ES$+"QPRINTING..."
252 : BEND
254 NEXT
256 RETURN
258 :
260 A=LEN(X$):PRINT"(RVS)"X$
262 FORN0=1TON
264 : FORJ=1TO10:X$(J)="":NEXT
266 :   FF$=FI$+MID$(STR$(NN),2)
268 :   PRINTFF$,"FILE N."NX
270 :   DOPEN#2,(FF$),W:GOSUB102
272 : FORJ0=1TO10    :REM LINEA
274 : : FORJ1=1TO5   :REM PAROLA
276 : :   FORJ2=1TO5 :REM CARATTERE
278 : :   X=INT(RND(0)*A)+1:A$=MID$(X$,X,1)
280 : :     X$(J0)=X$(J0)+A$
282 : :     IFJ2=5ANDJ1<5THENX$(J0)=X$(J0)+CHR$(32)
284 : :   NEXT
286 : : NEXT
288 : : PRINT#2,CHR$(34)X$(J0)CHR$(34)
290 : : PRINTJ0,"(RVS)"X$(J0)
292 : NEXT:PRINT
294 : NN=NN+1:NX=NX+1
296 : CLOSE2:GOSUB102
298 NEXT
300 RETURN
302 :
304 DATA "0) ALFABETICI PARZIALI        "
306 DATA "1) NUMERICI PARZIALI          "
308 DATA "2) PUNTEGGIATURA PARZIALE     "
310 DATA "3) ALFABETICI TOTALI          "
312 DATA "4) ALFABETICI & NUMERICI      "
314 DATA "5) ALFABETICI & PUNTEGGIATURA "
316 DATA "6) NUMERICI & PUNTEGGIATURA   "
318 DATA "7) ALFAB., NUMERICI & PUNTEGG."
320 :
322 DATA AEIOU,BC,DF,GH,JK,LM,NP,QR,ST,VWZ,END
324 DATA 01234,56789,END
326 DATA "'(,","-./",":=?",END

