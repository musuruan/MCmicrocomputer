1 REM**********************
2 REM****     DELETE   ****
3 REM****  DI A.TONTINI****
4 REM**********************
10 F=PEEK(55)+PEEK(56)*256-6:H=INT(F/256):L=F-H*256:POKE51,L:POKE55,L:POKE52,H:POKE56,H
20 IP=PEEK(45)+PEEK(46)*256-2:H=INT(IP/256):L=IP-H*256:POKEF+2,L:POKEF+4,L:POKEF+3,H:POKEF+5,H
30 PRINT"(CLR)(DOWN)*PREPARA LA CASSETTA":PRINT" CON IL PROGRAMMA":PRINT" IN CUI DEVI ELIMINA-"
32 PRINT" RE DELLE ISTRUZIONI,":PRINT"*POI PREMI'F1'."
34 GETA$:IFA$=""THEN34
35 IFASC(A$)=133THEN40
36 GOTO34
40 PRINT"(DOWN)(DOWN)(WHT)?C(SHIFT-H)(31):P(SHIFT-O)43,";PEEK(F+4);":P(SHIFT-O)44,";PEEK(F+5);":L(SHIFT-O)"
50 PRINT"(WHT)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)P(SHIFT-O)43,1:P(SHIFT-O)44,18:R(SHIFT-U)100(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)":POKE198,10:POKE631,13:POKE632,13:END
100 F=PEEK(55)+PEEK(56)*256:IP=PEEK(F+2)+PEEK(F+3)*256
105 PRINT"(CLR)(DOWN)(DOWN) *ELIMINAZIONE":PRINT"  ISTRUZIONI":PRINT:INPUT"  DALLA NUMERO";P
110 PRINT:INPUT"   ALLA NUMERO";U:PRINT"(CLR)(DOWN)(DOWN) *ATTENDERE,":PRINT"  PREGO!"
120 POKEF+1,U/256:POKEF,U-PEEK(F+1)*256
130 E=PEEK(IP+2)+PEEK(IP+3)*256:IFE<PTHEN150
145 GOTO190
150 FORI=4TO999:IFPEEK(IP+I)=0THENIP=IP+I+1:I=999:NEXT:GOTO130
170 NEXT
190 POKEF+3,IP/256:POKEF+2,IP-PEEK(F+3)*256
200 F=PEEK(55)+PEEK(56)*256:IP=PEEK(F+2)+PEEK(F+3)*256:U=PEEK(F)+PEEK(F+1)*256
210 PRINT"(CLR)(DOWN)(DOWN)(WHT)P(SHIFT-O)43,";PEEK(F+4);":P(SHIFT-O)44,";PEEK(F+5);"(BLU)":PRINT:PRINT
220 FORK=1TO8
230 E=PEEK(IP+2)+PEEK(IP+3)*256:IFE>UTHENK=8:NEXT:S=1:GOTO500
260 PRINTE
270 FORI=4TO999:IFPEEK(IP+I)=0THENIP=IP+I+1:I=999:NEXT:GOSUB900:NEXT:GOTO500
280 NEXT
500 PRINT"P(SHIFT-O)43,1:P(SHIFT-O)44,18:";:IFS=1THENPRINT"R(SHIFT-U)1000":GOTO510
505 PRINT"R(SHIFT-U)200"
510 POKE198,10:FORJ=0TO9:POKE631+J,13:NEXT
520 PRINT"(HOME)";:END
900 IFPEEK(IP+1)=0THENS=1:GOTO500
910 :RETURN
1000 F=PEEK(55)+PEEK(56)*256
1005 PRINT"(CLR)(DOWN)(DOWN) *VUOI ELIMINARE":PRINT"  QUALCHE ALTRA":PRINT"  ISTRUZIONE?"
1010 PRINT:INPUT"  S/N";R$
1020 IFR$="S"THENPOKEF+2,PEEK(F+4):POKEF+3,PEEK(F+5):RUN100
1030 IFR$="N"THENPOKE43,PEEK(F+4):POKE44,PEEK(F+5):END
1040 GOTO1005

