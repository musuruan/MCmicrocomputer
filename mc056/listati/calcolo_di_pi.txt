2 PRINT"(CLR)"
5 PRINT"  CALCOLO DI ~,CON IL NUMERO DI CIFRE     ESATTE DESIDERATE":PRINT
10 INPUT"QUANTE CIFRE DECIMALI ESATTE";N
12 REM ===================== CALCOLI == PRELIMINARI ============================
14 PR=9
16 G=INT((PR-1)/2)
18 P=INT(10^G)
20 N1=N+G
22 M=INT(N1/G)+2:IF M=N1/G+2 THEN M=M-1
24 M1=M+1 :CE=G*(M-2)
26 Y=LOG(5)/LOG(10):W=N1/(2*Y):VF=INT(W)
28 Y1=LOG(239)/LOG(10):W1=N1/(2*Y1)
30 WF=INT(W1+1):PRINT"VF="VF:REMPRINT"WF="WF
32 TP=(M*VF)/12:SS=INT(TP)+5
33 IF N>480 AND N<540 THEN TP= 3600+4*(N-480):GOTO 36
34 IF TP>3600 THEN TP=(M*VF)/14
36 IF TP>3600 THEN HP=INT(TP/3600):MP=(TP/3600-HP)*60:IM=INT(MP)+1
38 IF HP>0 THEN PRINT"DURATA PRESUNTA DI CALCOLO:"HP"(CBM-N)(CBM-S) "IM"' ":GOTO 46
40 IF TP>60 THEN PP=INT(TP/60):SP=(TP/60-PP)*60 :IS=INT(SP)+1
42 IF TP>60 THEN PRINT"DURATA PRESUNTA DI CALCOLO:"PP"' "IS"''":GOTO 46
44 PRINT"DURATA PRESUNTA DI CALCOLO:"SS"SEC."
46 T1=TI
47 PRINT:PRINT" VF-V =":PRINTTAB(10)"(UP)   (LEFT)(LEFT)(LEFT)(LEFT)(LEFT)"VF,
48 DIM A(M1),B(M1),C(M1+10),D(M1)
50 REM ===================== CALCOLO == DI == 16/5 =============================
52 A(1)=3  :A(2)=.2*P
54 REM ===================== CALCOLO DI == 4/239 ===============================
56 B(1)=4 :B=239
58 FOR K=1 TO M: Q=INT(B(K)/B): R=B(K)-Q*B: B(K)=Q: B(K+1)=B(K+1)+R*P
60 NEXT K
62 REM ===================== CALCOLO == DI == 16/5-4/239 =======================
65 X=A(M)
70 FOR K=M TO 1 STEP-1
75 IF X>=B(K) THEN 90
80 C(K)=X+P-B(K): X=A(K-1)-1
85 GOTO 95
90 C(K)=X-B(K): X=A(K-1)
95 NEXT K
98 REM ********************** INIZIO ** CALCOLI ** TERMINI ** SOMMATORIA *******
100 Z=1:K1=1:K2=1:K3=1:PRINT
105 A=25: B=57121
110 V=V+1:Z=-Z:PRINTTAB(10)"(UP)   (LEFT)(LEFT)(LEFT)(LEFT)(LEFT)"VF-V
115 REM ===================== CALCOLO == DI == A(K)/5^2 ========================
125 A1=0:B1=0:D1=0
130 FOR K=K1 TO M
132 IF A1=0 AND A(K)<>0 THEN K1=K:A1=1
137 Q=INT(A(K)/A):R=A(K)-Q*A: A(K)=Q: A(K+1)=A(K+1)+R*P
138 IF A(K+1)=0 THEN K=M
145 NEXT K
165 REM ===================== CALCOLO == DI == B(K)/239^2 ======================
170 FOR K=K2 TO M
175 IF B1=0 AND B(K)<>0 THEN K2=K:B1=1
178 Q=INT(B(K)/B):R=B(K)-Q*B: B(K)=Q: B(K+1)=B(K+1)+R*P
185 NEXT K
205 C =2*V+1
207 IF V>WF THEN 320
210 REM ===================== CALCOLO == DI == D(K)=A(K)-B(K) ==================
212 D=A(M)
220 FOR K=M  TO K3 STEP-1
225 IF D>=B(K) THEN 275
245 D(K)=D+P-B(K)
255 D=A(K-1)-1
260 GOTO 280
275 D(K)=D-B(K)
278 D=A(K-1)
280 NEXT K
300 REM ===================== CALCOLO == DI == D(K)/(2*V+1) ====================
305 FOR K=K3 TO M
307 IF D1=0 AND D(K)<>0 THEN K3=K:D1=1
310 Q=INT(D(K)/C):R=D(K)-Q*C: D(K)=Q: D(K+1)=D(K+1)+R*P
312 NEXT K
315 GOTO 370
320 REM ===================== CALCOLO == DI == A(K)/(2*V+1) ====================
323 FOR K=K3 TO M :D(K)=A(K):NEXT
325 FOR K=K3 TO M
327 IF D1=0 AND D(K)<>0 THEN K3=K:D1=1
330 Q=INT(D(K)/C):R=D(K)-Q*C: D(K)=Q: D(K+1)=D(K+1)+R*P
332 NEXT K
360 REM ===================== CALCOLO == DI == C(K)=C(K)-D(K) ==================
370 IF Z=1 THEN 470
380 FOR K=M   TO K3 STEP -1
390 IF C(K)>=D(K) THEN 430
400 C(K)=C(K)+P-D(K): IF K=1 THEN 440
410 C(K-1)=C(K-1)-1
420 GOTO 440
430 C(K)=C(K)-D(K)
440 NEXT K
450 REM FOR K=1 TO M :PRINT C(K),:NEXT
460 GOTO 540
465 REM ===================== CALCOLO == DI == C(K)=C(K)+D(K) ==================
470 FOR K=M   TO K3 STEP -1
480 C(K)=C(K)+D(K)
490 IF C(K)<P THEN 520
500 C(K)=C(K)-P
510 C(K-1)=C(K-1)+1
520 NEXT K
530 REM FOR K=1 TO M :PRINT C(K),:NEXT
540 IF V=VF THEN 555
550 GOTO 110
555 C$=" ~ = 3.": D$="~":PRINT
558 REM ********************* PRESENTAZIONE ** RISULTATI ***********************
565 PRINT" ~ = 3.",
570 FOR L=2 TO M-1
575 IF C(L)<10 THEN PRINT "(RGHT)(RGHT)(RGHT)"C(L)"(LEFT)(LEFT)(LEFT)(LEFT)(LEFT)000",:GOTO 595
580 IF C(L)<100 THEN PRINT "(RGHT)(RGHT)"C(L)"(LEFT)(LEFT)(LEFT)(LEFT)(LEFT)00",:GOTO 595
585 IF C(L)<1000 THEN PRINT "(RGHT)"C(L)"(LEFT)(LEFT)(LEFT)(LEFT)(LEFT)0",:GOTO 595
590 PRINT C(L),
595 NEXT L
600 T2=TI
605 REM ===================== CALCOLO == DURATA == EFFETTIVA ===================
610 PRINT:PRINT:PRINT"TEMPO IMPIEGATO:"
615 DT= (T2-T1)/60
620 IF DT>=3600 THEN 645
625 IF DT>=60 THEN 670
630 TR=INT(DT*100)/100+.01
635 PRINTTAB(16)"(UP)"TR"SEC."
640 GOTO 685
645 DH=DT/3600 :HO=INT(DH)
650 DM=(DH-HO)*60: MN=INT(DM)
655 S1=(DM-MN)*60: S2=INT(S1) +1
660 PRINTTAB(16)"(UP)"HO"(CBM-N)(CBM-S). "MN"MIN."S2"SEC."
665 GOTO 685
670 DM=DT/60:MN=INT(DM)
675 S1=(DM-MN)*60: S2=INT(S1)+1
680 PRINTTAB(16)"(UP)"MN"MIN. "S2"SEC."
685 PRINT:PRINT" "D$" RISULTA CALCOLATO CON"CE"CIFRE"
687 PRINT"   DECIMALI ESATTE DOPO LA VIRGOLA":PRINT
690 REM ********************* ISTRUZIONI ** PER ** STAMPANTE *******************
695 C(M)=0:PRINT
700 PRINT:PRINT"SI VUOLE LA STAMPA?"
705 PRINT"(SE SI,PREMERE TASTO S;                  ALTRIMENTI PREMERE TASTO N)"
710 GET A$:IF A$=""  THEN 710
715 IF A$= "N" THEN END
720 IF A$<>"S"THEN 710
725 TR$=STR$(TR):SC$=TR$+" SEC."
730 CE$=STR$(CE)
735 Q=0:H=0
740 OPEN1,4
745 OPEN2,4,2
750 OPEN3,4,1
770 B$="AAAAA"
775 PRINT#2,B$
780 PRINT#3,CHR$(10);
785 PRINT#3,"~= 3.";CHR$(141);
790 G$="     ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ 2ZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ "
795 PRINT#2,G$
800 FORJ=2 TO 10 : IF J>M-1 THEN 810
805 PRINT#3,C(J);
810 NEXT J
815 IF M<12 THEN PRINT#3,CHR$(141):         GOTO 870
820 PRINT#3,C(11)
825 F$="     ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ "
830 PRINT#2,F$
835 Q=Q+10 :IF Q>=M-2 THEN 870
840 FOR L=2 TO 10  :IF Q+L>M-1 THEN 850
845 PRINT#3,C(Q+L);
850 NEXT
855 IF M<Q+12 THEN PRINT#3,CHR$(141):         GOTO 870
860 PRINT#3,C(Q+11)
865 GOTO 835
870 PRINT#3,CHR$(10);CHR$(141)
875 PRINT#3,"~ RISULTA CALCOLATO CON";CE$" CIFRE DECIMALI ESATTE "
880 PRINT#3," DOPO LA VIRGOLA"
890 PRINT#3,CHR$(141)
900 IF DT>=60 THEN 920
910 PRINT#3," TEMPO IMPIEGATO:";SC$:  GOTO 970
920 MN$=STR$(MN):S2$=STR$(S2)
930 MT$=MN$+" MII. "+S2$+" SEC."
940 HO$=STR$(HO):HT$=HO$+" (SHIFT-H) ."+MT$
950 IF DT>=3600 THEN PRINT#3,"TEMPO IMPIEGATO:";HT$: GOTO 970
960 IF DT>=60 THEN PRINT#3,"TEMPO IMPIEGATO:";MT$
970 PRINT#3,CHR$(10);CHR$(141)
980 CLOSE1:CLOSE2:CLOSE3
985 PRINT" VUOI LA RISTAMPA ?":GOTO 705
990 END

