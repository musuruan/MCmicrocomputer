10 REM CLOCK PER C-128
20 REM BY MICHELE GALANTE 1988
30 X=160:Y=100:RX=60:RY=60
40 GRAPHIC1,1:WINDOW0,24,39,24
50 FAST:SCNCLR:CIRCLE1,X,Y,RX,RY,,,,1
60 FORA=0TO11:K=A*30+360:CIRCLE0,X,Y,RX*.9,RY*.9,K-1,K:X1=RDOT(0):Y1=RDOT(1):CIRCLE1,X,Y,RX,RY,K-1,K:DRAW1TOX1,Y1:NEXT:BB=1:SLOW
70 DO
80 O$=LEFT$(TI$,2):O=VAL(O$):IFO>12THENO=O-12
90 M$=MID$(TI$,3,2):M=VAL(M$)
100 S$=RIGHT$(TI$,2):S=VAL(S$):IFBB=1THENBB=0:O1=O:M1=M:S1=S:GOTO290
110 IFO>O1THEN290
120 IFM>M1THEN300
130 IFS>S1THEN310
140 GETA$:LOOPWHILEA$=""
150 IFA$="+"THENRX=RX+5:RY=RY+5:GOTO50
160 IFA$="-"THENRX=RX-5:RY=RY-5:GOTO50
170 IFA$="1"THENRX=RX+5:GOTO50
180 IFA$="2"THENRX=RX-5:GOTO50
190 IFA$="3"THENRY=RY+5:GOTO50
200 IFA$="4"THENRY=RY-5:GOTO50
210 IFA$="(LEFT)"THENX=X-5:GOTO50
220 IFA$="(RGHT)"THENX=X+5:GOTO50
230 IFA$="(UP)"THENY=Y-5:GOTO50
240 IFA$="(DOWN)"THENY=Y+5:GOTO50
250 IFA$=" "THENGRAPHIC2,,24:INPUT"ORA ";TI$:GRAPHIC1:GOTO50
260 IFA$="R"THENGRAPHIC2,,24:INPUT"RAGGI";RX,RY:GRAPHIC1:GOTO50
270 IFA$="P"THENGRAPHIC2,,24:INPUT"POSIZIONE";X,Y:GRAPHIC1:GOTO50
280 GOTO70
290 WIDTH2:A=30*O1+360:CIRCLE0,X,Y,RX*.5,RY*.5,A-1,A:DRAW0TOX,Y:O1=O:WIDTH1
300 WIDTH2:A=6*M1+360:CIRCLE0,X,Y,RX*.8,RY*.8,A-1,A:DRAW0TOX,Y:M1=M:WIDTH1
310 A=6*S1+360:CIRCLE0,X,Y,RX*.8,RY*.8,A-1,A:DRAW0TOX,Y:S1=S
320 WIDTH2:A=30*O1+360:CIRCLE0,X,Y,RX*.5,RY*.5,A-1,A:DRAW1TOX,Y
330 A=6*M1+360:CIRCLE0,X,Y,RX*.8,RY*.8,A-1,A:DRAW1TOX,Y:WIDTH1
340 A=6*S1+360:CIRCLE0,X,Y,RX*.8,RY*.8,A-1,A:DRAW1TOX,Y
350 CHAR1,16,24,O$+":"+M$+":"+S$:LOOP

