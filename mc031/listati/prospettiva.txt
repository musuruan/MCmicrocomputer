10 REM ********************************
11 REM PROGRAMMA  PER DISEGNARE  FIGURE SOLIDE  IN PROSPETTIVA
12 REM CONFIGURAZIONE:COMMODORE 64 E REGISTRATORE
13 REM OPTIONAL:PLOTTER COMMODORE 1520
18 REM **********************************
20 POKE52,92:POKE56,92:POKE55,0:CLR:REM ABBASSA  PUNTATORI FINE BASIC
21 FORK=49152TO49222:READP:POKEK,P:NEXT:REM CARICA ROUTINE L.M.PER CLEAR GRAFICA
49 REM ***** SCELTE INIZIALI ***********
50 PRINT"(CLR)(DOWN)PROGRAMMA PER DISEGNARE FIGURE SOLIDE IN PROSPETTIVA"
51 PRINT"(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)PROCEDURE:"
52 PRINT"(DOWN)(DOWN)(DOWN)A : INPUT SPIGOLI"
53 PRINT"(DOWN)B : CUBO"
54 PRINT"(DOWN)C : TETRAEDRO"
55 PRINT"(DOWN)D : PIRAMIDE"
60 PRINT"(HOME)";:FORI=1TO23:PRINT"(DOWN)";:NEXT
61 PRINT"(RVS)PREMERE UNO DEI TASTI INDICATI":FORI=0TO200:NEXT
62 PRINT"(UP)PREMERE UNO DEI TASTI INDICATI":FORI=0TO200:NEXT
70 GETA$:IFA$=""GOTO60
71 IFA$="A"GOTO100
72 IFA$="B"GOTO5000
73 IFA$="C"GOTO6000
74 IFA$="D"GOTO7000
99 REM ******* PROCEDURA A:INPUT SPIGOLI *********
100 PRINT"(CLR)":INPUT"NOME DEL SOLIDO";NS$
103 PRINT:INPUT"NUMERO DEGLI SPIGOLI DA COLLEGARE";NS
105 DIMMS(NS-1,2):DIM MP(NS-1,1)
110 PRINT"SPIGOLI"TAB(13)"X"TAB(23)"Y"TAB(33)"Z"
120 FOR I=0 TO NS-1
130 PRINTI+1
131 PRINT"(UP)"SPC(11):INPUTX:MS(I,0)=X
132 PRINT"(UP)"SPC(21):INPUTY:MS(I,1)=Y
133 PRINT"(UP)"SPC(31):INPUTZ:MS(I,2)=Z
135 X=0:Y=0:Z=0
140 NEXT I
150 PRINT:PRINT:GOSUB950
151 IFA$="N"THEN CLR:GOTO100
199 REM **** INPUT PARAMETRI DELLA PROSPETTIVA DEL SOLIDO ******
200 PRINT"(CLR)(DOWN)PARAMETRI PER IL CALCOLO DELLA PROSPETTIVA DEL SOLIDO:";NS$
205 DI=0:TR=0:SC=0:AX=0:AY=0:AZ=0
210 PRINT"(DOWN)DISTANZA DEL PUNTO DI FUGA";:INPUT DI
212 PRINT"(DOWN)ALTEZZA DELL'OSSERVATORE";:INPUT TR
214 PRINT"(DOWN)SCALA";:INPUT SC
216 PRINT"(DOWN)ROTAZIONI (IN GRADI) ATTORNO AGLI ASSI:"
217 INPUT"ROT ASSE X";AX
218 INPUT"ROT ASSE Y";AY
219 INPUT"ROT ASSE Z";AZ
220 RX=AX*2*~/360:RY=AY*2*~/360:RZ=AZ*2*~/360
230 GOSUB950
240 IFA$="N"GOTO200
250 PRINT"(CLR)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)VUOI IL DISEGNO SU VIDEO O SU PLOTTER?"
255 PRINT"(DOWN)PREMERE V OPPURE P"
260 GETZ$:IFZ$=""GOTO260
270 IFZ$="V"THENPRINT"(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)ATTENDERE":GOTO310
280 IFZ$="P"THEN PRINT"(DOWN)(DOWN)(DOWN)SCEGLIERE IL COLORE DELLA PENNA DEL PLOTTER"
290 PRINT"(DOWN)NERO:0","BLU:1","VERDE:2","ROSSO:3"
295 INPUT CL%:IFCL%>3THEN295
297 PRINT"(DOWN)          ATTENDERE"
300 REM **** PROCEDURA DI CALCOLO DEI PUNTI DA DISEGNARE E MEMORIZZARE ****
310 FOR I=0 TO NS-1
320 XP=MS(I,0):YP=MS(I,1):ZP=MS(I,2)
330 ZZ=ZP+TR
340 XX=XP*COS(RZ)-YP*SIN(RZ):YY=XP*SIN(RZ)+YP*COS(RZ)
341 YT=YY*COS(RX)-ZZ*SIN(RX):ZT=YY*SIN(RX)+ZZ*COS(RX)
342 YY=YT:ZZ=ZT
344 XT=XX*COS(RY)-ZZ*SIN(RY):ZT=XX*SIN(RY)+ZZ*COS(RY)
346 XX=XT:YY=YT:ZZ=ZT
350 XX=XX/SC:YY=YY/SC:ZZ=ZZ/SC
360 MP(I,0)=DI*XX/(YY+DI):MP(I,1)=DI*ZZ/(YY+DI)
370 NEXTI
380 IFZ$="P"THEN700  :REM PASSAGGIO AL PLOTTER
399 REM ***** DISEGNO SUL VIDEO *******
400 GOSUB2000
405 X1=MP(0,0):Y1=MP(0,1)
406 I=1
407 X2=MP(I,0):Y2=MP(I,1)
410 FORP=0TO1 STEP.05
420 XD=(X2-X1)*P+X1:XD=XD+160
421 YD=(Y2-Y1)*P+Y1:YD=YD+100
422 CH=INT(XD/8):RO=INT((200-YD)/8):LN=(200-YD)AND7
423 BY=BASE+RO*320+8*CH+LN:BI=7-(XDAND7):POKEBY,PEEK(BY)OR(2^BI)
424 NEXTP
425 I=I+1:IFI>NS-1THEN500
426 X1=X2:Y1=Y2
427 GOTO407
450 CH=INT(XD/8):RO=INT((200-YD)/8):LN=(200-YD)AND7
451 BY=BASE+RO*320+8*CH+LN:BI=7-(XDAND7):POKEBY,PEEK(BY)OR(2^BI)
460 NEXTP
461 I=I+1:IFI>NS-1THEN500
463 X1=X2:Y1=Y2
465 GOTO407
500 POKE23552,0    :REM SEGNALE FINE DISEGNO
510 GETA$:IFA$=""GOTO510
520 GOSUB2500      :REM RITORNO IN PAGINA TESTO E MESSAGGI
550 PRINT"(CLR)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)PER TORNARE ALLE SCELTE INIZIALI:I"
555 PRINT"(DOWN)(DOWN)PER CAMBIARE I PARAMETRI DEL SOLIDO:P"
556 PRINT"(DOWN)(DOWN)PER TERMINARE PREMERE UN QUALUNQUE ALTRO TASTO"
560 GETA$:IFA$=""GOTO560
570 IF A$="I" THEN RUN
580 IFA$="P"THEN200
600 END
699 REM ***** PROCEDURA PER IL DISEGNO CON PLOTTER *****
700 OPEN4,6       :REM PRINT ASCII DATA
701 OPEN1,6,1     :REM PLOT X,Y DATA
702 OPEN2,6,2     :REM SELECT COLOR
709 REM ***** STAMPA PARAMETRI DELLA PROSPETTIVA *****
710 PRINT#4,"PROSPETTIVA ";NS$
715 PRINT#4,"PARAMETRI:"
720 PRINT#4,"DISTANZA PUNTO DI FUGA:";DI
725 PRINT#4,"ALTEZZA OSSERVATORE:";TR
730 PRINT#4,"SCALA:";SC
735 PRINT#4,"ROT X:";AX
740 PRINT#4,"ROT Y:";AY
745 PRINT#4,"ROT Z:";AZ
750 PRINT#4,CHR$(13)    :REM RETURN HOME
755 REM ***** DISEGNO *****
760 PRINT#2,CL%      :REM SET COLOR PENNA
765 PRINT#1,"H"      :REM PENNA IN HOME
770 PRINT#1,"M",240,-250:REM MUOVE LA PENNA ALLA NUOVA ORIGINE DEGLI ASSI
780 PRINT#1,"I"      :REM NUOVA ORIGINE FISSATA
790 X1=INT(MP(0,0)):Y1=INT(MP(0,1))
800 PRINT#1,"R",X1,Y1:REM SET PENNA AL PRIMO PUNTO
810 I=1
820 X2=INT(MP(I,0)):Y2=INT(MP(I,1))
830 PRINT#1,"J",X2,Y2
840 I=I+1:IFI>NS-1THEN870
850 GOTO820
870 PRINT#1,"H"      :REM PENNA IN HOME
880 PRINT#1,"M",0,-500:REM PENNA IN FONDO ALLO SPAZIO DEL DISEGNO
885 OPEN7,6,7        :REM RESET PLOTTER
890 PRINT#7
900 CLOSE4:CLOSE1:CLOSE2:CLOSE7
910 GOTO550
949 REM ***** LAMPEGGIO SCRITTE *****
950 PRINT"(HOME)";:FORI=1TO23:PRINT"(DOWN)";:NEXT
951 PRINT"(RVS)TUTTO O.K.? S/N(OFF)":FORI=0TO200:NEXT
953 PRINT"(UP)TUTTO O.K.? S/N":FORI=0TO200:NEXT
954 GET A$:IFA$=""GOTO950
955 RETURN
999 REM **** DATA PER SUBROUTINE IN L.M.*****
1000 DATA 173,24,208,9,120,141,24,208
1001 DATA 173,17,208,9,32,141,17,208
1002 DATA 169,0,133,251,169,96,133,252
1003 DATA 160,0,169,0,145,251,200,192
1004 DATA 0,208,249,230,252,169,128,197
1005 DATA 252,208,239,169,0,133,251,169
1006 DATA 92,133,252,160,0,169,3,145
1007 DATA 251,200,192,0,208,249,230,252
1008 DATA 169,96,197,252,208,239,96
2000 REM ***** SUBROUTINE DI ACCENSIONE E CLEAR GRAFICA HR *****
2001 POKE 56578,PEEK(56578)OR3
2002 POKE56576,PEEK(56576)AND252OR2
2003 BASE=24576:POKE53272,PEEK(53272)AND7OR120
2004 POKE 53265,PEEK(53265)OR32
2005 SYS49152
2010 RETURN
2500 REM ***** CHIUSURA MODO GRAFICO *****
2501 POKE 56578,PEEK(56578)OR3
2502 POKE 56576,PEEK(56576)AND252OR3
2503 POKE 53272,PEEK(53272)AND7OR16
2504 POKE 53265,PEEK(53265)AND223
2506 RETURN
5000 REM ***** PROCEDURA READ/DATA PER CUBO *****
5005 RESTORE:FORI=0TO70:READA:NEXT:NS$="CUBO"
5010 READNS:DIMMS(NS-1,2):DIMMP(NS-1,1)
5020 I=0
5030 J=0
5040 READA:MS(I,J)=A
5050 J=J+1
5060 IFJ<=2THEN5040
5070 I=I+1
5080 IFI<=NS-1THEN5030
5090 GOTO 200
5500 DATA 16,-40,-40,40,40,-40,40,40,40,40,-40,40,40
5501 DATA -40,-40,40,-40,-40,-40,40,-40,-40,40,40,-40
5502 DATA 40,40,40,40,40,-40,-40,40,-40,-40,40,40
5503 DATA -40,40,-40,-40,-40,-40,40,-40,-40,40,-40,40
6000 REM ***** PROCEDURA READ/DATA PER TETRAEDRO *****
6010 RESTORE:FORI=0TO119:READA:NEXT:NS$="TETRAEDRO"
6020 GOTO5010
6500 DATA8,-50,0,0,50,0,0,0,-50,0,-50,0,0
6501 DATA 0,0,-50,50,0,0,0,-50,0,0,0,-50
7000 REM ***** PROCEDURA READ/DATA PER PIRAMIDE *****
7010 RESTORE:FORI=0TO144:READA:NEXT:NS$="PIRAMIDE"
7020 GOTO5010
7500 DATA 11,0,0,50,-50,-50,-50,50,-50,-50,0,0,50
7501 DATA 50,50,-50,50,-50,-50,50,50,-50,-50,50,-50
7502 DATA 0,0,50,-50,50,-50,-50,-50,-50
9999 REM ***** USCITA MODO GRAFICO PER HELP *****
10000 POKE 56578,PEEK(56578)OR3
10001 POKE 56576,PEEK(56576)AND252OR3
10002 POKE 53272,PEEK(53272)AND7OR16
10003 POKE 53265,PEEK(53265)AND223
10004 POKE 53270,PEEK(53270)AND239
10005 RETURN

