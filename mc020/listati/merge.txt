1 P$=" PRIMO":GOSUB60:GOTO3
2 P$=" SECONDO":F=1:GOSUB60
3 UL=PEEK(55)+PEEK(56)*256-2:H=INT(UL/256):L=UL-H*256:POKE51,L:POKE55,L:POKE52,H:POKE56,H
4 GETA$:IFA$=""THEN4
5 IFASC(A$)=133ANDF=0THEN10
6 IFASC(A$)=133THEN40
7 GOTO4
10 GOSUB50:PRINT"(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(WHT)63999REM":PRINT:PRINT"P(SHIFT-O)43,1:P(SHIFT-O)44,18:R(SHIFT-U)2(BLU)(HOME)";:END
40 GOSUB50:PRINT"(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(WHT)P(SHIFT-O)43,1:P(SHIFT-O)44,18:R(SHIFT-U)80(BLU)(HOME)";:END
50 LH=UL+1:LL=UL:LO=PEEK(45)+PEEK(46)*256-2:H=INT(LO/256):L=LO-H*256
51 POKELH,H:POKELL,L
52 POKE198,10:FORI=0TO5:POKE631+I,13:NEXT
53 PRINT"(CLR)(DOWN)(DOWN)(WHT)P(SHIFT-O)43,";L;":P(SHIFT-O)44,";H;":L(SHIFT-O)(BLU)";:RETURN
60 PRINT"(CLR)(DOWN)(DOWN) CARICAMENTO":PRINTP$;" PROGRAMMA":PRINT"(DOWN) PREPARA LA CASSETTA":PRINT" POI PREMI'F1'":RETURN
80 UL=PEEK(55)+PEEK(56)*256-5:A=INT(UL/256):B=UL-A*256:POKE51,B:POKE55,B:POKE52,A:POKE56,A
90 POKEUL+3,PEEK(45):POKEUL+4,PEEK(46):GOTO100
95 FB=PEEK(55)+PEEK(56)*256+5:POKEFB-2,PEEK(45):POKEFB-1,PEEK(46)
100 DIMC$(75):FORI=0TO75:READC$(I):NEXT:PRINT"(CLR)(DOWN)":FB=PEEK(55)+PEEK(56)*256+5
110 POKE198,10:FORI=0TO4:POKE631+I,13:NEXT
120 PRINT"(WHT)P(SHIFT-O)43,";PEEK(FB+2);":P(SHIFT-O)44,";PEEK(FB+3);"(BLU)":PRINT:L=PEEK(FB)+PEEK(FB+1)*256
130 HE=PEEK(L+3):LE=PEEK(L+2):POKEFB-3,HE:POKEFB-4,LE:ET=LE+HE*256:PRINT:PRINTET;"(LEFT)";
140 FORK=4TO999:P=PEEK(L+K):IFP=0THEN200
150 IFP=34THENGOSUB500
160 IFP>127THEN180
175 PRINTCHR$(P);:NEXT
180 IFP=255THENPRINT"~";:NEXT
182 IFF=1THEN175
185 C$=C$(P-128):IFRIGHT$(C$,1)="#"THENC$=LEFT$(C$,LEN(C$)-2)+CHR$(ASC(MID$(C$,LEN(C$)-1,1))+128)
190 PRINTC$;:NEXT
200 L=L+K+2:K=999:NEXT:POKEFB-5,PEEK(L)
210 PRINT"(DOWN)(WHT)P(SHIFT-O)43,1:P(SHIFT-O)44,18:R(SHIFT-U)300(HOME)";:END
300 PRINT"(CLR)(DOWN)":FB=PEEK(55)+PEEK(56)*256+5
320 ET=PEEK(FB-4)+PEEK(FB-3)*256:CP=PEEK(FB-2)+PEEK(FB-1)*256
330 SC=PEEK(45)+PEEK(46)*256-CP:I2=PEEK(FB)+PEEK(FB+1)*256+SC:H2=INT(I2/256):L2=I2-H2*256
348 POKEFB,L2:POKEFB+1,H2:POKE198,10:FORI=0TO4:POKE631+I,13:NEXT
350 PRINT"(WHT)P(SHIFT-O)43,";L2;":P(SHIFT-O)44,";H2;"(BLU)":PRINT:PRINT:PRINTET:PRINT"(WHT)P(SHIFT-O)43,1:P(SHIFT-O)44,18:";
370 IFPEEK(FB-5)=0THENPRINT"(DOWN)(DOWN)(DOWN)R(SHIFT-U)390(BLU)(HOME)";:END
380 PRINT"R(SHIFT-U)95(BLU)(HOME)";:END
390 I=PEEK(55)+PEEK(56)*256+7:H=INT(I/256):L=I-H*256:POKE43,PEEK(I):POKE44,PEEK(I+1)
399 UB=PEEK(45)+PEEK(46)*256:POKEUB-8,0:POKEUB-7,0:PRINT"(CLR)":END
400 DATAEN#,FO#,NE#,DA#,IN#,INPUT,DI#,RE#,LE#,GO#,RU#,IF,RES#,GOS#,RET#,REM,ST#,ON,WA#
401 DATALO#,SA#,VE#,DE#,PO#,PR#,?,CO#,LI#,CL#,CM#,SY#,OP#,CLO#,GE#,NEW,TA#,TO,FN,SP#
402 DATATH#,NOT,STE#,+,-,*,/,^,AN#,OR,>,=,<,SG#,INT,AB#,US#,FR#,POS,SQ#,RN#,LOG,EX#
403 DATACOS,SI#,TAN,AT#,PE#,LEN,STR#,VA#,AS#,CH#,LEF#,RI#,MI#,GO
500 IFF=0THENF=1:RETURN
510 F=0:RETURN

