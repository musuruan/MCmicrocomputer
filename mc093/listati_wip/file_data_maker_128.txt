10 COLOR4,12:PRINT"(CLR)(GRY3)"TAB(8)"????????????????????????"
20 PRINTTAB(8)"(RVS)*** FILE DATA  MAKER ***(OFF)(DOWN)"
30 PRINTTAB(19)"BY(DOWN)":PRINTTAB(10)"MICHELE GALANTE 1989(LGRN)"
40 WINDOW0,10,39,24
50 INPUT"(CLR)NOME FILE";NF$
60 PRINT"(DOWN)DATA (RVS)D(OFF)ECIMALI O (RVS)E(OFF)SADECIMALI ?"
70 DO:GETKEYA$:LOOPUNTILINSTR("DE",A$)>0
80 IFA$="E"THEN110
90 PRINT"(DOWN)NUMERI CON LA STESSA LUNGHEZZA (S/N)?"
100 DO:GETKEYMO$:LOOPUNTILINSTR("SN",MO$)>0
110 INPUT"(DOWN)LUNGHEZZA DELLA RIGA BASIC";LU:IFLU>160THENPRINT"(UP)(UP)(UP)":GOTO110
120 PRINT"(DOWN)LOAD ADRESS (S/N)?"
130 DO:GETKEYLO$:LOOPUNTILINSTR("SN",LO$)>0
140 FAST:DOPEN#1,(NF$):NR=999:IFLO$="N"THENGET#1,X$,X$
150 IFA$="E"THEN250
160 GET#1,X$:ND=ND+1:CK=CK+ASC(X$):C$=MID$(STR$(ASC(X$)),2):IFMO$="S"THENP$="000":MID$(P$,4-LEN(C$))=C$: ELSEP$=C$
170 DO:NR=NR+1:PRINT"(CLR)"MID$(STR$(NR),2)"DATA";
180 LL=LU-4-LEN(STR$(NR)):A$=P$
190 DO:GET#1,X$:ND=ND+1:CK=CK+ASC(X$):C$=MID$(STR$(ASC(X$)),2):IFMO$="S"THENP$="000":MID$(P$,4-LEN(C$))=C$:ELSEP$=C$
200 IFLEN(A$)+LEN(P$)<LLTHENA$=A$+","+P$:ELSEEXIT
210 LOOPUNTILST
220 PRINTA$:PRINT"GOTO230":POKE208,10:POKE842,19:POKE843,13:POKE844,13:END
230 LOOPUNTILST
240 GOTO330
250 GET#1,X$:ND=ND+1:CK=CK+ASC(X$):P$=MID$(HEX$(ASC(X$)),3)
260 DO:NR=NR+1:PRINT"(CLR)"MID$(STR$(NR),2)" DATA";
270 LL=LU-4-LEN(STR$(NR)):A$=P$
280 DO:GET#1,X$:ND=ND+1:CK=CK+ASC(X$):P$=MID$(HEX$(ASC(X$)),3)
290 IFLEN(A$)+LEN(P$)<LLTHENA$=A$+","+P$ :ELSEEXIT
300 LOOPUNTILST
310 PRINTA$:PRINT"GOTO320": POKE208,10:POKE842,19: POKE843,13:POKE844,13:END
320 LOOPUNTILST
330 NR=NR+1:PRINT"(CLR)"MID$(STR$(NR),2)" REM CHECKSUM ="CK":NUMERO DATI ="ND
340 PRINT"GOTO350":POKE208,10:POKE842,19:POKE843,13:POKE844,13:END
350 CLOSE1:PRINT"(CLR)FINITO!":PRINT"(DOWN)(DOWN)I DATA SI TROVANO NELLE RIGHE ":PRINT"(DOWN) DA 1000 A"NR".":SLOW:END

