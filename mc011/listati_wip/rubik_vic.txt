10 GOSUB3250:POKE36879,13
20 PRINT"(HOME)(DOWN)(DOWN)(WHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)RUBIK CUBE(DOWN)" 
30 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)BY(DOWN)" 
40 PRINT"(RGHT)(RGHT)(RGHT)ANDREA DE PRISCO"
50 REM  *** INIZIALIZZAZIONE VARIABILI ***
60 DIMC$(2,2,2),A$(3),H$(5),D%(15)
70 FORI=0TO15:READD%(I):NEXT
80 FORI=0TO5
90 READH$(I):NEXT
100 FORX=0TO2:FORY=0TO2:FORZ=0TO2
110 C$(X,Y,Z)="  ":NEXTZ,Y,X
120 FORX=0TO2STEP2
130 FORY=0TO2STEP2
140 FORZ=0TO2STEP2
150 C$(X,Y,Z)="   ":NEXTZ,Y,X:FORT=1TO2500:NEXT:POKE36879,24
160 PRINT"(CLR)(DOWN)(BLU)VUOI INSERIRE UN CUBO GIA' MISCHIATO ?"
170 GETV$:IFV$=""THEN170
180 IFV$<>"S"ANDV$<>"N"THEN170
190 REM  *** INPUT CUBO MISCHIATO ***
200 H$="(PUR)ARANCIO(BLU)":PRINT"(CLR)(BLU)":GOSUB3400:FORN=0TO5:READZ$
210 PRINT"(HOME)(DOWN)(BLU)FACCIA COLOR "+H$
220 PRINT"ORIENTATA NORD-"+Z$:PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)"+MID$(H$,2,1)
230 F$=Z$:Z$=MID$(H$,2,1):GOSUB910:Z$=F$
240 POKE38205,T:POKE38206,T:POKE38227,T:POKE38228,T:POKE38249,T:POKE38250,T
250 F$=Z$:Z$=MID$(Z$,2,1):GOSUB910:Z$=F$
260 POKE38052,T:POKE38053,T:POKE38054,T
270 F=0:FORI=1TO9
280 READA:F=1-F
290 X=INT(A/100)
300 Y=INT((A/100-X)*10+.4)
310 Z=INT((A/10-INT(A/10))*10+.4)
320 IFI=9THENC$(X,Y,Z)=MID$(H$,2,1):GOTO420
330 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(RVS)";I;"(OFF)";
340 IFV$="N"THENX$=MID$(H$,2,1):GOTO370
350 GETX$:IFX$=""THEN350
360 GOTO3580
370 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(BLU)"+X$
380 READB:IFB=0THENC$(X,Y,Z)=LEFT$(C$(X,Y,Z),LEN(C$(X,Y,Z))-1)+X$:GOTO420
390 IFB=2THENC$(X,Y,Z)=X$+RIGHT$(C$(X,Y,Z),LEN(C$(X,Y,Z))-1):GOTO420
400 IFF=1THENC$(X,Y,Z)=LEFT$(C$(X,Y,Z),1)+X$+RIGHT$(C$(X,Y,Z),1):GOTO420
410 B=2:GOTO390
420 NEXT
430 H$=Z$:NEXT:GOSUB3250:GOSUB660
440 PRINT"(HOME)(DOWN) VUOI MISCHIARLO ?"
450 GETQ$:IFQ$=""THEN450
460 IFQ$="S"THENINPUT"(HOME)(DOWN) QUANTI MOVIMENTI";E:GOTO2440
470 IFQ$<>"N"THEN450
480 GOTO1450
490 REM  *****************************************
500 REM  *  QUESTA ROUTINE SERVE PER RUOTARE LE  *
510 REM  *        FACCIE DEL CUBO DI RUBIK       *
520 REM  *****************************************
530 BB$=N$:GOSUB1300:FORG=1TOLEN(BB$)STEP2:L=VAL(MID$(BB$,G,1)):D=VAL(MID$(BB$,G+1,1))
540 MO=MO+1:IF1<=LANDL<=3THEND=1-D
550 I=D*14:SS=2-4*D
560 FORR=DTO1-DSTEP1-2*D:GOSUB1050:TT$=C$(X,Y,Z)
570 FORJ=1TO3:A=X:B=Y:C=Z:GOSUB1050
580 N$=C$(X,Y,Z):P=(L+3*(L>2))*R:GOSUB970
590 C$(A,B,C)=N$
600 NEXT:N$=TT$:GOSUB970:C$(X,Y,Z)=N$
610 NEXT:NEXT
620 REM  *****************************************
630 REM  * QUESTA ROUTINE SERVE PER VISUALIZZARE *
640 REM  *   A COLORI FACCIE DEL CUBO DI RUBIK   *
650 REM  *****************************************
660 Z$=MID$(C$(0,0,0),3,1):GOSUB910:POKE38114,T
670 Z$=MID$(C$(0,0,1),2,1):GOSUB910:POKE38116,T
680 Z$=MID$(C$(0,0,2),3,1):GOSUB910:POKE38118,T
690 Z$=MID$(C$(0,1,2),1,1):GOSUB910:POKE38139,T
700 Z$=MID$(C$(0,2,2),3,1):GOSUB910:POKE38160,T
710 Z$=MID$(C$(0,2,1),2,1):GOSUB910:POKE38158,T
720 Z$=MID$(C$(0,2,0),3,1):GOSUB910:POKE38156,T
730 Z$=MID$(C$(0,1,0),1,1):GOSUB910:POKE38135,T
740 Z$=MID$(C$(0,2,2),2,1):GOSUB910:POKE38205,T
750 Z$=MID$(C$(0,1,2),2,1):GOSUB910:POKE38184,T
760 Z$=MID$(C$(0,0,2),2,1):GOSUB910:POKE38163,T
770 Z$=MID$(C$(1,0,2),1,1):GOSUB910:POKE38207,T
780 Z$=MID$(C$(2,0,2),2,1):GOSUB910:POKE38251,T
790 Z$=MID$(C$(2,1,2),2,1):GOSUB910:POKE38272,T
800 Z$=MID$(C$(2,2,2),2,1):GOSUB910:POKE38293,T
810 Z$=MID$(C$(1,2,2),1,1):GOSUB910:POKE38249,T
820 Z$=MID$(C$(0,2,0),1,1):GOSUB910:POKE38221,T
830 Z$=MID$(C$(0,2,1),1,1):GOSUB910:POKE38223,T
840 Z$=MID$(C$(0,2,2),1,1):GOSUB910:POKE38225,T
850 Z$=MID$(C$(1,2,2),2,1):GOSUB910:POKE38269,T
860 Z$=MID$(C$(2,2,2),1,1):GOSUB910:POKE38313,T
870 Z$=MID$(C$(2,2,1),1,1):GOSUB910:POKE38311,T
880 Z$=MID$(C$(2,2,0),1,1):GOSUB910:POKE38309,T
890 Z$=MID$(C$(1,2,0),2,1):GOSUB910:POKE38265,T
900 RETURN
910 IFZ$="A"THENT=36:RETURN
920 IFZ$="B"THENT=33:RETURN
930 IFZ$="C"THENT=38:RETURN
940 IFZ$="G"THENT=39:RETURN
950 IFZ$="V"THENT=37:RETURN
960 IFZ$="R"THENT=34:RETURN
970 A$(0)=MID$(N$,3,1)
980 A$(1)=MID$(N$,2,1)
990 A$(2)=MID$(N$,1,1)
1000 H=P+1+3*(P>1)
1010 K=P+2+3*(P>0)
1020 A$(3)=A$(H):A$(H)=A$(K):A$(K)=A$(3)
1030 N$=A$(2)+A$(1)+A$(0)
1040 RETURN
1050 T$=""
1060 S=0:T=0
1070 S=S+1:IFMID$(H$(L),S,1)<>"*"THENT$=T$+" "+MID$(H$(L),S,1):GOTO1090
1080 T$=T$+STR$(D%(I+T)):T=T+1
1090 IFS<3THEN1070
1100 X=VAL(MID$(T$,1,2))
1110 Y=VAL(MID$(T$,3,2))
1120 Z=VAL(MID$(T$,5,2))
1130 I=I+SS:RETURN
1140 Q=1
1150 FORI=1TOLEN(N$)
1160 Q=Q*ASC(MID$(N$,I,1))
1170 NEXT
1180 RETURN
1190 REM  *****************************************
1200 REM  *  QUESTA ROUTINE SERVE PER RICERCARE   *
1210 REM  *  UN DETERMINATO MATTONCINO DEL CUBO   *
1220 REM  *****************************************
1230 GOSUB1140:P=Q
1240 FORA=0TO2
1250 FORB=0TO2
1260 FORC=0TO2
1270 N$=C$(A,B,C):GOSUB1140
1280 IFQ=PTHENT=A*100+B*10+C:A=3:B=3:C=3
1290 NEXTC,B,A:RETURN
1300 IFPEEK(197)=64THEN1300
1310 PRINT"(HOME)":FORP=1TO8:PRINT"                     ":NEXT:PRINT"(HOME)"
1320 FORY=1TOLEN(N$)STEP2
1330 Q$=MID$(N$,Y,1)
1340 P$=MID$(N$,Y+1,1)
1350 IFQ$="0"THENPRINT" ARANCIO ";:GOTO1410
1360 IFQ$="1"THENPRINT" BLU     ";:GOTO1410
1370 IFQ$="2"THENPRINT" GIALLO  ";:GOTO1410
1380 IFQ$="3"THENPRINT" ROSSO   ";:GOTO1410
1390 IFQ$="4"THENPRINT" VERDE   ";:GOTO1410
1400 IFQ$="5"THENPRINT" BIANCO  ";
1410 IFP$="0"THENPRINT"ORARIO      "
1420 IFP$="1"THENPRINT"ANTIORARIO  "
1430 NEXT:RETURN
1440 REM *** SITEMAZIONE CROCE ARANCIO ***
1450 MO=0:IFC$(0,2,1)="BA"ANDC$(0,0,1)="GA"ANDC$(0,1,0)="AV"ANDC$(0,1,2)="AC"THEN1670
1460 PRINT"(HOME)(DOWN)  (WHT)(RVS) RISOLUZIONE CUBO (OFF)":FORW=1TO4:READN$
1470 IFC$(0,2,1)=N$THENN$="01":GOTO1640
1480 H$=N$:GOSUB1230
1490 IFT=21THENN$="50":GOTO1610
1500 IFT=10THENN$="405050":GOTO1610
1510 IFT=1THENN$="2040405050":GOTO1610
1520 IFT=12THENN$="11":GOTO1610
1530 IFT=122THEN1620
1540 IFT=120THENN$="5050":GOTO1610
1550 IFT=100THENN$="004040015050":GOTO1610
1560 IFT=102THENN$="01101000":GOTO1610
1570 IFT=221THENN$="51":GOTO1610
1580 IFT=210THENN$="3051":GOTO1610
1590 IFT=201THENN$="303051":GOTO1610
1600 IFT=212THENN$="3151"
1610 GOSUB530
1620 IFC$(1,2,2)=H$THENN$="0110":GOTO1640
1630 N$="5101"
1640 GOSUB530
1650 NEXT
1660 REM *** SITEMAZIONE ANGOLI ARANCIO ***
1670 IFC$(0,0,0)="GVA"ANDC$(0,2,0)="BVA"ANDC$(0,2,2)="BCA"THENN$="00":GOSUB530:GOTO1860
1680 FORW=1TO3:READN$
1690 IFC$(0,2,2)=N$THENN$="01":GOTO1830
1700 H$=N$:GOSUB1230
1710 IFT=0THENN$="41303040":GOTO1790
1720 IFT=2THENN$="1030301130":GOTO1790
1730 IFT=20THENN$="51305030":GOTO1790
1740 IFT=22THENN$="113010":GOTO1790
1750 IFT=222THEN1800
1760 IFT=220THENN$="30":GOTO1790
1770 IFT=200THENN$="3030":GOTO1790
1780 IFT=202THENN$="31"
1790 GOSUB530
1800 IFMID$(C$(2,2,2),1,1)="A"THENN$="50305101":GOTO1830
1810 IFMID$(C$(2,2,2),2,1)="A"THENN$="11311001":GOTO1830
1820 IFMID$(C$(2,2,2),3,1)="A"THENN$="11303010":GOSUB530:N$="3011311001":GOTO1830
1830 GOSUB530
1840 NEXT
1850 REM *** SITEMAZIONE CINTURA ***
1860 IFC$(1,2,0)="VB"ANDC$(1,0,0)="VG"ANDC$(1,0,2)="CG"ANDC$(1,2,2)="CB"THEN2500
1870 IFC$(1,2,2)="CB"THENN$="00":GOSUB530:GOTO2010
1880 N$="CB":GOSUB1230
1890 IFT>200THEN1970
1900 IFT=122THENN$="113110":GOTO1940
1910 IFT=120THENN$="0040304101":GOTO1940
1920 IFT=100THENN$="00004130400000":GOTO1940
1930 IFT=102THENN$="0110311100":GOTO1940
1940 GOSUB530
1950 IFC$(2,2,1)="BC"THENN$="3050315100":GOSUB530:GOTO2010
1960 N$="11301000":GOSUB530:GOTO2010
1970 IFT=221THEN1950
1980 IFT=212THENN$="31":GOTO1940
1990 IFT=201THENN$="3030":GOTO1940
2000 IFT=210THENN$="30":GOTO1940
2010 IFC$(1,2,0)="VB"THENN$="00":GOSUB530:GOTO2150
2020 N$="VB":GOSUB1230
2030 IFT>200THEN2110
2040 IFT=122THENN$="011131311000":GOTO2080
2050 IFT=120THENN$="513150":GOTO2080
2060 IFT=100THENN$="0020302101":GOTO2080
2070 IFT=102THENN$="00002130200000":GOTO2080
2080 GOSUB530
2090 IFC$(2,1,0)="BV"THENN$="3040314100":GOSUB530:GOTO2150
2100 N$="51305000":GOSUB530:GOTO2150
2110 IFT=210THEN2090
2120 IFT=212THENN$="3030":GOTO2080
2130 IFT=201THENN$="30":GOTO2080
2140 IFT=221THENN$="31":GOTO2080
2150 IFC$(1,0,0)="VG"THENN$="00":GOSUB530:GOTO2290
2160 N$="VG":GOSUB1230
2170 IFT>200THEN2250
2180 IFT=122THENN$="00001130100000":GOTO2220
2190 IFT=120THENN$="0140314100":GOTO2220
2200 IFT=100THENN$="413140":GOTO2220
2210 IFT=102THENN$="0010301101":GOTO2220
2220 GOSUB530
2230 IFC$(2,0,1)="VG"THENN$="41304000":GOSUB530:GOTO2290
2240 N$="3020312100":GOSUB530:GOTO2290
2250 IFT=201THEN2230
2260 IFT=212THENN$="30":GOTO2220
2270 IFT=221THENN$="3030":GOTO2220
2280 IFT=210THENN$="31":GOTO2220
2290 IFC$(1,0,2)="CG"THEN2500
2300 N$="CG":GOSUB1230
2310 IFT>200THEN2390
2320 IFT=122THENN$="0050305101":GOTO2360
2330 IFT=120THENN$="00005130500000":GOTO2360
2340 IFT=100THENN$="0120312100":GOTO2360
2350 IFT=102THENN$="213120":GOTO2360
2360 GOSUB530
2370 IFC$(2,1,2)="CG"THENN$="213020":GOSUB530:GOTO2500
2380 N$="30103111":GOSUB530:GOTO2500
2390 IFT=212THEN2370
2400 IFT=210THENN$="3030":GOTO2360
2410 IFT=201THENN$="31":GOTO2360
2420 IFT=221THENN$="30":GOTO2360
2430 REM *** ROUTINE MOVIMENTI RANDOM ***
2440 IFE>99THENPRINT"(CLR)":GOSUB3250:GOSUB660
2450 FORQ=1TOE
2460 N$=RIGHT$(STR$(INT(RND(0)*6)),1)+RIGHT$(STR$(INT(RND(0)*2)),1):GOSUB530:NEXT
2470 IFPEEK(197)=64THEN2470
2480 GOTO1450
2490 REM *** SISTEMAZIONE ANGOLO ''CGA'' ***
2500 IFC$(0,0,2)="GCA"THEN2630
2510 N$="GCA":GOSUB1230
2520 IFT=202THEN2580
2530 IFT=200THENN$="31":GOTO2570
2540 IFT=222THENN$="30":GOTO2570
2550 IFT=220THENN$="3030":GOTO2570
2560 IFT=2THENN$="1030403111304131":GOTO2570
2570 GOSUB530
2580 IFMID$(C$(2,0,2),1,1)="A"THENN$="3020002131200121":GOTO2610
2590 IFMID$(C$(2,0,2),2,1)="A"THENN$="3111011030110010":GOTO2610
2600 IFMID$(C$(2,0,2),3,1)="A"THENN$="30301010415111":GOSUB530:N$="50405110501010":GOTO2610
2610 GOSUB530
2620 REM *** SISTEMAZIONE ANGOLI ROSSI ***
2630 N$="BCR":GOSUB1230
2640 IFT=222THEN2690
2650 IFT=200THENN$="3030":GOTO2680
2660 IFT=202THENN$="31":GOTO2680
2670 IFT=220THENN$="30":GOTO2680
2680 GOSUB530
2690 IFC$(2,2,0)="BVR"ANDC$(2,0,0)="GVR"ANDC$(2,0,2)="GCR"THEN2900
2700 N$="BVR":GOSUB1230:FF=T
2710 N$="GVR":GOSUB1230:GG=T
2720 N$="GCR":GOSUB1230:HH=T
2730 IFFF=220ANDGG=200ANDHH=202THEN2830
2740 IFFF=200ANDGG=220ANDHH=202THENN$="3020105011":GOSUB530:N$="21105111":GOTO2820
2750 IFFF=220ANDGG=202ANDHH=200THENN$="3010504051":GOSUB530:N$="11504151":GOTO2820
2760 IFFF=202ANDGG=200ANDHH=220THEN2790
2770 IFFF=202ANDGG=220ANDHH=200THENN$="4020102141201121":GOTO2820
2780 IFFF=200ANDGG=202ANDHH=220THENN$="2141514020415040":GOTO2820
2790 N$="201050112110":GOSUB530
2800 N$="511140201021":GOSUB530
2810 N$="4120112131":GOTO2820
2820 GOSUB530
2830 FORE=1TO4
2840 IFMID$(C$(2,2,2),3,1)="R"THEN2880
2850 IFMID$(C$(2,2,2),2,1)="R"THENN$="5011511050115110":GOTO2870
2860 IFMID$(C$(2,2,2),1,1)="R"THENN$="1150105111501051":GOTO2870
2870 GOSUB530
2880 N$="30":GOSUB530:NEXT
2890 REM *** SISTEMAZIONE CROCE ROSSA ***
2900 IFC$(2,0,1)="GR"ANDC$(2,2,1)="BR"ANDC$(2,1,0)="RV"ANDC$(2,1,2)="RC"THEN3190
2910 N$="BR":GOSUB1230
2920 IFT=221THEN2970
2930 IFT=212THENN$="1010404000114050":GOSUB530:N$="5010410010104040":GOTO2960
2940 IFT=210THENN$="1010404001114050":GOSUB530:N$="5010410110104040":GOTO2960
2950 IFT=201THENN$="2020505001215010":GOSUB530:N$="1020510120205050":GOTO2960
2960 :GOSUB530
2970 N$="VR":GOSUB1230:FF=T
2980 IFC$(2,0,1)="GR"ANDC$(2,2,1)="BR"ANDC$(2,1,0)="RV"ANDC$(2,1,2)="RC"THEN3190
2990 N$="GR":GOSUB1230:GG=T
3000 N$="CR":GOSUB1230:HH=T
3010 IFFF=210ANDGG=201ANDHH=212THEN3050
3020 IFFF=212ANDGG=210ANDHH=201THENN$="4040101000411020":GOSUB530:N$="2040110040401010"
3030 IFFF=201ANDGG=212ANDHH=210THENN$="4040101001411020":GOSUB530:N$="2040110140401010"
3040 GOSUB530
3050 IFC$(2,0,1)="GR"ANDC$(2,2,1)="BR"ANDC$(2,1,0)="RV"ANDC$(2,1,2)="RC"THEN3190
3060 FORE=1TO4
3070 IFMID$(C$(2,2,1),2,1)="R"THEN3170
3080 IFMID$(C$(2,0,1),1,1)="R"THEN3110
3090 IFMID$(C$(2,1,0),2,1)="R"THEN3140
3100 GOTO3170
3110 N$="4110504110004110":GOSUB530
3120 N$="2041103040112140":GOSUB530
3130 N$="1101401151401131":GOSUB530:GOTO3170
3140 N$="1140201041303011":GOSUB530
3150 N$="4020114000114050":GOSUB530
3160 N$="5010410010104040":GOSUB530
3170 IFC$(2,0,1)="GR"ANDC$(2,2,1)="BR"ANDC$(2,1,0)="RV"ANDC$(2,1,2)="RC"THEN3190
3180 N$="30":GOSUB530:NEXT
3190 IFMO=0THENPRINT"(HOME)(DOWN)(RVS)  CUBO GIA' RISOLTO   ":GOTO3210
3200 PRINT"(HOME)(DOWN)(RVS) RISOLTO IN";MO;"(LEFT) MOSSE "
3210 IFPEEK(197)<>39THEN3210
3220 GETZ$:IFZ$<>""THEN3220
3230 RUN
3240 REM ***  ROUTINE DISEGNI GRAFICI ***
3250 POKE36879,9
3260 PRINT"(CLR)(WHT)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT) (SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)"
3270 PRINT"(WHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT-N)(PUR)(SHIFT-Q) (SHIFT-Q) (SHIFT-Q)(WHT)(SHIFT-N)(SHIFT--)"
3280 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT-N)(PUR)(SHIFT-Q) (SHIFT-Q) (SHIFT-Q)(WHT)(SHIFT-N) (SHIFT--)"
3290 PRINT"(RGHT)(RGHT)(RGHT)(SHIFT-N)(PUR)(SHIFT-Q) (SHIFT-Q) (SHIFT-Q)(WHT)(SHIFT-N)(BLU) (SHIFT-Q)(WHT)(SHIFT--)"
3300 PRINT"(RGHT)(RGHT) (SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*) (BLU) (SHIFT-Q)(WHT) (SHIFT--)"
3310 PRINT"(RGHT)(RGHT)(SHIFT--)     (SHIFT--)(BLU)(SHIFT-Q) (SHIFT-Q)(WHT)(SHIFT--)"
3320 PRINT"(RGHT)(RGHT)(SHIFT--)(SHIFT-Q) (SHIFT-Q) (SHIFT-Q)(SHIFT--)(BLU) (SHIFT-Q)(WHT) (SHIFT--)"
3330 PRINT"(RGHT)(RGHT)(SHIFT--)     (SHIFT--)(BLU)(SHIFT-Q) (SHIFT-Q)(WHT)(SHIFT--)"
3340 PRINT"(RGHT)(RGHT)(SHIFT--)(SHIFT-Q) (SHIFT-Q) (SHIFT-Q)(SHIFT--)(BLU) (SHIFT-Q)(WHT)"
3350 PRINT"(RGHT)(RGHT)(SHIFT--)     (SHIFT--)(BLU)(SHIFT-Q) (WHT)(SHIFT-N)"
3360 PRINT"(RGHT)(RGHT)(SHIFT--)(SHIFT-Q) (SHIFT-Q) (SHIFT-Q)(SHIFT--) (SHIFT-N)"
3370 PRINT"(RGHT)(RGHT)(SHIFT--)     (SHIFT--)(SHIFT-N)
3380 PRINT"(RGHT)(RGHT) (SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*) "
3390 RETURN
3400 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)"
3410 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT) (SHIFT-N)        (SHIFT-N)"
3420 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)  (SHIFT-N)  (WHT)(RVS)(SHIFT-£) (OFF)(SHIFT-£)  (BLU) (SHIFT-N)" 
3430 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT) (SHIFT-N)        (SHIFT-N)"
3440 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-A)(SHIFT-*)(SHIFT-*)(CBM-R)(SHIFT-*)(SHIFT-*)(CBM-R)(SHIFT-*)(SHIFT-*)(CBM-S)" 
3450 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--)  (SHIFT--)  (SHIFT--)  (SHIFT--)" 
3460 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--) (SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-*)(SHIFT-+)(CBM-S) (SHIFT--)" 
3470 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--)  (SHIFT--)  (SHIFT--)(SHIFT--) (SHIFT--)" 
3480 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-Q)(SHIFT-*)(SHIFT-*)(SHIFT-+)(SHIFT-*)(SHIFT-*)(SHIFT-+)(SHIFT-+)(SHIFT-*)(CBM-W)" 
3490 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--)  (SHIFT--)(WHT)(RVS)  (OFF)(BLU)(SHIFT--)(SHIFT--) (SHIFT--)" 
3500 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--)  (SHIFT--)(WHT)(RVS)  (OFF)(BLU)(SHIFT--)(SHIFT--) (SHIFT--)" 
3510 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--) ^(SHIFT--)(WHT)(RVS)  (OFF)(BLU)(SHIFT--)(SHIFT--) (SHIFT--)" 
3520 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-Q)(SHIFT-*)(SHIFT-+)(SHIFT-+)(SHIFT-*)(SHIFT-*)(SHIFT-+)(SHIFT-+)(SHIFT-*)(CBM-W)" 
3530 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--) (SHIFT--)(SHIFT--)  (SHIFT--)(SHIFT--) (SHIFT--)  (SHIFT-N)" 
3540 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--) (CBM-Z)(SHIFT-+)(SHIFT-*)(SHIFT-*)(SHIFT-+)(CBM-X) (SHIFT--) (SHIFT-N)" 
3550 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SHIFT--)  (SHIFT--)  (SHIFT--)  (SHIFT--)(SHIFT-N)" 
3560 PRINT"(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(CBM-Z)(SHIFT-*)(SHIFT-*)(CBM-E)(SHIFT-*)(SHIFT-*)(CBM-E)(SHIFT-*)(SHIFT-*)(CBM-X)" 
3570 RETURN
3580 XX=VAL(X$)
3590 IFXX<2ORXX=4ORXX>8THEN350
3600 IFXX=2THENX$="B"
3610 IFXX=3THENX$="R"
3620 IFXX=5THENX$="A"
3630 IFXX=6THENX$="V"
3640 IFXX=7THENX$="C"
3650 IFXX=8THENX$="G"
3660 GOTO370
3670 DATA0,1,1,0,2,1,1,2,0,0,2,0,2,2,0,2
3680 DATA0**,**2,*0*,2**,**0,*2*
3690 DATA "(YEL)GIALLO (BLU)",0,0,1,0,2,0,12,1,22,0,21,0,20,0,10,1,11
3700 DATA"(RED)ROSSO  (BLU)",200,2,201,1,202,2,102,0,2,2,1,1,0,2,100,0,101
3710 DATA "(GRN)VERDE  (BLU)",200,0,210,1,220,0,221,0,222,0,212,1,202,0,201,0,211
3720 DATA"(RVS)BIANCO(OFF) ",20,1,120,1,220,1,210,0,200,1,100,1,0,1,10,0,110
3730 DATA"(BLU)C(LEFT)BLU   ",22,2,122,0,222,2,221,1,220,2,120,0,20,2,21,1,121
3740 DATA"(RED)ROSSO(BLU)",202,1,212,0,222,1,122,1,22,1,12,0,2,1,102,1,112
3750 DATABA,VA,GA,CA,BCA,VBA,GVA
