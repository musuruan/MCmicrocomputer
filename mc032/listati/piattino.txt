10 REM -----------------------------------------------------------
11 REM -----  PIATTINO - DI ROBERTO RIMINI - CANNIZZARO (CT)  ----
12 REM -----------------------------------------------------------
13 POKE36879,26:O=36878
14 PRINT"(CLR)(BLU)******(YEL)PIATTINO(BLU)********(DOWN)"
15 PRINT" FAI LA TUA PUNTATA E  SE ESCE UN NUMERO"
16 PRINT" MAGGIORE DI (RVS)4(OFF) VINCI"
17 PRINT" ALTRIMENTI PERDI.(DOWN)"
18 PRINT" SE ESCE (RVS)9(OFF)  VINCI IL    DOPPIO!(DOWN)"
19 PRINT" SE ESCE (RVS)0(OFF) PERDI  IL    DOPPIO!"
20 PRINT"(YEL)(DOWN)    BUONA FORTUNA(BLK)."
21 INPUT"(BLU)(DOWN)CAPITALE \";K
22 INPUT"(DOWN)QUANTE \ PUNTI";J
23 IFJ>KTHENPRINT"(BLK)(RVS)(DOWN)NON PUOI BARO(OFF)(BLU)":GOTO22
24 REM PRINT"(BLU)(DOWN)PREMI UN TASTO PER CONTINUARE"
25 C$="       (SHIFT-U)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-I)"
26 D$="       (SHIFT--)   (SHIFT--)"
27 E$="       (SHIFT-J)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-K)"
28 PRINT"(BLK)(CLR)(DOWN)(DOWN)(DOWN)(DOWN)";C$
29 FORG=1TO4:PRINTD$:NEXTG:PRINTE$:POKE36879,248
30 Q=INT(10+20*RND(0))
31 FORZ=1TOQ
32 V=INT(0+10*RND(0))
33 POKE36878,15:POKE36876,254-V
34 PRINT"(HOME)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)";V
35 FORM=1TO50:NEXTM,Z
36 IFV=0THEN65
37 IFV<5THEN51
38 IFV=9THEN59
39 PRINT"(RED)(DOWN)(DOWN)(DOWN)(DOWN)     (RVS)HAI VINTO!":RESTORE
40 POKE36878,15:S=36876:T=100
41 READN,A:IFN=-1THEN45
42 POKES,N:FORD=1TOT*A:NEXT:GOTO41
43 DATA195,3,187,1,183,2,175,2,163,2,175,2,183,2,163,2,175,1
44 DATA183,1,187,1,175,1,183,3,175,1,163,2,159,2,163,4,-1,-1
45 POKES,0:POKE36878,0
46 K=K+J
47 PRINT"(BLU)(DOWN)IL TUO CAPITALE E' ORA DI \";K
48 PRINT"(DOWN)GIOCHI ANCORA(S/N)
49 INPUT W$:IFW$="N"THEN58
50 GOTO22
51 PRINT"(DOWN)(DOWN)(DOWN)(DOWN)(BLK)MI DISPIACE MA          (RVS)HAI PERSO(OFF)!"
52 POKE36878,15:POKE36877,234
53 FORH=1TO500:NEXTH
54 POKE36878,0:POKE36877,0:K=K-J
55 IFK=0ORK<0THENPRINT"(RVS)(BLU)(DOWN)SEI RIMASTO AL VERDE!":END
56 PRINT"(BLU)(DOWN)IL TUO CAPITALE E' ORA DI \";K
57 GOTO48
58 PRINT"(DOWN)(BLK)******FIFONE****":END
59 PRINT"(DOWN)(DOWN)(DOWN)(DOWN)FORTUNATO HAI VINTO IL (RVS)(BLU)DOPPIO(OFF)(BLK) DI \";J
60 POKE36878,15:FORL=1TO100
61 POKE36876,INT(RND(1)*128)+128
62 FORM=1TO10:NEXTM,L:POKE36876,0:POKE36878,0:K=K+2*J
63 PRINT"(BLU)(DOWN)IL TUO CAPITALE E' ORA DI \";K
64 GOTO48
65 PRINT"(DOWN)(DOWN)(DOWN)(DOWN)(BLK)MI DISPIACE MA HAI    (RVS)(GRN)PERSO IL DOPPIO (OFF)(BLK)       DI \";J
66 POKE36878,15:FORL=250TO200STEP-2:POKE36876,L:FORM=1TO70:NEXTM,L
67 POKE36876,0:POKE36878,0:K=K-2*J
68 PRINT"(BLU)(DOWN)IL TUO CAPITALE E' ORA DI \";K
69 IFK<0THEN55
78 GOTO48

