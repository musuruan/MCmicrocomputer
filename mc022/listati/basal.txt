10000 REM         ++++++++++++++++++++++++++++++++++++++++++++++++
10010 REM         +  ***    **   ****   **   *       ****     *  +
10020 REM         +  *  *  *  *  *     *  *  *          *     *  +
10030 REM         +  ***   ****  ****  ****  *       ****     *  +
10040 REM         +  *  *  *  *     *  *  *  *       *        *  +
10050 REM         +  ***   *  *  ****  *  *  ****    ****  *  *  +
10060 REM         +                                              +
10070 REM         +            (C) 1983 ADP-SOFTWARE             +
10080 REM         +              MC  MICROCOMPUTER               +
10090 REM         ++++++++++++++++++++++++++++++++++++++++++++++++
10100 POKE56,91:POKE36879,29:GOTO10430
10110 DIMA$(180):FL=0
10120 PRINT"(CLR)":A$(1)="BEGIN":PRINT"  BEGIN"
10130 FORI=2TO180:N=I
10140 IFA$(I)<>""THENPRINT"  "+A$(I):GOTO10390
10150 INPUTA$(I)
10160 IFA$(I)=""THENN=N-1:I=999:NEXT:GOTO10630
10170 PO%=0:FORKL=1TOLEN(A$(I))
10180 IFMID$(A$(I),KL,1)="_"THENPO%=KL
10190 NEXT
10200 KL$=MID$(A$(I),PO%+1,100)
10210 GOSUB10280
10220 A$(I)=LEFT$(A$(I),PO%)+KL$
10230 FORZ=0TOHH%:PRINT"(UP)";:NEXT:PRINT"  ";A$(I)
10240 GOTO10390
10250 REM *************************
10260 REM * ROUTINE ABBREVIAZIONI *
10270 REM *************************
10280 HH%=(LEN(KL$)+2)/22
10290 IFLEFT$(KL$,2)="C "THENKL$="CASE"+RIGHT$(KL$,LEN(KL$)-1)
10300 IFLEFT$(KL$,2)="G "THENKL$="GOSUB"+RIGHT$(KL$,LEN(KL$)-1)
10310 IFLEFT$(KL$,2)="I "THENKL$="INPUT"+RIGHT$(KL$,LEN(KL$)-1)
10320 IFLEFT$(KL$,2)="B"ORRIGHT$(KL$,2)="_B"THENKL$=KL$+"EGIN"
10330 IFLEFT$(KL$,2)="U "THENKL$="UNTIL"+RIGHT$(KL$,LEN(KL$)-1)
10340 IFLEFT$(KL$,2)="W "THENKL$="WHILE"+RIGHT$(KL$,LEN(KL$)-1)
10350 IFLEFT$(KL$,2)="IF"ANDRIGHT$(KL$,1)="T"THENKL$=KL$+"HEN"
10360 IFLEFT$(KL$,2)="R"THENKL$="REPEAT"
10370 IFLEFT$(KL$,1)="?"THENKL$="PRINT"+RIGHT$(KL$,LEN(KL$)-1)
10380 RETURN
10390 IFA$(I)<>"E"THENNEXT:GOTO10540
10400 A$(I)="":L=I-1:GOSUB10840:PRINT"(CLR)"
10410 FORK=1TON-1:PRINT"  ";A$(K):NEXT
10420 GOTO10150
10430 PRINT"(CLR)(DOWN)(RVS)      BASAL 2.1       (BLU)"
10440 PRINT"(DOWN) SCELTA OPZIONE"
10450 PRINT" (SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)(SHIFT-E)"
10460 PRINT"(DOWN) L:LIST  SU  VIDEO  "
10470 PRINT"(DOWN) E:EDIT  DI  LINEA"
10480 PRINT"(DOWN) P:PRECOMPILAZIONE"
10490 PRINT"(DOWN) C: CONCATENAMENTO"
10500 PRINT"(DOWN) (RVS)N(OFF):NUOVO PROGRAMMA"
10510 PRINT"(DOWN) (RVS)S(OFF):SAVE  PROGRAMMA"
10520 PRINT"(DOWN) (RVS)L(OFF):LOAD  PROGRAMMA"
10530 PRINT"(DOWN)(RVS)  (C)  ADP  SOFTWARE  (BLU)";
10540 GETZ$:IFZ$=""THEN10540
10550 IFZ$="E"THENGOSUB10820:GOTO10430
10560 IFZ$="C"THEN10120
10570 IFZ$="(SHIFT-N)"THENCLR:GOTO10110
10580 IFZ$="P"THENPRINT"(CLR)":GOSUB11460:GOTO11230
10590 IFZ$="(SHIFT-S)"THENPRINT"(CLR)":GOTO11090
10600 IFZ$="(CBM-L)"THENGH=80:OPEN1,4:CMD1:GOTO10640
10610 IFZ$="(SHIFT-L)"THENPRINT"(CLR)":GOTO11140
10620 IFZ$<>"L"THEN10430
10630 GH=22
10640 PRINT"(CLR)":H=0:FORI=1TON
10650 IFRIGHT$(A$(I-1),5)="BEGIN"THENH=H+1-(GH>30)
10660 IFA$(I)="END"ORA$(I)="END."THENH=H-1+(GH>30)
10670 IFLEN(A$(I))+H<=GHTHEN10740
10680 B$=A$(I)
10690 IFB$=""THENPRINT:GOTO10750
10700 FORII=1TOH:PRINT" ";:NEXT
10710 PRINTLEFT$(B$,GH-H);
10720 B$=MID$(B$,GH+1-H,100)
10730 GOTO10690
10740 PRINTSPC(H);A$(I)
10750 IFPEEK(653)<>0THEN10750
10760 NEXT
10770 GETZ$:IFZ$=""THEN10770
10780 CLOSE1:GOTO10550
10790 REM *************************
10800 REM * ROUTINE EDIT DI LINEA *
10810 REM *************************
10820 I=2
10830 L=N
10840 T=I:Q=0
10850 T=T-1
10860 IFT>LTHENT=T-1
10870 IFT<1THENT=T+1
10880 PRINT"(CLR)(DOWN)(RGHT)(RGHT)(OFF)";A$(T)
10890 PRINT"(HOME)";TAB(244);TAB(229);"(RVS)";T;"(LEFT) "
10900 GETZ$:IFZ$=""THEN10900
10910 IFZ$=CHR$(145)THEN10850
10920 IFZ$="I"THEN11030
10930 IFZ$="D"THEN11060
10940 IFZ$="C"THEN10120
10950 IFZ$=CHR$(17)THENT=T+1:GOTO10860
10960 IFZ$=CHR$(13)THENRETURN
10970 IFZ$<>CHR$(29)THEN10900
10980 PRINT"(CLR)(DOWN)(RGHT)(RGHT)";A$(T)
10990 PRINT"(HOME)";TAB(244);TAB(229);"(RVS)";T;"(LEFT) (HOME)"
11000 INPUTKL$:TY=I:I=T:GOSUB10280:A$(T)=KL$:I=TY:IFQ>1THENQ=Q-1:T=T+1:GOTO10980
11010 Q=0:GOTO10880
11020 RETURN
11030 Q=Q+1:FORW=NTOTSTEP-1
11040 A$(W+1)=A$(W)
11050 NEXT:A$(T)="":N=N+1:L=L+1:I=I+1:GOTO10880
11060 FORW=TTOL
11070 A$(W)=A$(W+1)
11080 NEXT:N=N-1:L=L-1:I=I-1:GOTO10880
11090 INPUT"(CLR)(DOWN)NOME FILE ";LJ$:LJ$=LJ$+",S,W"
11095 OPEN2,8,2,LJ$
11100 PRINT#2,N
11110 FORI=1TON
11120 PRINT#2,A$(I)
11130 NEXT:CLOSE2:GOTO10430
11140 CLR:INPUT"(CLR)(DOWN)NOME FILE ";LJ$:LJ$=LJ$+",S,R"
11145 OPEN2,8,2,LJ$
11150 INPUT#2,N:DIMA$(180)
11160 FORI=1TON
11170 INPUT#2,A$(I)
11180 NEXT:CLOSE2:GOTO10430
11190 REM ***********************************************
11200 REM * QUESTA ROUTINE  TRASFERISCE AUTOMATICAMENTE *
11210 REM * IL PROGRAMMA GENERATO NELLA MEMORIA DEL VIC *
11220 REM ***********************************************
11230 S=24320:Z=0:PRINT"(CLR)":FORI=1TON
11240 FORK=1TOLEN(A$(I))
11250 PO=ASC(MID$(A$(I),K,1))
11260 IFPO=34THENZ=1-Z
11270 IF(PO=32ORPO=160)ANDZ=0THEN11290
11280 S=S-1:POKES,PO
11290 NEXT
11300 S=S-1:POKES,0
11310 NEXT:POKE56,S/256-2:POKES-1,1:POKES-2,2:POKES-3,3
11320 I=24320
11330 I=I-1
11340 IFPEEK(I)<>0THENA$=A$+CHR$(PEEK(I)):GOTO11330
11350 IFPEEK(I-1)=1ANDPEEK(I-2)=2ANDPEEK(I-3)=3THENPRINT"(CLR)(DOWN)(DOWN)(DOWN)";A$:PRINT"(BLU)RUN(HOME)":GOTO11400
11360 POKE24319,I/256
11370 POKE24318,(I/256-PEEK(24319))*256
11380 PRINT"(CLR)(DOWN)(DOWN)(DOWN)(WHT)";A$:A$=""
11390 PRINT"GOTO11410(HOME)"
11400 POKE198,10:POKE631,13:POKE632,13:END
11410 I=PEEK(24319)*256+PEEK(24318):GOTO11330
11420 REM ***********************************
11430 REM * TRADUZIONE DA BASAL A BASIC DEL *
11440 REM * PROGRAMMA  CONTENUTO  IN  A$(N) *
11450 REM ***********************************
11460 FORI=2TON
11470 FORH=1TOLEN(A$(I))
11480 IFMID$(A$(I),H,1)<>"(SHIFT--)"THEN11500
11490 A$(I)=LEFT$(A$(I),H-1)+","+MID$(A$(I),H+1,100)
11500 NEXT:NEXT
11510 FORI=2TON
11520 IFLEFT$(A$(I),2)="IF"THEN12410
11530 IFLEFT$(A$(I),3)="FOR"THEN12610
11540 IFLEFT$(A$(I),2)="GO"THEN12150
11550 IFLEFT$(A$(I),5)="UNTIL"THEN12060
11560 IFLEFT$(A$(I),5)="WHILE"THEN12270
11570 NEXT
11580 FORI=2TON
11590 IFLEFT$(A$(I),4)="CASE"THEN11800
11600 NEXT
11610 FORI=2TON
11620 IFLEFT$(A$(I),1)="*"THEN12740
11630 NEXT
11640 FORI=2TON
11650 IFLEFT$(A$(I),2)="IF"ORLEFT$(A$(I),2)="GO"THEN12780
11660 IFLEFT$(A$(I),1)="@"THENA$(I)=RIGHT$(A$(I),LEN(A$(I))-1):GOTO12780
11670 IFLEFT$(A$(I),1)="#"THENA$(I)=RIGHT$(A$(I),LEN(A$(I))-1):GOTO11680
11680 NEXT
11690 PRINT"(CLR)":FORI=1TON
11700 IFA$(I)="BEGIN"ORA$(I)="END"THENA$(I)=""
11710 IFA$(I)="END."THENA$(I)="END"
11720 A$(I)=STR$(I*10)+A$(I)
11730 IFLEN(A$(I))>4THENPRINTA$(I)
11740 NEXT
11750 IFPEEK(197)=64THEN11750
11760 RETURN
11770 REM ***********************
11780 REM * PREC. CASE..OF..... *
11790 REM ***********************
11800 H=4
11810 H=H+1
11820 IFMID$(A$(I),H,2)<>"OF"THEN11810
11830 B$=MID$(A$(I),5,H-5)
11840 GOSUB12880:A$(I)="":A$(I+1)="":A$(J)=""
11850 J$=STR$((J+1)*10)
11860 FORT=I+2TOJ-1
11870 H=1:IFA$(T)="OT_BEGIN"THENK=1:D=I:I=T:GOSUB12890:I=D:A$(T)="":A$(J)="":GOTO11990
11880 H=H+1
11890 IFMID$(A$(T),H,1)<>"_"THEN11880
11900 H=H-1:K=LEN(A$(T))-H-1
11910 L$=LEFT$(A$(T),H):R$=RIGHT$(A$(T),K)
11920 IFL$="OT"THENA$(T)=R$:GOTO11950
11930 IFR$="BEGIN"THEN11960
11940 A$(T)="IF"+B$+"="+L$+"THEN"+R$+":GOTO "+J$
11950 NEXT:GOTO11600
11960 D=I:I=T:K=1:GOSUB12890:I=D
11970 A$(T)="IF"+B$+"<>"+L$+"THEN "+STR$((J+1)*10)
11980 A$(J)="@GOTO"+J$
11990 T=J
12000 NEXT
12010 I=T+1
12020 GOTO11600
12030 REM ***********************
12040 REM * PREC. REPEAT..UNTIL *
12050 REM ***********************
12060 J=I
12070 J=J-1
12080 IFA$(J)<>"REPEAT"THEN12070
12090 A$(J)=""
12100 A$(I)="IFNOT("+RIGHT$(A$(I),LEN(A$(I))-5)+")THEN"+STR$((J+1)*10)
12110 GOTO11570
12120 REM ***********************
12130 REM * PREC. SALTI INCOND. *
12140 REM ***********************
12150 H=0
12160 H=H+1:IFMID$(A$(I),H,1)<>"*"THEN12160
12170 H=H-1:K=LEN(A$(I))-H
12180 B$=RIGHT$(A$(I),K)
12190 M=1
12200 M=M+1:IFLEFT$(A$(M),K)<>B$THEN12200
12210 A$(I)=LEFT$(A$(I),H)+STR$((M+1)*10)
12220 IFLEFT$(A$(I),4)="GOTO"THENA$(M)=A$(M)+"%"
12230 GOTO11570
12240 REM ***********************
12250 REM * PREC. WHILE...DO... *
12260 REM ***********************
12270 B$=MID$(A$(I),6,LEN(A$(I))-7)
12280 A$(I)="IFNOT("+B$+")THEN"
12290 IFA$(I+1)="BEGIN"THEN12330
12300 A$(I)=A$(I)+STR$((I+2)*10)
12310 A$(I+1)="@"+A$(I+1)+":GOTO"+STR$(I*10)
12320 GOTO11570
12330 GOSUB12880
12340 A$(I)=A$(I)+STR$((J+1)*10)
12350 A$(I+1)=""
12360 A$(J)="@GOTO"+STR$(I*10)
12370 GOTO11570
12380 REM **********************
12390 REM * PRECOMPILAZIONE IF *
12400 REM **********************
12410 IFA$(I+1)="BEGIN"THEN12490
12420 A$(I)=A$(I)+A$(I+1)
12430 A$(I+1)=""
12440 IFA$(I+2)<>"ELSE"THENA$(I)="#"+A$(I):GOTO11570
12450 D=I:I=I+2:GOSUB12880:I=D
12460 A$(I)=A$(I)+":GOTO"+STR$((J+1)*10)
12470 A$(I+2)=""
12480 I=I+2:GOTO11570
12490 A$(I)=A$(I)+STR$((I+2)*10)
12500 GOSUB12880
12510 A$(I+1)="@GOTO"+STR$((J+1)*10)
12520 A$(J)=""
12530 IFA$(J+1)<>"ELSE"THEN11570
12540 H=J:D=I:I=J+1
12550 GOSUB12880:I=D
12560 A$(H)="@GOTO"+STR$((J+1)*10)
12570 A$(H+1)="":GOTO11570
12580 REM ***********************
12590 REM * PRECOMPILAZIONE FOR *
12600 REM ***********************
12610 H=0
12620 IFRIGHT$(A$(I),2)="DO"THEN12980
12630 IFA$(I+1)="BEGIN"THEN12670
12640 IFLEFT$(A$(I+1),3)="FOR"THENI=I+1:H=H+1:GOTO12620
12650 A$(I+1)=A$(I+1)+":NEXT"
12660 J=I+1:GOTO12700
12670 GOSUB12880
12680 A$(I+1)=""
12690 A$(J)="NEXT"
12700 IFH=0THEN11570
12710 FORT=1TOH
12720 A$(J)=A$(J)+":NEXT"
12730 NEXT:GOTO11570
12740 IFRIGHT$(A$(I),1)="%"THENA$(I)="":GOTO11630
12750 A$(I)=""
12760 IFA$(I+1)<>"BEGIN"THENA$(I+1)=A$(I+1)+":RETURN":GOTO11630
12770 GOSUB12880:A$(I+1)="":A$(J)="RETURN":GOTO11630
12780 L=LEN(A$(I))-1
12790 L=L-1
12800 IFMID$(A$(I),L,1)<>" "THEN12790
12810 S=VAL(RIGHT$(A$(I),LEN(A$(I))-L))-10
12820 S=S+10:IFA$(S/10)=""ORA$(S/10)="END"ORA$(S/10)="BEGIN"THEN12820
12830 A$(I)=LEFT$(A$(I),L)+STR$(S):GOTO11680
12840 REM ***********************
12850 REM * RICERCA  DELL'  END *
12860 REM * RELATIVO A UN BEGIN *
12870 REM ***********************
12880 K=0
12890 J=I
12900 J=J+1
12910 IFRIGHT$(A$(J),5)="BEGIN"THENK=K+1:GOTO12900
12920 IFA$(J)="END"THENK=K-1:KK=1
12930 IFK<>0THEN12900
12940 RETURN
12950 REM **************************
12960 REM * PREC. FOR I=..;..;..DO *
12970 REM **************************
12980 K=0:A$="RESTORE:DATA":T=0
12990 K=K+1:IFMID$(A$(I),K,1)<>"="THEN12990
13000 FORR=K+1TOLEN(A$(I))-2
13010 IFMID$(A$(I),R,1)=";"THENA$=A$+",":T=T+1:GOTO13030:
13020 A$=A$+MID$(A$(I),R,1)
13030 NEXT
13040 B$=MID$(A$(I),4,K-4)
13050 A$(I)=A$+":FORII=1TO"+STR$(T+1)+":READ"+B$
13060 A$="":GOTO12620


