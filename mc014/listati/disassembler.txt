10 REMBRKABPLJJSRBBMIJRTIABVCJRTSABVSJ
20 REM----BCCJLDYDBCSJCPYDBNEJCPXDBEQJ
30 REMORAGORAHANDGANDHEORGEORHADCGADCH
40 REMSTAGSTAHLDAGLDAHCMPGCMPHSBCGSBCH
50 REM--------BITC--------------------
60 REMSTYCSTYILDYCLDYICPYC----CPXC----
70 REMORACORAIANDCANDIEORCEORIADCCADCI
80 REMSTACSTAILDACLDAICMPCCMPISBCCSBCI
90 REMASLCASLIROLCROLILSRCLSRIRORC----
100 REMSTXCSTXILDXCLDXIDECCDECIINCCINCI
110 REMPHPACLCAPLPASECAPHAACLIAPLAASEIA
120 REMDEYATYAATAYACLVAINYACLDAINXASEDA
130 REMORADORAFANDDANDFEORDEORFADCDADCF
140 REM----STAFLDADLDAFCMPDCMPFSBCDSBCF
150 REMASLM----ROLH----LSRM----RORM----
160 REMTXAATXSATAXATSXADEXA----NOPA----
170 REM--------BITB----JMPB----JMPK----
180 REMSTYB----LDYBLDYECPYB----CPXB----
190 REMORABORAEANDBANDEEORBEOREADCBADCE
200 REMSTABSTAELDABLDAECMPBCMPESBCBSBCE
210 REMASLBASLEROLBROLELSRBLSRERORB----
220 REMSTXB----LDXBLDXFDECBDECEINCBINCE
230 DATA0,1,0,16,2,3,4,16,5,6,7,16,8,9,10,16
240 POKE56,26:DIMA%(15)
250 FORI=0TO15:READA%:A%(I)=A%:NEXT
260 P$="":INPUTP$
262 IFRIGHT$(P$,1)="H"THENP$=LEFT$(P$,LEN(P$)-1):GOTO600
264 IFP$<>""THENGOSUB580:F=0
266 IFF=1THEN600
270 FORI=0TO21:D=X:GOSUB570:PRINT":  ";
280 L=PEEK(X)AND15:H=(PEEK(X)AND240)/16:M=A%(L)
290 IFM>15THEN550
300 IFL<>2THEN330
310 IFH<>10THEN550
320 S$="LDXD":GOTO360
330 S=PEEK(43)+PEEK(44)*256+76*M-38*(H>7)+4*(HAND7)+5
340 IFPEEK(S)=45THEN550
350 S$=CHR$(PEEK(S))+CHR$(PEEK(S+1))+CHR$(PEEK(S+2))+CHR$(PEEK(S+3))
360 M=ASC(RIGHT$(S$,1))-64:S$=LEFT$(S$,3)
370 PRINTS$" ";:GOSUB520:ONMGOSUB390,400,410,420,430,440,450,460,470,480,490,500,510
380 NEXT:GOTO260
390 PRINT:RETURN
400 PRINT"$";:GOSUB560:PRINT:RETURN
410 PRINT"$";:GOSUB530:GOSUB520:PRINT:RETURN
420 PRINT"#$";:GOSUB530:IFPEEK(X)>31ANDPEEK(X)<96THENPRINT" '"CHR$(PEEK(X));
422 GOSUB520:PRINT:RETURN
430 PRINT"$";:GOSUB560:PRINT",X":RETURN
440 PRINT"$";:GOSUB560:PRINT",Y":RETURN
450 PRINT"($";:GOSUB530:GOSUB520:PRINT",X)":RETURN
460 PRINT"($";:GOSUB530:GOSUB520:PRINT"),Y":RETURN
470 PRINT"$";:GOSUB530:GOSUB520:PRINT",X":RETURN
480 M=PEEK(X):D=X+(256-M)*(M>127)-M*(M<128)+1:D=D+65536*(D>65535)
482 PRINT"$";:GOSUB570:GOSUB520:PRINT:RETURN
490 PRINT"($";:GOSUB560:PRINT")":RETURN
500 PRINT"$";:GOSUB530:GOSUB520:PRINT",Y":RETURN
510 PRINT"A":RETURN
520 X=-(X<65535)*(X+1):RETURN
530 H=(PEEK(X)AND240)/16:L=PEEK(X)AND15
540 PRINTCHR$(H-7*(H>9)+48)+CHR$(L-7*(L>9)+48);:RETURN
550 PRINT"    ";:GOSUB530:IFPEEK(X)>31ANDPEEK(X)<96THENPRINT" '"CHR$(PEEK(X));
552 GOSUB520:PRINT:GOTO380
560 H=(PEEK(-(X<65535)*(X+1))AND240)/16:L=PEEK(-(X<65535)*(X+1))AND15
562 GOSUB540:GOSUB530:GOSUB520:GOSUB520:RETURN
570 M=INT(D/256):H=(MAND240)/16:L=MAND15:GOSUB540
572 M=D-INT(D/256)*256:H=(MAND240)/16:L=MAND15:GOSUB540:RETURN
580 IFLEN(P$)>4THENP$=LEFT$(P$,4)
582 X=0:FORI=0TOLEN(P$)-1:C$=MID$(P$,LEN(P$)-I,1)
584 X=X+2^(4*I)*(ASC(C$)+7*(C$>"9")-48):NEXT:RETURN
600 IFP$<>""THENGOSUB580
610 F=1:X=INT(X/4)*4:FORI=0TO21:D=X:GOSUB570:C$="":PRINT":";
620 FORD=0TO3:GOSUB530:PRINT" ";:IFPEEK(X)>32ANDPEEK(X)<96THENC$=C$+CHR$(PEEK(X)):GOTO640
630 C$=C$+" "
640 GOSUB520:NEXT:PRINT"(LEFT)'"C$:NEXT:GOTO260

