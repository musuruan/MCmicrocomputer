2 PRINT"(CLR)"
5 PRINT"  CALCOLO DI  E  CON IL NUMERO DI CIFRE    ESATTE DESIDERATE":PRINT
10 INPUT"QUANTE CIFRE DECIMALI ESATTE";DE
15 REM ===================== CALCOLI == PRELIMINARI ============================
20 T1=TI
25 PR=9
30 G=INT((PR-1)/2)
35 P=INT(10^G)
40 N1=DE+G
45 K=K+1
50 A=A+LOG(K): B=A/LOG(10)
55 IF B>N1 THEN N=K :GOTO 65
60 GOTO 45
65 PRINT "N="N,
70 M=INT(B/4)+2 :CE=G*(M-2):PRINT"M="M
75 DIM C(M): C(1)=1 :PRINT
80 PRINT" N-H =":PRINTTAB(10)"(UP)   (LEFT)(LEFT)(LEFT)(LEFT)(LEFT)"
85 REM ********************** INIZIO ** CALCOLI ** TERMINI ** SOMMATORIA *******
90 FOR H=N TO 1 STEP-1
95 PRINTTAB(10)"(UP)   (LEFT)(LEFT)(LEFT)(LEFT)(LEFT)"H
100 FOR I=1 TO M-1
105 Q=INT(C(I)/H): R=C(I)-Q*H
110 C(I)=Q: C(I+1)=C(I+1)+R*P
115 NEXT I
120 C(1)=C(1)+1
125 NEXT H
130 REM ********************* PRESENTAZIONE ** RISULTATI ***********************
135 D$="E": PRINT: PRINT"E= 2.",
140 FOR L=2 TO M-1
145 IF C(L)<10 THEN PRINT "(RGHT)(RGHT)(RGHT)"C(L)"(LEFT)(LEFT)(LEFT)(LEFT)(LEFT)000",:GOTO 165
150 IF C(L)<100 THEN PRINT "(RGHT)(RGHT)"C(L)"(LEFT)(LEFT)(LEFT)(LEFT)(LEFT)00",:GOTO 165
155 IF C(L)<1000 THEN PRINT "(RGHT)"C(L)"(LEFT)(LEFT)(LEFT)(LEFT)(LEFT)0",:GOTO 165
160 PRINT C(L),
165 NEXT L
170 T2=TI
605 REM ===================== CALCOLO == DURATA == EFFETTIVA ===================
610 PRINT:PRINT:PRINT"TEMPO IMPIEGATO:"
615 DT= (T2-T1)/60
620 IF DT>=3600 THEN 645
625 IF DT>=60 THEN 670
630 TR=INT(DT*100)/100+.01
635 PRINTTAB(16)"(UP)"TR"SEC."
640 GOTO 685
645 DH=DT/3600 :HO=INT(DH)
650 DM=(DH-HO)*60: MN=INT(DM)
655 S1=(DM-MN)*60: S2=INT(S1) +1
660 PRINTTAB(16)"(UP)"HO"(CBM-N)(CBM-S). "MN"MIN."S2"SEC."
665 GOTO 685
670 DM=DT/60:MN=INT(DM)
675 S1=(DM-MN)*60: S2=INT(S1)+1
680 PRINTTAB(16)"(UP)"MN"MIN. "S2"SEC."
685 PRINT:PRINT" "D$" RISULTA CALCOLATO CON"CE"CIFRE"
687 PRINT"   DECIMALI ESATTE DOPO LA VIRGOLA":PRINT
690 REM ********************* ISTRUZIONI ** PER ** STAMPANTE *******************
695 C(M)=0:PRINT
700 PRINT:PRINT"SI VUOLE LA STAMPA?"
705 PRINT"(SE SI,PREMERE TASTO S;                  ALTRIMENTI PREMERE TASTO N)"
710 GET A$:IF A$=""  THEN 710
715 IF A$= "N" THEN END
720 IF A$<>"S"THEN 710
725 TR$=STR$(TR):SC$=TR$+" SEC."
730 CE$=STR$(CE)
735 Q=0:H=0
740 OPEN1,4
745 OPEN2,4,2
750 OPEN3,4,1
770 B$="AAAAA"
775 PRINT#2,B$
780 PRINT#3,CHR$(10);
785 E$=CHR$(69):PRINT#3,CHR$(17)E$" = 2.";CHR$(145);CHR$(141);
790 G$="     ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ 2ZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ "
795 PRINT#2,G$
800 FORJ=2 TO 10 : IF J>M-1 THEN 810
805 PRINT#3,C(J);
810 NEXT J
815 IF M<12 THEN PRINT#3,CHR$(141):         GOTO 870
820 PRINT#3,C(11)
825 F$="     ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ ZZZZ "
830 PRINT#2,F$
835 Q=Q+10 :IF Q>=M-2 THEN 870
840 FOR L=2 TO 10  :IF Q+L>M-1 THEN 850
845 PRINT#3,C(Q+L);
850 NEXT
855 IF M<Q+12 THEN PRINT#3,CHR$(141):         GOTO 870
860 PRINT#3,C(Q+11)
865 GOTO 835
870 PRINT#3,CHR$(10);CHR$(141)
875 PRINT#3,CHR$(145)CHR$(17)E$CHR$(146)" RISULTA CALCOLATO CON";CE$" CIFRE DECIMALI ESATTE "
880 PRINT#3," DOPO LA VIRGOLA"
890 PRINT#3,CHR$(141)
900 IF DT>=60 THEN 920
910 PRINT#3," TEMPO IMPIEGATO:";SC$:  GOTO 970
920 MN$=STR$(MN):S2$=STR$(S2)
930 MT$=MN$+" MII. "+S2$+" SEC."
940 HO$=STR$(HO):HT$=HO$+" (SHIFT-H) ."+MT$
950 IF DT>=3600 THEN PRINT#3,"TEMPO IMPIEGATO:";HT$: GOTO 970
960 IF DT>=60 THEN PRINT#3,"TEMPO IMPIEGATO:";MT$
970 PRINT#3,CHR$(10);CHR$(141)
980 CLOSE1:CLOSE2:CLOSE3
985 PRINT" VUOI LA RISTAMPA ?":GOTO 705
990 END

