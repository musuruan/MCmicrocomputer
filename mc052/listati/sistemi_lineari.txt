1 REM                             SISTEMI LINEARI
2 REM                             ---------------
3 REM
4 REM
5 REM ----------------------------------------------------------------
6 REM
7 REM INGRESSO DATI, LORO CONTROLLO E INIZIALIZZAZIONE DELLE VARIABILI
8 REM
9 REM ----------------------------------------------------------------
10 PRINTCHR$(147):INPUT"EQUAZIONI";EQ:PRINT
20 INPUT"INCOGNITE";IN:PRINTCHR$(147)
30 IFEQ<>INT(EQ)ORIN<>INT(IN)THEN10
40 IFEQ<2ORIN<2THEN10
50 DIMA(EQ,IN+1):DIMP(EQ)
60 FORI=1TOEQ
70 FORJ=1TOIN+1:PRINT"A("I;J")":PRINT:PRINT:INPUTA(I,J):PRINT"(CLR)":NEXT
80 FORJ=1TOIN
90 IFA(I,J)<>0THEN110
100 NEXT:RUN
110 J=IN:NEXT:NEXT
120 K=1:N=1:E1=EQ:V=1:S=1
125 REM --------------------------------------
126 REM
127 REM CALCOLO DEL SISTEMA DISPOSTO A GRADINI
128 REM
129 REM --------------------------------------
130 IFA(N,N)<>0THEN180
140 FORI=KTOEQ
150 IFA(I,N)<>0THEN170
160 NEXT:K=K+1:GOTO220
170 FORJ=KTOIN+1:Z=A(N,J):A(N,J)=A(I,N):A(I,N)=Z:NEXT
180 K=K+1
190 FORI=KTOEQ
200 FORJ=KTOIN+1:A(I,J)=A(I,J)-(A(I,N)/A(N,N))*A(N,J):NEXT
210 NEXT
215 REM ---------------------------------------------
216 REM
217 REM CONTROLLO DEI COEFFICIENTI E DEI TERMINI NOTI
218 REM
219 REM ---------------------------------------------
220 FORI=KTOEQ
230 FORJ=KTOIN
240 IFA(I,J)=0THEN260
250 J=IN:NEXT:NEXT:GOTO370
260 NEXT
270 IFA(I,IN+1)=0THEN300
280 PRINT"SISTEMA:INCOMPATIBILE":P=I+W:PRINT
290 PRINT"EQUAZIONE";P:END
295 REM ---------
296 REM
297 REM GRIGLIA 1
298 REM
299 REM ---------
300 P=I+W:V=V+1:P(V)=P
310 PRINT"EQUAZIONE";P:PRINT:PRINT"DIPENDENTE" :PRINT
320 IFI=EQ THEN360
330 FORM=ITOEQâ€”1
340 FORJ=KTOIN+1:Z=A(M,J):A(M,J)=A(M+1,J):A(M+1,J)=Z:NEXT
350 NEXT:W=W+1:EQ=EQ-1:GOTO220
360 EQ=EQ-1
365 REM ---------------------------------
366 REM
367 REM CONTROLLO SUL SISTEMA E SOLUZIONI
368 REM
369 REM ---------------------------------
370 IFEQ=IN THEN540
380 IFEQ>IN THEN460
390 IFK>=EQ THEN410
400 N=N+1:GOTO130
410 A=IN-EQ
420 PRINT"SOLUZIONI:INFINITE^";A:PRINT
430 PRINT"EQUAZIONI:"E1:PRINT
440 PRINT"EQUAZIONI":PRINT:PRINT"INDIPENDENTI:";EQ:PRINT
450 PRINT"INCOGNITE:";IN:END
460 IFK=IN THEN480
470 GOTO400
480 FORI=IN TO EQ-1
490 A1=A(I,IN+1)/A(I,IN):A2=A(I+1,IN+1)/A(I+1,IN)
500 A1=INT(A1*1000+.5)/1000:A2=INT(A2*1000+.5)/1000
510 IFA1=A2THEN530
520 GOTO650
530 NEXT:EQ=IN:GOTO750
540 IFK=EQ THEN560
550 GOTO400
560 DIMX(IN+1)
570 PRINT"SOLUZIONI:":PRINT
580 FORI=IN TO 1 STEP-1
590 FORJ=I+1 TO IN:X(I)=-A(I,J)*X(J)+X(I):NEXT
600 X(I)=(X(I)+A(I,IN+1))/A(I,I)
610 Y=INT(X(I)*1000+.5)/1000
620 PRINT"X("I")=";Y;"->":PRINT
630 GETA$:IFA$=""THEN630
640 NEXT:GOTO430
645 REM ---------
646 REM
647 REM GRIGLIA 2
648 REM
649 REM ---------
650 L=I+1
660 FORM=2TOE1
670 FORJ=2TOE1
680 IFP(J)=M THEN720
690 J=E1
700 NEXT:NEXT
710 PRINT"SISTEMA: INCOMPATIBILE":PRINT:PRINT"EQUAZIONE";L:END
720 IFM<L THEN740
730 GOTO690
740 L=L+1:GOTO690
745 REM ----------
746 REM
747 REM GRIGLIA 3
748 REM
749 REM ---------
750 FORI=2TOE1
760 FORJ=2TOE1
770 IFI=P(J)THEN810
780 NEXT:S=S+1
790 IFS<=IN THEN820
800 PRINT"EQUAZIONE";I:PRINT:PRINT"DIPENDENTE":PRINT:GOTO820
810 J=E1:NEXT
820 NEXT:GOTO370

