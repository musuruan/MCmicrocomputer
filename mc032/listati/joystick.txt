100 REM *****************************
101 REM ***   USIAMO I JOYSTICK   ***
102 REM *****************************
103 :
108 REM * MEMORIZZIAMO GLI SPOSTAMENTI
109 :
110 DATA0,-40,40,0,-1,-41,39,0,1,-39,41
120 FORT=0TO10:READX(T):NEXT
121 :
125 REM * IN B INIZIA LA MAPPA VIDEO
126 REM * IN C+B INIZIA LA MAPPA COLORE
127 :
130 B=1024:C=54272
133 :
134 REM * FNJ(I) SCEGLIE UN VALORE
135 REM * DA 0 A 10 COME IN TAB. 1;
136 :
140 DEFFNJ(I)=15-PEEK(56320+I)AND15
150 FORI=0TO1:A=X(FNJ(I))
152 POKEB,32:POKEC+B,6
154 B=B+A
155 :
156 REM * VERIFICHIAMO LA POSIZIONE
157 REM * PER RESTARE NELLO SCHERMO
158 :
160 IFB<1024THENB=1024
170 IFB>2023THENB=2023
190 POKEB,81:POKEC+B,1
192 IF(PEEK(56320+I)AND16)=16THEN200
195 PRINT"FUOCO!"
200 NEXT:GOTO150

