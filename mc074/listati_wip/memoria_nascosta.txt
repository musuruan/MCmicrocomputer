10 CL$=CHR$(147) :    REM CLEAR SCREEN
20 AD=65000 :         REM MEMORIZZEREMO NELLA LOCAZIONE 65000 (RAM NASCOSTA)
30 MS=49152 :         REM LA ROUTINE IN LINGUAGGIO MACCHINA A PARTIRE DA 49152
40 :                  REM LA LOCAZIONE 1 CONTERRA' IL VALORE DI BL
50 D=128 :            REM METTE NELLA VARIABILE "D" IL BYTE DI DATI DESIDERATO
60 POKE AD,D :        REM SCRIVE IL CONTENUTO DI "D" NELLA LOCAZIONE AD
70 :
80 IF AD>40959 AND AD<49152 THEN BL=54 : GOTO 160
90 :                  REM SE BL=54 LA ROM BASIC E' DISATTIVATA
100 IF AD>53247 AND AD<57344 THEN BL=51 : GOTO 160
110 :                 REM SE BL=51 LE ROUTINES DI I/O SONO DISATTIVATE
120 IF AD>57343 AND AD<65536 THEN BL=53 : GOTO 160
130 :                 REM SE BL=53 LA ROM KERNAL E' DISATTIVATA
140 PRINT CL$;"ERRORE, SCRITTURA IN AREA NON NASCOSTA" : END
145 :
150 :                 REM QUESTI SONO I DATA PER LA ROUTINE IN CODICE MACCHINA
160 DATA 162,00,134,1,174,00,00,134,251,162,55,134,1,96
170 FOR I=0 TO 13 :   REM INIZIO CICLO DEL CARICATORE BASIC
180 READ ML :         REM LEGGE BYTE DAI DATA
190 POKE MS+I,ML :    REM CARICA IL BYTE IN MEMORIA A PARTIRE DA 49152
200 NEXT I :          REM FINE CICLO
210 :
220 POKE MS+1,BL :    REM PONE IL VALORE DI BL NELLA LOCAZIONE 1 DELLA ROUTINE
225 :                 REM IN L.M. (SERVIRA' AD ABILITARE LA RAM NASCOSTA)
227 :
230 HI=INT(AD/256) :  REM BYTE ALTO DELL'INDIRIZZO DELLA RAM NASCOSTA
240 LO=AD-256*HI :    REM BYTE BASSO DELL'INDIRIZZO DELLA RAM NASCOSTA
250 POKE MS+5,LO :    REM CARICA L'INDIRIZZO, BYTE BASSO NELLA ROUTINE IN L.M.
260 POKE MS+6,HI :    REM CARICA L'INDIRIZZO, BYTE ALTO NELLA ROUTINE
270 POKE 56333,127 :  REM DISABILITA LE INTERRUZIONI
280 SYS(MS) :         REM ESEGUE LA ROUTINE IN CODICE MACCHINA (CHE LEGGE UN
285 :                 REM BYTE DA HI-LO E LO SCRIVE NELLA LOCAZIONE 251)
290 POKE 56333,129 :  REM RIABILITA LE INTERRUZIONI
300 A=PEEK(251) :     REM LEGGE IL BYTE DI DATI (DISPONIBILI ANCHE 252/253/254)
310 PRINT CL$ :       REM CANCELLA LO SCHERMO
315 :                 REM SCRIVE I DUE BYTE ROM E RAM PER NOTARE LE DIFFERENZE
320 PRINT "VALORE IN ROM";PEEK(AD)
325 PRINT "VALORE IN RAM";A
330 END
340 :
350 REM DI SEGUITO E' MOSTRATA LA ROUTINE IN L.M. PER I DATI DEL PROGRAMMA
355 REM CAMBIANDO I VALORI DI AD E DI D CHIARAMENTE ALCUNI BYTE CAMBIANO
360 :
370 C000 LDX #$35     ' CARICA X CON 53
380 C002 STX $01      ' TRASFERISCE DA X A 1 DISABILITANDO LA ROM
390 C004 LDX $FDE8    ' CARICA X CON IL CONTENUTO DI 65000
400 C007 STX $FB      ' TRASFERISCE DA X A 251
410 C009 LDX #$37     ' CARICA X CON 55
420 C00B STX $01      ' TRASFERISCE DA X A 1 E RIATTIVA LE ROM BASIC-I/O-KERNAL
430 C00D RTS          ' RITORNO AL BASIC

