100 REM$L-,$S-,$O+
102 GOTO120
104 :
106 V=LEN(A$):L(V)=L(V)+1:L=L(V)+16*V:CA$(L,1)=A$:CA$(L,0)=C$(J,0):RETURN
108 :
110 PRINT"(HOME)(HOME)(CLR)NO/SCROLL:PAUSA,  ESCAPE:ESCE ...":PRINTL$:PRINTCHR$(27)"T";:RETURN
112 :
114 CHAR1,W1,22,CHR$(27)+"Q":RETURN
116 :
118 REM INIZIALIZZAZIONE
120 IFRGR(0)=5THENFAST:W=79:W1=20:W2=20:COLOR6,1:ELSESLOW:W=39:W1=0:W2=10:COLOR0,1:COLOR4,1
122 FORJ=1TO8:KEYJ,"":NEXT:BANK15:POKE4104,0:POKE4105,0
124 IT$="(RVS)TASTIERA(OFF) DISCO":ID$="TASTIERA (RVS)DISCO(OFF)"
126 SI=54272:POKESI+24,15:POKESI+5,0:POKESI+6,9*16
128 POKESI,23:POKESI+1,80:POKESI+2,240:POKESI+3,240
130 TT=250:GOSUB218:S=0:I=0:II$=IT$:II=0
132 FORJ=1TO3:S1$=S1$+CHR$(24):S2$=S2$+CHR$(25):NEXT
134 FORJ=1TO W:L$=L$+"-":NEXT:L$="(GRN)"+L$+"(YEL)"
136 :
138 DIM C$(218,1),CA$(129,1),L(8)
140 FORJ=32TO218:C$(J,0)=CHR$(J):NEXT
142 :
144 FORJ=65TO90:READA$:GOSUB106:REM CARATTERI ALFABETICI
146 : C$(J,1)=A$ :WA$=WA$+CHR$(J)
148 : C$(J+128,1)=A$:WA$=WA$+CHR$(J+128)
150 NEXT
152 FORJ=48TO57:WA$=WA$+CHR$(J):READA$:C$(J,1)=A$:GOSUB106:NEXT:REM NUMERI
154 FORJ=21TO27:WA$=WA$+CHR$(J):READA$:READB$:C$(J,0)=B$:C$(J,1)=A$:GOSUB106:NEXT:REM SIMB. SPEC.
156 FORI=1TO12:READA:READA$:WA$=WA$+CHR$(A):C$(A,0)=CHR$(A):C$(A,1)=A$:J=A:GOSUB106:NEXT:REM PUNTEGG.
158 :
160 REM ========= MENU ==========
162 POKE208,0:PRINTCHR$(27)"N(HOME)(HOME)(CLR)(SWLC)(GRN)"
164 CHAR1,W1+1,0,">>>      C128  MORSE SCHOOL!!      <<<(YEL)",1
166 CHAR1,W1+9,3,"MENU'"
168 CHAR1,W1+1,4,"(GRN).- -.. ... .-.-.- ... --- ..-. - .-.-.(YEL)"
170 CHAR1,W1+9,6,"1) VISUALIZZA SIMBOLI."
172 CHAR1,W1+9,8,"2) RICEZIONE."
174 CHAR1,W1+9,10,"3) TRASMISSIONE."
176 CHAR1,W1+9,12,"4) VELOCITA':"+STR$(TT)
178 CHAR1,W1+9,14,"5) TESTO: "+II$
180 CHAR1,W1+9,16,"6) FINE."
182 CHAR1,W1+1,18,"(GRN)-.. . ... .- -... -... .- - .- .-.-.-(YEL)"
184 CHAR1,W1+9,20,"SCEGLI  (1/6)..."
186 CHAR1,W1+1,24,"(GRN)>>> ADS.SOFTWARE  BY A. DE SABBATA <<<(YEL)",1
188 DO:GETKEYA$:IN=INSTR("123456",A$):LOOPUNTILIN
190 ON IN GOSUB 240,292,372,198,230,422
192 ON IN GOTO  162,162,162,188,188,188
194 :
196 REM RIDEFINISCE VELOCITA'
198 CHAR1,W1+1,22,"[SCIFT]/(+/-):MODIFICA, RETURN:RITORNA"
200 DO:GETKEYA$:A=INSTR("(SHIFT-+)(SHIFT--)+-"+CHR$(13),A$):LOOPUNTILA
202 ONAGOTO204,206,208,210,216
204 TT=TT+(10ANDTT<990):GOTO212
206 TT=TT-(10ANDTT>14):GOTO212
208 TT=TT+(1ANDTT<999):GOTO212
210 TT=TT-(1ANDTT>5)
212 CHAR1,W1+22,12,STR$(TT)+"  ":GOTO200
214 :
216 GOSUB114
218 TP=INT(1000/TT*W2):REM DURATA PUNTO
220 T1=TP     :REM SOLO PER PROGRAMMA DA COMPILARE
222 T1=TP/12  :REM SOLO PER PROGRAMMA SORGENTE
224 T2=T1*3   :RETURN
226 :
228 REM RIDEFINISCE INPUT
230 IF II THEN II$=IT$:II=0:ELSE II=1:II$=ID$
232 CHAR1,W1+19,14,II$
234 RETURN
236 :
238 REM VISUALIZZA SIMBOLI
240 SCNCLR:CHAR1,0,0,L$
242 CHAR1,5,1,"(GRN)     CARATTERI ALFABETICI    (YEL)",1:PRINT
244 FORJ=65 TO90 STEP 2
246 : FORI=0TO1
248 :   PRINTTAB(5+20*I)C$(J+I,0),C$(J+I,1);:IFI=1THENPRINT
250 : NEXT
252 NEXT
254 :
256 CHAR1,5,15,"(GRN)       CODICI SPECIALI       (YEL)",1:PRINT
258 RESTORE 1078
260 FORJ=1TO7: READ A$:READB$
262 : PRINTTAB(10)A$,B$
264 NEXT
266 IFW=39THENPRINT:PRINTL$;:GOSUB286:SCNCLR:PRINTL$
268 CHAR1,W-30,1,"(GRN)  CARATTERI NUMERICI  (YEL)",1:PRINT
270 FORJ=48 TO57
272 : PRINTTAB(W-25)C$(J,0),C$(J,1)
274 NEXT
276 :
278 CHAR1,W-30,12,"(GRN)    PUNTEGGIATURA     (YEL)",1:PRINT:RESTORE1094
280 FORJ=1TO11:READA:READA$:IFA=32THENB$="SPACE":ELSEB$=CHR$(A)
282 : PRINTTAB(W-25)B$CHR$(27)"O",C$(A,1)
284 NEXT:PRINTL$;
286 CHAR1,W1+9,24,"(GRN)  PREMI UN TASTO...  ",1:GETKEYA$:RETURN
288 :
290 REM --- ROUTINE DI TRASMISSIONE TESTO (SS$) ---
292 Y$="": REM ROUTINE DI TRASMISSIONE TESTO (SS$)
294 IFIITHENBEGIN:FI$=""
296 : CHAR1,W1+1,22,"NOME FILE : ":INPUTFI$:IFFI$=""THENRETURN
298 : FI$=LEFT$(FI$,16)
300 : GOSUB110
302 : DOPEN#3,(FI$):IFDSTHENPRINTDS$:CLOSE3:GOTO332
304 : SS$=S1$:GOSUB340
306 : DO:INPUT#3,A$:SA=ST:IFA$=""THEN310
308 : SS$=A$:GOSUB340
310 : IFSA>0ORY$=CHR$(27)THENEXIT
312 : LOOP
314 : CLOSE3:GOTO326
316 BEND
318 TS$="":INPUT"(HOME)(HOME)(CLR)(SWLC)TESTO: ";TS$:IFTS$=""THEN334
320 GOSUB110
322 SS$=S1$:GOSUB340
324 SS$=TS$:GOSUB340
326 SS$=S2$:GOSUB340
328 :
330 PRINT:PRINT"(HOME)(HOME)OK!... PREMI SPAZIO PER CONTINUARE"CHR$(27)"Q"
332 GETKEYA$:IFA$<>CHR$(32)THEN332
334 RETURN
336 :
338 REM --- TRASFORMA SS$ IN CODICE MORSE ---
340 FORJ=1TOLEN(SS$):GETY$:IFY$=CHR$(27)THENJ=255
342 : K=ASC(MID$(SS$,J)):IFK=32THENPRINT"(RVS) (OFF) ";:GOTO356
344 : IFINSTR(WA$,CHR$(K))=0THEN360
346 : PRINTC$(K,1)"(DOWN)(LEFT)(GRN)"C$(K,0)CHR$(27)"O(YEL)(UP) ";
348 : FORI=1TOLEN(C$(K,1))
350 :  POKESI+4,17:FORA=1TO(TP+((TP*2)ANDMID$(C$(K,1),I,1)="-")):NEXT:POKESI+4,0
352 :   FORA=1 TO TP:NEXT
354 : NEXT
356 : FOR A=1 TO TP*(3+(2AND K=32)):NEXT
358 : IF PEEK(236)>W-10THENPRINT:PRINT"(DOWN)"L$
360 NEXT
362 IFIITHENFORA=1TOTP*5:NEXT
364 IFPEEK(236)>0THENPRINT:PRINT"(DOWN)"L$
366 RETURN
368 :
370 REM ------ ROUTINE DI TRASMISSIONE ------
372 PRINT"(HOME)(HOME)(CLR)>> IL PUNTO SUL TASTIERINO E' IL TASTO  DA USARE. PREMI ESCAPE PER TERMINARE.<<":PRINTL$:PRINTCHR$(27)"T";
374 A$="":S=0
376 Y$=""
378 SS$=CHR$(26)+CHR$(26)+CHR$(26):GOSUB340
380 DO
382 : IFS>T2ANDA$<>""THEN388
384 : P=PEEK(212):IFP=72THENEXIT
386 : IFP<>82THENS=S+1:GOTO382
388 : IFS>T1ANDA$<>""THENBEGIN
390 :   B$="(RVS)?(OFF)":L=LEN(A$):IFL>8THEN398
392 :   FORJ=1TOL(L)
394 :     IF A$=CA$(J+16*L,1)THENB$=CA$(J+16*L,0):J=L(L)
396 :   NEXT
398 :   PRINT"(DOWN)(LEFT)"B$CHR$(27)"O(UP) ";:IFS=>T2THENPRINT"(RVS) (OFF) ";
400 :   IFPEEK(236)>W-10THENPRINT:PRINT"(DOWN)"L$
402 :   A$="":GOTO414
404 : BEND
406 :   S=0:POKESI+4,17
408 :   IFPEEK(212)=82THENI=I+1:GOTO408
410 :   IFI<T1 THENI$=".":ELSEI$="-"
412 :   A$=A$+I$:PRINTI$;:POKESI+4,0:I=0
414 LOOP
416 RETURN
418 :
420 REM ---------------- END -------------
422 CHAR1,W1+9,22,"SEI SICURO? (Y/N)"
424 GETKEYA$:IFA$="N"THENGOSUB114:RETURN:ELSEIFA$<>"Y"THEN424
426 PRINT"(HOME)(HOME)(CLR)":END
428 :
1000 REM ------------------- CARATTERI ALFABETICI
1002 DATA ".-"       :REM A
1004 DATA "-..."     :REM B
1006 DATA "-.-."     :REM C
1008 DATA "-.."      :REM D
1010 DATA "."        :REM E
1012 DATA "..-."     :REM F
1014 DATA "--."      :REM G
1016 DATA "...."     :REM H
1018 DATA ".."       :REM I
1020 DATA ".---"     :REM J
1022 DATA "-.-"      :REM K
1024 DATA ".-.."     :REM L
1026 DATA "--"       :REM M
1028 DATA "-."       :REM N
1030 DATA "---"      :REM O
1032 DATA ".--."     :REM P
1034 DATA "--.-"     :REM Q
1036 DATA ".-."      :REM R
1038 DATA "..."      :REM S
1040 DATA "-"        :REM T
1042 DATA "..-"      :REM U
1044 DATA "...-"     :REM V
1046 DATA ".--"      :REM W
1048 DATA "-..-"     :REM X
1050 DATA "-.--"     :REM Y
1052 DATA "--.."     :REM Z
1054 REM------------------- CARATTERI NUMERICI
1056 DATA "-----"    :REM 0
1058 DATA ".----"    :REM 1
1060 DATA "..---"    :REM 2
1062 DATA "...--"    :REM 3
1064 DATA "....-"    :REM 4
1066 DATA "....."    :REM 5
1068 DATA "-...."    :REM 6
1070 DATA "--..."    :REM 7
1072 DATA "---.."    :REM 8
1074 DATA "----."    :REM 9
1076 REM------------------- CODICI SPECIALI
1078 DATA "...-.-",  "ENDWRD" :REM 21 FINE PAROLA
1080 DATA "...-.",   "OK."    :REM 22 OK. CAPITO
1082 DATA "........","ERROR"  :REM 23 ERRORE
1084 DATA "-.-.-",   "START"  :REM 24 INIZIO TRASMISSIONE
1086 DATA ".-.-.",   "ENDTRS" :REM 25 FINE TRASMISSIONE
1088 DATA "-.-",     "READY"  :REM 26 INVITO ALLA TRASMISSIONE
1090 DATA ".-...",   "WAIT"   :REM 27 ATTESA
1092 REM ------------------ PUNTEGGIATURA
1094 DATA 32,"...-.-"         :REM SPAZIO (FINE PAROLA)
1096 DATA 34,".-..-."         :REM VIRGOLETTE
1098 DATA 39,".----."         :REM APOSTROFO
1100 DATA 40,"-.--.-"         :REM PARENTESI APERTA/CHIUSA
1102 DATA 41,"-.--.-"         :REM PARENTESI APERTA/CHIUSA
1104 DATA 44,"--..--"         :REM VIRGOLA
1106 DATA 45,"-....-"         :REM LINEETTA/ SEGNO MENO
1108 DATA 46,".-.-.-"         :REM PUNTO
1110 DATA 47,"-..-."          :REM BARRA
1112 DATA 58,"---..."         :REM DUE PUNTI
1114 DATA 61,"-...-"          :REM UGUALE
1116 DATA 63,"..--.."         :REM PUNTO INTERROGATIVO
1118 REM ------------------- END ---------

