1 REM ***********************
2 REM *                     *
3 REM *  MACCHINA DEL TEMPO *
4 REM *                     *
5 REM ***********************
6 REM
9 DIMO$(12),L(13):POKE36878,15:PRINTCHR$(14)
10 A$="":FED=0:I=0:SET=2
14 GOSUB8000:POKE36879,29:PRINT"(CLR)"
15 GOSUB5000
20 NO=0:WE=0:EA=0:SO=0:UP=0:DO=0:L$=""
31 ONSETGOSUB1,1000,1030,1060,1090,1150,1180,1120,1210,1240,1270,1300,1330,1360,1390
32 IFSET=16THENGOSUB1420
33 IFSET=17THENGOSUB1450
34 IFSET=18THENGOSUB1480
40 PRINT"(CLR)":GOSUB9100
41 PRINTL$
60 FOUND=0
61 TROV=0
62 FORN=1TO11-(SET<>7)
63 IFL(N)<>SETTHEN70
64 IFFOUNDTHEN68
66 PRINT"DI INTERESSANTE QUI   INTORNO VEDO",:POKE36879,26:PRINT"---------------------"
67 FOUND=-1
68 PRINTO$(N)
69 TROV=1
70 NEXTN
72 IFTROV=0THEN90
80 GOTO100
90 IFA$<>"GUARDA"ORFOUNDTHEN100
91 PRINT"NON VEDO NULLA DI SPE-CIALE"
100 POKE36879,29:INPUT"(BLK)(DOWN)COSA DEVO FARE";I$
102 IFI$=""THEN100
103 PRINT
105 IFSET<>10OR(RND(1)<=.95)THEN108
106 GOSUB9100:PRINT"IL CANE SI E'SVEGLIATOE TI HA SBRANATO"
107 GOTO9000
108 GOSUB500
110 IFA$="N"AND(NO)THENSET=NO:GOTO20
120 IFA$="S"AND(SO)THENSET=SO:GOTO20
130 IFA$="O"AND(WE)THENSET=WE:GOTO20
140 IFA$="E"AND(EA)THENSET=EA:GOTO20
150 IFA$="SCENDI"AND(DO)THENSET=DO:GOTO20
155 IFA$="SALI"THEN6100
160 IFA$="GIRA"THEN40"
170 IFA$="AIUTO"ORA$="AIUTAMI"THENGOSUB9100:PRINT"NESSUNO PUO' AIUTARTI":GOTO100
180 IFA$="PRENDI"ORA$="PIGLIA"THEN2000
190 IFA$="POSA"ORA$="LASCIA"ORA$="BUTTA"THEN4500
200 IFA$="APRI"THEN2500
205 IFA$="NUTRI"ORA$="ALIMENTA"ORA$="SFAMA"THEN3500
210 IFA$="GUARDA"THENGOSUB9100:PRINTL$:GOTO60
215 IFA$="NUOTA"THEN4000
220 IFA$="ROMPI"ORA$="SPACCA"ORA$="DISTRUGGI"THEN6000
225 IFA$="ACCENDI"THEN7500
230 IFA$="LEGGI"THEN6500
240 IFA$="SBLOCCA"THEN3000
250 IFA$="INVENTARIO"THENGOSUB5500:GOTO100
260 IFA$="INSERISCI"THEN7000
275 IFA$="BASTA"THEN9000
490 IFLEN(A$)=1THENGOSUB9100:PRINT"(RED)NON POSSO":GOTO100
498 GOSUB9100:PRINT"(RED)(RVS)NON POSSO(OFF): NON CONOSCOQUESTA PAROLA O NON SOAPPLICARLA ORA":GOTO100
500 SP=0:A$="":B$=""
503 FORN=1TOLEN(I$)
504 IFMID$(I$,N,1)=" "ANDNOTSPTHENSP=-1:GOTO520
510 IFNOTSPTHENA$=A$+MID$(I$,N,1)
515 IFSPTHENB$=B$+MID$(I$,N,1)
520 NEXTN
530 RETURN
1000 L$="SEI NELLA FORESTA.    CI SONO SENTIERI CHE  CONDUCONO A NORD E A  SUD."
1010 NO=15:SO=3
1020 RETURN
1030 L$="SEI NELLA FORESTA, CI SONO SENTIERI VERSO   NORD E EST E UNA RADU-RA A OVEST."
1040 NO=2:WE=4:EA=5
1050 RETURN
1060 L$="SEI NELLA RADURA. NEL MEZZO O'E' LA MACCHINADEL TEMPO SENZA ENER- "
1061 L$=L$+"GIA. L'USCITA E' A EST"
1070 EA=3:RETURN
1090 L$="SEI NELLA FORESTA.C'E'UN SENTIERO NELLA DIREZIONE EST-OVEST."
1100 WE=3:EA=8:RETURN
1120 L$="SEI NELLA FORESTA. DA UNA RADURA A NORD PUOIVEDERE UN LAGO. I SEN-"
1121 L$=L$+"TIERI PUNTANO A EST E A OVEST. C'E' UNA GROTTA A SUD."
1122 EA=12:WE=5:SO=6:RETURN
1150 L$="SEI IN UNA PICCOLA CA-VERNA. LA LUCE VIENE  DA NORD. C'E' UNA PIC-"
1151 L$=L$+"COLA PORTA A OVEST E  UNA APERTURA A SUD."
1152 SO=7:NO=8:RETURN
1180 L$="SEI IN UN'AMPIA CAVER-NA. C'E' UN BAULE IN  UN ANGOLO ED UNO SCHE-"
1181 L$=L$+"LETRO LI' VICINO. L'  USCITA E' A NORD."
1190 NO=6:RETURN
1210 L$="NON VEDO NIENTE !"
1220 UP=6:RETURN
1240 L$="SEI IN UN LUNGO CUNICOLO. PUOI UDIRE IL SUO-NO DELL'ACQUA CHE GOC-"
1241 L$=L$+"CIOLA. A NORD C'E' UNASCALA CON UN GROSSO CANE ADDDRMENTATO SULL' "
1242 L$=L$+"ULTIMO GRADINO."
1250 SO=9:RETURN
1270 L$="SEI SU UN'ISOLA NEL   CENTRO DEL LAGO. C'E' UN'APERTURA NEL TERRE-"
1271 L$=L$+"NO CON UNA SCALA DEN- TRO."
1280 DO=10
1300 L$="SEI ACCANTO AD UN LAGOCON UN'ISOLA NEL MEZZOA OVEST C'E' LA FORE- "
1301 L$=L$+"STA. A NORD C,E' UNA  ZONA SASSOSA."
1310 NO=13:WE=8:RETURN
1330 L$="SEI IN UNA PIETRAIA.ILLAGO E' A OVEST,A NORDC'E' UNA ZONA PALUDOSA"
1331 L$=L$+"L'USCITA E' A SUD E A NORD."
1340 NO=14:SO=12:RETURN
1360 PRINT"(HOME)(RED)";:POKE36879,138:FORJ=140TO240:POKE36875,J:PRINT"GLUG ";:NEXT
1362 PRINT"(HOME)(BLK)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)SONO APPENA FINITO NELLE SABBIE MOBILI."
1364 POKE36875,0:FORI=1TO5000:NEXT:PRINT"(CLR)":GOTO9000
1390 L$="SEI TRA DEI CESPUGLI. IL LAGO E' A EST,LA FORESTA E' A SUD E C'E' "
1391 L$=L$+"UN PRATO A NORD."
1400 NO=16:SO=2:RETURN
1420 L$="SEI IN UN PRATO.IL LA-GO E' A SUD-EST. A SUDCI SONO DEI CESPUGLI  "
1421 L$=L$+"E AD EST C'E' UNA TOR-RE."
1430 EA=17:SO=15:RETURN
1450 L$="SEI AI PIEDI DI UNA   TORRE.NON VEDO NESSUNAPORTA. AD OVEST C'E'  "
1451 L$=L$+"IL PRATO, AD EST UNA  ZONA PALUDOSA."
1460 WE=16:EA=14:UP=18:RETURN
1480 L$="SEI IN CIMA ALLA TORREA SUD SI VEDE IL LAGO CON L'ISOLA IN MEZZO E"
1481 L$=L$+"LA FORESTA ACCANTO. ALCENTRO DELLA PIATTAFORMA C'E' UNO SCRIGNO DI"
1482 L$=L$+"CRISTALLO NEL CUI IN- TERNO TRASPARE UNA CHIAVE D'ORO."
1490 DO=17:RETURN
2000 IFI<5THEN2003
2001 GOSUB9100:PRINT"NON RIESCO A PORTARE  PIU' DI QUESTO !"
2002 GOTO100
2003 IFB$<>""THEN2006
2004 GOSUB9100:PRINTA$" COSA ??":GOTO100
2006 FOUND=0
2007 FORN=1TO12
2008 IF(O$(N)<>B$)OR(L(N)<>SET)OR(I>=5)THEN2015
2009 GOSUB9100:PRINT"OK.."
2010 FOUND=1:L(N)=0:I=I+1
2015 NEXTN
2020 IFFOUND=0THENGOSUB9100:PRINT"IMPOSSIBILE !"
2030 GOTO100
2500 IFB$<>""THEN2505
2501 GOSUB9100:INPUT"APRO COSA ";B$
2502 GOTO2500
2505 IFB$<>"LA PORTA"ORSET=6THEN2508
2506 GSUB9100:PRINT"QUALE PORTA ?"
2507 GOTO100
2508 IFB$<>"LA PORTA"ORUNDTHEN2511
2509 GOSUB9100:PRINT"E' BLOCCATA CON UN CA-TENACCIO !":GOTO100
2511 IFB$<>"LA PORTA"THEN2515
2512 GOSUB9100:PRINT"C'E' UNA SCALA CHE    SCENDE SOTTO."
2513 DO=9:GOTO100
2515 IFB$<>"IL BAULE"ORSET=7THEN2518
2516 GOSUB9100:PRINT"NON VEDO NESSUN BAULE.":GOTO100
2518 IFB$<>"IL BAULE"ORUNCTHEN2521
2519 GOSUB9100:PRINT"E' BLOCCATO CON UN CA-TENACCIO.":GOTO100
2521 IFB$<>"IL BAULE"THEN498
2522 GOSUB9100:PRINT"OK..":L(11)=SET:GOTO100
3000 IFB$=""THENGOSUB9100:INPUT"APRO COSA ";B$:GOTO3000
3005 IFB$<>"LA PORTA"OR(SET=6)THEN3008
3006 GOSUB9100:PRINT"QUALE PORTA ?":GOTO100
3008 IFL(7)THEN498
3010 IFB$<>"LA PORTA"ANDB$<>"IL CATENACCIO"ORSET<>6THEN3015
3011 UND=1
3012 GOSUB9100:PRINT"CLICK! LA SERRATURA E'SCATTATA!":GOTO100
3015 IFB$<>"IL BAULE"ORSET=7THEN3020
3016 GOSUB9100:PRINT"QUALE BAULE ?":GOTO100
3020 IFB$<>"IL BAULE"ANDB$<>"IL CATENACCIO"ORSET<>7THEN498
3021 UNC=1
3022 GOSUB9100:PRINT"CLUNK! LA SERRATURA E'SCATTATA!"
3023 GOTO100
3500 IFB$<>""THEN3510
3501 GOSUB9100:INPUT"NUTRIRE CHI ";B$
3502 GOTO3500
3510 IFB$="IL CANE"THEN3520
3511 GOSUB9100:PRINT"STAI SCHERZANDO,SPERO!":GOTO498
3520 IFSET=10THEN3530
3521 GOSUB9100:PRINT"NON C'E' NESSUN CANE  QUI !":GOTO100
3530 IFL(12)=0THEN3540
3531 GOSUB9100:PRINT"NON HO CIBO PER CANI  CON ME !"
3532 GOTO100
3540 I=I-1:FED=1:UP=11
3543 GOSUB9100:PRINT"MUNCH,MUNCH!"
3544 L(12)=7
3545 GOTO100
3550 GOTO498
4000 IFSET=8ORSET=11ORSET=12THEN4004
4001 IFSET=13ORSET=15ORSET=16THEN4004
4002 GOSUB9100"PRINT"NON C'E' ACQUA !":GOTO100
4004 GOSUB9100:PRINT"L'ACQUA E' TROPPO FREDDA !":GOTO100
4500 FOUND=0
4501 FORN=1TO12
4502 IFO$(N)<>B$ORL(N)<>0THEN4507
4503 GOSUB9100:PRINT"OK.."
4504 L(N)=SET:FOUND=1:I=I-1
4507 IFB$<>""THEN4510
4508 GOSUB9100:PRINTA$" COSA ??"
4509 GOTO100
4510 NEXTN
4520 IFFOUNDTHEN4530
4521 GOSUB9100:PRINT"NON HO "B$" CON ME !"
4530 GOTO100
5000 UND=0:UNC=0
5020 RESTORE
5030 DATALA CORDA,13,LA PIETRA,13,LA SCALA,12,L'ANELLO,5,IL FUCILE,15,LA SPINA,16
5035 DATALA CHIAVE,1000,LA TORCIA,6,LE BATTERIE,11,IL TRONCO,11,IL MANOSCRITTO,1
5037 DATAUN OSSO,7
5040 FORN=1TO12
5041 READO$(N),L(N)
5042 NEXTN
5080 RETURN
5500 FOUND=0:TROV=0
5502 FORN=1TO12
5503 IFL(N)THEN5530
5505 IFFOUNDTHEN5520
5506 GOSUB9100:PRINT"HO LE SEGUENTI COSE..."
5507 FOUND=1
5520 PRINT"  "O$(N)
5521 TROV=1
5530 NEXTN
5540 IFFOUNDORTROVTHEN5550
5541 GOSUB9100:PRINT"NON HO NULLA CON  ME !"
5550 RETURN
6000 IFB$<>""THEN6010
6002 GOSUB9100:PRINTA$" COSA ??":INPUTB$
6003 GOTO 6000
6010 IF(B$<>"IL CRISTALLO")AND(B$<>"LO SCRIGNO")OR(SET<>18)THEN498
6020 IFL(2)=0THEN6030
6021 GOSUB9100:PRINT"NON PUOI DIRMI "A$" IL CRISTALLO SE NON HO NULLA PER FARLO!"
6022 GOTO100
6030 GOSUB9100:PRINT"SMASH ! TINKLE !"
6031 I=I-1:L(7)=SET:L(2)=SET
6034 GOTO100
6100 IFSET=10THEN6200
6105 IFSET<>17ANDUPTHENSET=UP:GOTO20
6110 IFL(3)=0ORSET<>17THEN6120
6111 GOSUB9100:PRINT"NON C'E' MANIERA DI    SALIRE !"
6112 GOTO100
6120 IFUP<>0THENSET=UP:GOTO20
6130 GOSUB9100:PRINT"NON POSSO SALIRE !"
6131 GOTO100
6200 IFFEDTHEN6210
6201 GOSUB9100:PRINT"C'E' IL CANE DI MEZZO!"
6202 GOTO100
6210 SET=11
6211 GOTO20
6220 GOTO498
6500 IFB$<>""THEN6510
6501 GOSUB9100:INPUT"LEGGO COSA ";B$
6502 GOTO6500
6510 IFB$<>"IL MANOSCRITTO"THEN498
6520 IFL(11)=0THEN6530
6521 GOSUB9100:PRINT"NON HO "B$
6522 GOTO100
6530 GOSUB9100:PRINT"DICE: PP3-TM ISOLA ?"
6531 GOTO100
7000 IFL(9)ORSET<>4THEN498
7002 PRINT"(CLR)(BLU)(DOWN)(DOWN)(DOWN)(DOWN)(DOWN)HAI RISOLTO CON PIENO (DOWN)SUCCESSO L'AVVENTURA."
7004 FORJ=130TO254:POKE36876,J:POKE36879,J:NEXT
7005 GOTO9000
7423 GOTO40
7500 IFB$<>""THEN7510
7501 GOSUB9100:INPUT"ACCENDO COSA ";B$
7502 GOTO7500
7510 IFL(8)THEN498
7520 IFSET<>9THEN7530
7521 NO=10
7522 L$="SEI IN UNA PICCOLA    STANZA. C'E' UNA SCALAA EST E UN CUNICOLO U-MIDO A NORD"
7525 GOTO40
7530 GOSUB9100:PRINT"NON HA GIOVATO MOLTO"
7531 GOTO100
8000 POKE36879,30:PRINT"(CLR)(DOWN)(DOWN)(RED)(RVS)LA MACCHINA DEL TEMPO "
8010 PRINT"(DOWN)(DOWN)(BLK)SEI BLOCCATO IN UN PASSATO OSCURO E LONTANO."
8020 PRINT"(BLU)CERCA LA VIA DEL RITORNO ALLA TUA CIVILTA'."
8030 PRINT"(DOWN)(DOWN)(RED)    PREMI UN TASTO(BLK)"
8040 GETZ$:IFZ$=""THEN8040
8400 Z$=""
8500 RETURN
9000 PRINT"(DOWN)(DOWN)(BLK)VUOI GIOCARE ANCORA   (S/N)":INPUTA$
9010 IFA$="S"THENRUN
9020 END
9100 FORJ=140TO250STEP2:POKE36875,J:POKE36876,390-J:NEXT
9110 POKE36875,0:POKE36876,0:RETURN

