0 REM *** LABYRINTH ***
1 REM ***    DI     ***
2 REM *** C. D'URSO ***
3 REM *****************
4 N=5:M=8:NF=N:MF=M:PRINT"(CLR)":DIMC(40,25),BX(300),BY(300)
5 X=N:Y=M:P=1:FL=0:POKE53280,0:POKE53281,0
6 INPUT"X - MAX";NN
7 INPUT"Y - MAX";MM
8 IFNN>38ORMM>23THEN6
9 REM *** FORMAZIONE BORDI ***
10 PRINT"(CLR)":FORR=1TONN
11 POKE1024+R+40,224
12 POKE1024+R+40*MM,224
13 C(R,1)=0:C(R,MM)=0
14 NEXTR
15 FORRR=1TOMM
16 POKE1025+40*RR,224
18 POKE1024+NN+40*RR,224
20 C(1,RR)=0:C(NN,RR)=0
22 NEXTRR
24 FORZ=2TONN-1:FORZZ=2TOMM-1
25 C(Z,ZZ)=1
26 NEXTZZ:NEXTZ
27 REM *** FORMAZIONE GRIGLIA ***
28 REM **************************
29 X=2:Y=2:CA=42
30 GETA$:FX=X:FY=Y
32 POKE1024+X+40*Y,CA
33 C(X,Y)=CA:IFCA=224THENC(X,Y)=0
34 IFCA=42THENC(X,Y)=1
35 IFA$="(UP)"THENGOSUB2000:Y=Y-1:GOSUB2050
36 IFA$="(DOWN)"THENGOSUB2000:Y=Y+1:GOSUB2050
37 IFA$="(RGHT)"THENGOSUB2000:X=X+1:GOSUB2050
38 IFA$="(LEFT)"THENGOSUB2000:X=X-1:GOSUB2050
39 IFA$=" "THENCA=224
40 IFA$="B"THENCA=2
41 IFA$="(F1)"THEN47
42 GOTO30
44 REM ****************************
45 REM *** PROGRAMMA DI RICERCA ***
46 REM ****************************
47 TI$="000000"
50 N=X:M=Y:IFC(X,Y)=2THENPRINT"HO TROVATO":END
55 GOSUB500
56 PRINT"(HOME)"TAB(15)"(RVS)TIME(OFF) "LEFT$(TI$,2)" "MID$(TI$,3,2)" "RIGHT$(TI$,2)
60 IFC(X,Y-1)=1ORC(X,Y-1)=2THENNO=1:GOTO62
61 NO=0
62 IFC(X,Y+1)=1ORC(X,Y+1)=2THENS=1:GOTO65
63 S=0
65 IFC(X-1,Y)=1ORC(X-1,Y)=2THENW=1:GOTO67
66 W=0
67 IFC(X+1,Y)=1ORC(X+1,Y)=2THENE=1:GOTO69
68 E=0
69 BI=0
70 IFNO=1AND(E=1ORS=1ORW=1)THENBI=1
72 IFS=1AND(NO=1ORE=1ORW=1)THENBI=1
74 IFW=1AND(NO=1ORE=1ORS=1)THENBI=1
76 IFE=1AND(NO=1ORW=1ORS=1)THENBI=1
78 IFBI=1THENBX(P)=X:BY(P)=Y:P=P+1
80 C(X,Y)=4
82 IFC(X,Y-1)=1ORC(X,Y-1)=2THENY=Y-1:GOSUB1000:GOTO50
84 IFC(X,Y+1)=1ORC(X,Y+1)=2THENY=Y+1:GOSUB1000:GOTO50
86 IFC(X-1,Y)=1ORC(X-1,Y)=2THENX=X-1:GOSUB1000:GOTO50
88 IFC(X+1,Y)=1ORC(X+1,Y)=2THENX=X+1:GOSUB1000:GOTO50
90 IFP-1>0THENP=P-1:X=BX(P):Y=BY(P)
92 GOSUB1000:GOSUB500:N=X:M=Y:IFC(X,Y)=2THENPRINT"HO TROVATO":END
105 GOTO50
498 REM *** VISUALIZZAZIONE UNITA' ***
499 REM ******************************
500 POKE1024+X+40*Y,62:FORU=0TO100:POKE1024+X+40*Y,96:C(X,Y)=4:RETURN
998 REM *** CONTROLLO COORDINATE ***
999 REM ****************************
1000 IFX<1ORX>NNTHENX=N
1010 IFY<1ORY>MMTHENY=M
1020 RETURN
1998 REM *** MOVIMENTO CURSORE ***
1999 REM *************************
2000 IFCA<>42THENRETURN
2010 POKE1024+X+40*Y,32
2020 RETURN
2050 L=0:IFX>NN-1ORX<2THENX=FX:L=1
2060 IFY>MM-1ORY<2THENY=FY:L=1
2065 IFL=0THENCA=42
2070 RETURN

