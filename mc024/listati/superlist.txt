10 REM SUPERLIST 1.0
20 REM -------------
30 REM (@) GIORGIO RUTIGLIANO
40 REM :
50 DIM CHS$(255)
60 OPEN 15,8,15:OPEN4,4
70 DEF FNI(I)=ASC(A$+CHR$(0))
80 DATA END,FOR,NEXT,DATA,INPUT#,INPUT
85 DATA DIM,READ,LET,GOTO,RUN,IF,RESTORE
90 DATA GOSUB,RETURN,REM,STOP,ON,WAIT,LOAD
95 DATA SAVE,VERIFY,DEF,POKE,PRINT#
100 DATA PRINT,CONT,LIST,CLR,CMD,SYS,OPEN
105 DATA CLOSE,GET,NEW,TAB(,TO,FN,SPC(
110 DATA THEN,NOT,STEP,+,-,*,/,^,AND,OR
115 DATA >,=,<,SGN,INT,ABS,USR,FRE,POS
120 DATA SQR,RND,LOG,EXP,COS,SIN,TAN,ATN
125 DATA PEEK,LEN,STR$,VAL,ASC,CHR$,LEFT$
130 DATA RIGHT$,MID$
140 PRINTTAB(7)"(CBM-A)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-S)"
150 PRINTTAB(7)"(SHIFT--) PROGRAM LISTER 1.0 (SHIFT--)"
160 PRINTTAB(7)"(CBM-Z)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(SHIFT-*)(CBM-X)(DOWN)"
170 FORI=0TO127:CHS$(I)=CHR$(I):NEXT
180 FORI=128TO202:READCHS$(I):NEXT
190 FORI=203TO255:CHS$(I)="~":NEXT
200 PRINT"NOME DEL PROGRAMMA";:INPUT PR$
210 OPEN1,8,0,PR$
220 INPUT#15,D,D$
230 IF D<>0 THEN PRINTD$:CLOSE1:GOTO200
240 PRINT"DATA LISTING (GG/MM/AA)";:INPUT DA$
250 IF LEN(DA$)=0THENDA$="08/07/83"
260 IF LEN(DA$)<>8 THEN 240
270 GOSUB 440:FL=PA:DE=0:LN=99
280 GOSUB440:IL=FL:FL=PA
290 O$="":TP=0:TS=0:IFFL=0THEN420
300 GOSUB440:NL$=MID$(STR$(PA),2):FV=0
310 IFLEN(NL$)<5THENNL$=" "+NL$:GOTO310
320 FORI=IL+5TOFL:GET#1,A$:A=FNI(0)
330 IFA$=":"ANDFV=0THENGOSUB470:GOTO410
340 P$=CHS$(A):IFFVTHENP$=CHR$(A)
350 IFA=129THENFD=FD+2
360 IFA=130THENDE=DE-2:IFDE<1THENDE=0
370 O$=O$+P$
380 IFA=167THENTP=LEN(O$)+1
390 IFFNI(0)=34THENFV=NOT(FV)
400 IFFNI(0)=143THENFV=99
410 NEXT:GOSUB470:GOTO280
420 PRINT"FINE DEL PROGRAMMA":CLOSE1
430 END
440 REM PRENDI UN NUMERO < 65535
450 GET#1,A$:PA=FNI(0):GET#1,A$
460 PA=PA+FNI(0)*256:RETURN
470 REM STAMPA LA ?A
480 IF LN<60 THEN 550
490 IFLN<66THENFORK=0TO6:PRINT#4:NEXT
500 PRINT#4,"S.LIST 1.0 - PROGRAMMA="+PR$;
510 PRINT#4,CHR$(16)"60"DA$+" - PAG:";
520 PG=PG+1:PG$=MID$(STR$(PG),2)
530 IFLEN(PG$)<4THENPG$="0"+PG$:GOTO530
540 PRINT#4,PG$:PRINT#4:PRINT#4:LN=4
550 PRINTNL$;SPC(1+DE+TS)O$
560 PRINT#4,NL$;SPC(1+DE+TS)O$
570 DE=DE+FD:FD=0:TS=TP
580 LN=LN+1:NL$="     ":O$="":RETURN

