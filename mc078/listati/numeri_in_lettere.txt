10 PRINT"(CLR)(WHT)(DOWN)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(SWLC)CONVERSIONE"
20 PRINT"(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)DI NUMERI INTERI <10^15"
30 PRINT"(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)IN LETTERE"
40 PRINT"(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)PER C-128"
50 PRINT"(WHT)(DOWN)(DOWN)(DOWN)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)DI V. CALAMIA"
60 SLEEP3
70 CLR:PRINT"(CLR)":DIMU$(40)
80 FORI=1TO40:READU$(I):NEXT
90 FORJ=1TO10:READM$(J):NEXT
100 NL$="":CF$="":INPUT"(CLR)NUMERO INTERO";N$:N=VAL(N$):X=LEN(N$):GOTO140
110 PRINT"(YEL)(DOWN)(DOWN)(DOWN)(DOWN)ALTRO NUMERO? (S=1/N=0)"
120 GETKEYT$:IFT$="S"ORT$="1"THEN100
130 IFT$="N"ORT$="0" THENEND:ELSEGOTO120
140 IF N<1E15THEN180:ELSEBEGIN
150 PRINT"(CLR)(DOWN)(DOWN)(DOWN)":FORL=1TO3
160 PRINT"(UP)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)                          ":SLEEP.4
165 SOUND1,8000,10
170 PRINT"(UP)(BLK)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)(RGHT)***NUMERO TROPPO GRANDE***":SLEEP1:NEXT:BEND:GOTO110
180 PRINT"(DOWN)(WHT)":IF N<>0THEN190:ELSEPRINT"(DOWN)ZERO"
190 FORI=1TO5
200 DD=0:Y=3*I:J=I+5:CM$=RIGHT$(N$,3):GOSUB270
210 IFVAL(CM$)=0THENNL$=CF$+NL$:GOTO240
220 IFVAL(CM$)=1THENNL$=CF$+M$(J)+NL$:GOTO240
230 NL$=CF$+M$(I)+NL$
240 IFX-Y>0THEN250:ELSEPRINTNL$:GOTO110
250 CS$=LEFT$(N$,X-Y):N$=CS$
260 IFVAL(CS$)<>1THENNEXT:ELSEPRINTM$(J+1)+NL$:GOTO110
270 CM=VAL(CM$):IFCM=1ANDY>=6THENCF$="":RETURN
280 IFCM>19THEN290:ELSECF$=U$(CM):RETURN
290 IFCM>99THEN300:ELSEGOTO320
300 CC$=LEFT$(CM$,1):CC=VAL(CC$)
310 IFCC=1THENCL$="":ELSECL$=U$(CC)
320 DU$=RIGHT$(CM$,2):DU=VAL(DU$)
330 IFDU>19THEN340:ELSECF$=U$(DU):GOTO390
340 DD$=LEFT$(DU$,1):DD=VAL(DD$)
350 UU$=RIGHT$(DU$,1):UU=VAL(UU$)
360 IFUU=1ORUU=8THENDL$=U$(DD+30):ELSEDL$=U$(DD+20)
370 IFUU=0THENUL$="":ELSEUL$=U$(UU)
380 CF$=DL$+UL$
390 IFCM>99THEN400:ELSERETURN
400 IFDD=8THENCC$="CENT":ELSECC$="CENTO"
410 CF$=CL$+CC$+CF$:RETURN
420 DATA UNO,DUE,TRE,QUATTRO,CINQUE,SEI,SETTE,OTTO,NOVE,DIECI,UNDICI,DODICI
430 DATA TREDICI,QUATTORDICI,QUINDICI,SEDICI,DICIASSETTE,DICIOTTO,DICIANNOVE,
440 DATA ,VENTI,TRENTA,QUARANTA,CINQUANTA,SESSANTA,SETTANTA,OTTANTA,NOVANTA,
450 DATA ,VENT,TRENT,QUARANT,CINQUANT,SESSANT,SETTANT,OTTANT,NOVANT,
460 DATA ,MILA,MILIONI,MILIARDI,BILIONI,,MILLE,UNMILIONE,UNMILIARDO,UNBILIONE

